!% -~S
!% -v5
!% -r
Constant Story "Internal Vigilance";
Constant Headline "^^By Simon Christiansen^^
			Version 1. Competition release.^^
			Type ~HINTS~ or ~HELP~ for instructions and hints, CREDITS for credits.^
			First time players should read the General Advice in the hints.^^";
Constant MANUAL_PRONOUNS;           ! Optional, but useful
Constant NO_SCORE;

Global 	act = 1;
Global	actiioutcome = 0;
Global  wake_up = 1;
Global	freedom = 2;
Global	freedomnotwings = 3;
Global	comply = 4;
Global	complynotwings = 5;
Global	hopelessfight = 6;
Global	refuse = 7;
Global 	rebels_defeated = false;

Replace MakeMatch;              ! required by pname.h
Replace Identical;
Replace NounDomain;
Replace TryGivenObject;
Replace DrawStatusLine;
!Replace ScoreSub;

Include "Parser";
Include "Shuffle";
Include "pname"; ! helps with disambiguation

Object  LibraryMessages         ! must be defined between Parser and VerbLib
  with  before [;
  		Wake:  	if (act == 2) {print "With great effort, you manage to force yourself to open 
  				your eyes and awaken.^"; actiioutcome = wake_up; actiii(); rtrue;}
  	    	Jump:	if (act == 2) {print "Why?"; if (wings in icarus) "You have wings!"; else rtrue;} 
  	    		else "You do some jumping exercises to stay in shape.";
            	Pray:   "You don't need God to help you. That's what the government is for.";
            	Sing:   "Your Agency training did not include singing.";
            	Sleep:  if (act == 2) "You suspect that you are already asleep."; 
            		else "You don't have time to sleep right now. There is work to do.";
            	Strong: "You are a highly trained and disciplined agent. You do not
            		use such language without reason.";
            	Ask: "You don't believe that question will get you anywhere at the moment.";
            	Tell: 	if (act == 2) "You prefer to keep the information to yourself.";
            		else "The Agency operates on a need to know basis. You don't share information
            		if it isn't strictly necessary.";
            	Yes, No, Answer: "You see no reason to say that.";
            	Miscellany: if (lm_n == 30) {"That is either not here, or doesn't need to be referred to.";}
            	Search: if (lm_n == 2) "You don't find anything new.";
            		if (lm_n == 6) "You see nothing worth taking.";
        ];

Include "VerbLib";
Include "Menus";

Include "SmartCantGo";              ! Optional, demo of using a Library package

!------------------------------------------------------------------------------!
!Entry point routines

[ DrawStatusLine width posa posb;
    #Ifdef TARGET_GLULX;
    ! If we have no status window, we must not try to redraw it.
    if (gg_statuswin == 0)
        return;
    #Endif;

    ! If there is no player location, we shouldn't try to draw status window
    if (location == nothing || parent(player) == nothing)
        return;

    StatusLineHeight(gg_statuswin_size);
    MoveCursor(1, 1);

    width = ScreenWidth();
    !posa = width-26;
    posa = width-60;
    posb = width-13;
    spaces width;

    MoveCursor(1, 2);
!    if (location == thedark) {
!        print (name) location;
!    }
!    else {
!        FindVisibilityLevels();
!        if (visibility_ceiling == location)
!            print (name) location;
!        else
!            print (The) visibility_ceiling;
!    }

    if (sys_statusline_flag && width > 53) {
        MoveCursor(1, posa);
        if (act == 1) print "Act I: Interrogation";
        if (act == 2) print "Act II: Flight";
        if (act == 3) print "Act III: Investigation";
        if (act == 4) print "Act IV: Conclusion";
        !print (string) TIME__TX;
        !LanguageTimeOfDay(sline1, sline2);
    }
    else {
        if (width > 66) {
            #Ifndef NO_SCORE;
            MoveCursor(1, posa);
            print (string) SCORE__TX, sline1;
            #Endif;
            MoveCursor(1, posb);
            print (string) MOVES__TX, sline2;
        }
        #Ifndef NO_SCORE;
        if (width > 53 && width <= 66) {
            MoveCursor(1, posb);
            print sline1, "/", sline2;
        }
        #Endif;
    }

    MainWindow(); ! set_window
];

[ Initialise;
	SetTime(60*21 + 00,0);
	Titlepage();
	lookmode = 2;
	location = office_chair;
	move gun to player;
	move badge to player;
	move sunglasses to player;
	move suit to player; give suit worn;
	move tie to player; give tie worn;
!	move notebook to player;
	player.description = "You are wearing a black suit and a tie. Also,
	working out regularly has ensured that
	you are as good looking as ever.";
	"
	^From: williams@@64agency.gov^
	To: dinnings@@64agency.gov^
	Subject: Assignment^^
	Dinnings^^I have assigned you to the case of prisoner no. 6, who
	was brought in yesterday. The details are too sensitive to be relayed via
	mail, but I have put the file on your desk. Good luck.^^-Williams^
	Agency supervisor.^^";
];

[ Titlepage i;
	box 	"The price of freedom is eternal vigilance."
		""
		"-Thomas Jefferson";
	@read_char 1 -> i; @erase_window -1;
	print "^^";
	box 	"Internal Vigilance"
		""
		"Act I: Interrogation";
	@read_char 1 -> i; @erase_window -1;
];

[ actii i;
	act = 2;
	print "(Press a key to continue)";
	@read_char 1 -> i; @erase_window -1;
	box 	"Far better to dare mighty things, to win glorious triumphs,"
		"even though checkered by failure, than to take rank with those"
		"poor souls who neither enjoy much, nor suffer much, because they"
		"live in the grey twilight that knows neither victory nor defeat."
			""
		"-Franklin Roosevelt";
	@read_char 1 -> i; @erase_window -1;
	box	"Act II: Flight";
	@read_char 1 -> i; @erase_window -1;
	ChangePlayer(icarus);
	<<Look>>;
];

[ actiii i;
	act = 3;
	print "^(Press a key to continue)";
	@read_char 1 -> i; @erase_window -1;
	box 	"Of all tyrannies a tyranny exercised for the good of its victims"
		"may be the most oppressive. It may be better to live under robber"
		"barons than under omnipotent moral busybodies. The robber baron's"
		"cruelty may sometimes sleep, his cupidity may at some point be" 
		"satiated, but those who torment us for our own good"
		"will torment us without end for they do so with the" 
		"approval of their own conscience."
			""
		"-C.S. Lewis";
	@read_char 1 -> i; @erase_window -1;
	box	"Act III: Investigation";
	@read_char 1 -> i; @erase_window -1;
	StopDaemon(person);
	StopDaemon(people);
	StopDaemon(basement_people);
	ChangePlayer(SelfObj);
	move notebook to player;
	PlayerTo(bed);
];

[ activ i;
	act = 4;
	StopDaemon(adam_wayne);
	print "(Press a key to continue)";
	@read_char 1 -> i; @erase_window -1;
	box 	"Every happy society is alike, but every unhappy society is unhappy in its own way."
			""
		"-The Writer, inspired by Tolstoy";
	@read_char 1 -> i; @erase_window -1;
	box	"Act IV: Conclusion";
	@read_char 1 -> i; @erase_window -1;
	PlayerTo(union_headquarters);
];

[ DeathMessage;
    if (deadflag == 3) print "You have accomplished nothing.";
    if (deadflag == 4) print "You have refused to negotiate with terrorists.";
    if (deadflag == 5) print "You have died, but you have accomplished something.
    				A pity that you will never know what it is.";
    if (deadflag == 6) print "You have accomplished something."; 
];

[ ChooseObjects obj code;
if (code < 2) {if (obj has scenery or static or animate) return 2; else rfalse;}

];

Attribute legible;

statusline time;

!------------------------------------------------------------------------------!
!Object Classes

Class   Room
  with  description "A bare and featureless room.",
  has   light;

Class   Furniture
  with  before [;
          Take,Pull,Push,PushDir:
            print_ret (The) self, " is too heavy for that.";
        ],
  has   static supporter scenery;

!Class Props
!  with before [; Examine: return false;
!		default: print_ret "You don't need to mess with", (the) self, ".";],
!  has scenery;

Class Normal_door
  with  
  	react_before [; 
  			Go: 	if (noun.door_dir() == self.door_dir())
  				return self.open_by_myself();
  			Enter: if (noun == self) return self.open_by_myself();
  			],
  	open_by_myself [ks ; if (self has open) rfalse;
  			print "(First opening ", (the) self, ")^";
  			ks = keep_silent; keep_silent=true;
  			<open self>; keep_silent = ks;
  			if (self hasnt open) rtrue;],
  	after [; Examine: if (self has open) print (The) self, " is open.^";
  				else print (The) self, " is closed.^";],
  has	door openable;

!------------------------------------------------------------------------------!
!The Game Objects


!Act I: Interrogation

Menu hints_menu "Hints for Internal Vigilance";
	Menu -> "General advice";
		Option -> -> "Conversation"
			with description [; <<Talk>>;];
		Option -> -> "Conflict"
			with description "In this game, ATTACK and HIT are separate commands. ATTACK <PERSON>, or one of its
			synonyms (like SHOOT or KILL), will try to kill the person, while HIT will merely attempt to 
			beat him up.";
		Option -> -> "Examining things"
			with description "It should be remembered that EXAMINE only tells you what you see
			with a cursory glance. To find hidden objects, you will need
			to use SEARCH, or LOOK UNDER.";
		Option -> -> "Changes in Version 2.0"
			with description "-Numerous errors in grammar, spelling and punctuation has been corrected.^^
					-The Mothers apartment number is now consistent throughout the game.^^
					-The button that opens the door in the mothers apartment is now
					mentioned in the room description.";
	Menu -> "Act I:   Interrogation";
		Option -> -> "I can't find the file!"
		  with	description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  			(1/2)^It's supposed to be on your desk.^^";
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h')
		  				print "(2/2)^SEARCH DESK^^";
		  			else return 2;
		  			];
		Option -> -> "How do I read the file?"
		  with	description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  			(1/2)^It's a cd, so you might want to use your computer.^^";
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h')
		  				print "(2/2)^Ensure that the computer is on,
		  					then simply put the cd in the cd drive.
		  					You can then type ~read file~.^^";
		  			else return 2;
		  			];
		Option -> -> "How do I make the prisoner talk?"
		  with	description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  			(1/6)^Well, you can start by asking him some questions. Try to experiment
		  				with some different topics.^^";
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(2/6)^If he won't cooperate, a good beating might change his mind!^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h')
		  				print "(3/6)^That didn't work very well. Maybe there is something he cares
		  					about, which can be used against him?^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(4/6)^Have you studied the file extensively? He visits his
		  				mother regularly.^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(5/6)^Ask him about his mother. That should make him more cooperative.
		  				You will need to have read the relevant file entry first, though.^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(6/6)^Once he is willing to cooperate, you can ask him about
		  				his terrorist connections.^^";
		  			else return 2;];
		Option -> -> "What do I do with the information I got from the prisoner?"
		  with	description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  			(1/3)^Just inform your supervisor.^^";
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h')
		  				print "(2/3)^You will need to have acquired the phone
		  					number from the prisoner first.^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h')
		  				print "(3/3)^Then you can just TELL your supervisor
		  				about it, or show him your notebook.^^";
		  			else return 2;
		  			];
	Menu -> "Act II:  Flight";
		Option -> -> "How do I get out of the cell?"
		  with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  			(1/6)^Just follow the nice man who comes to get you.^^";
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(2/6)^Maybe you don't want to follow him. Attack him instead!^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(3/6)^He looks tough. Perhaps you can find something to use as a weapon?^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(4/6)^The cell is very bare. Is there any place where something could conceivably be hidden?^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(5/6)^Maybe under the cot?^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(6/6)^LOOK UNDER COT^^";
		  			else return 2;
							];
		Option -> -> "What can I do about the men in the meeting room?"
		  with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  			(1/3)^For starters, you can answer their question. The
		  				answer is up to you.^^";
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(2/3)^If you say yes, you can converse with them afterwards.^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(3/3)^Did you find the knife earlier? If so you might try to attack them^^";
		  			else return 2;
							];
	Menu -> "Act III: Investigation";
		Option -> -> "What am I supposed to do?"
		  with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  			(1/3)^Try reading your notebook.^^";
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(2/3)^You can use the phone to call the phone number you got earlier,
		  				as well as the prisoner's family members.^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(3/3)^If you get stuck, you can also call Agency and hear
		  				if they have any advice.^^";
		  			else return 2;
							];
		Menu -> -> "At the sister's house";
			Option -> -> -> "What to do in the living room?"
		  		with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  				(1/5)^You can't leave until you have talked to Allyson. You will
		  					have to wait for her. Use the time to examine your surroundings.
		  					When she arrives, you can ask her about anything you want.^^";
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(2/5)^She really wants you to drink that tea, doesn't she?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(3/5)^If you don't want to drink it, you will have to distract
		  					her somehow.^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(4/5)^Throw your own tea at her! That should give you a few
		  					seconds to act, before she recovers.^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(5/5)^When she is distracted, you can either shoot her, or try
		  					to seize her gun. It's your choice.^^";
		  				else return 2;
							];
			Option -> -> -> "What to do in the kitchen?"
		  		with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  				(1/2)^The only important thing to do here is to find the pills in the second cabinet.^^";
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(2/2)^If you like, you can experiment with them by dissolving them
		  					in the teapot or in a glass of water.^^";
		  				else return 2;
							];
			Option -> -> -> "What to do in the bedroom?"
		  		with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  				(1/5)^You can always pick up the book from the table. It might be an important clue.^^";
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(2/5)^Might there be some other books of importance in the room?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(3/5)^Maybe in the bookshelf?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(4/5)^But you can't make out the titles? Is there any way
		  						you could get more light?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(5/5)^You can always open the curtains.^^";
		  				else return 2;
							];
		Menu -> -> "At the mother's apartment";
			Option -> -> -> "I can't enter the building!"
		  		with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  				(1/6)^The door can only be opened from the inside.^^";
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(2/6)^You will have to use the keypad to call her apartment.^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(3/6)^She doesn't answer? That's inconvenient. Try calling someone
		  					else. They might be willing to let you in.^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(4/6)^Is there any you can prove that you are really an agent?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(5/6)^Your badge, maybe?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(6/6)^Show the badge to the camera.^^";
		  				else return 2;
							];
			Option -> -> -> "I can't get into the apartment proper!"
		  		with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  				(1/4)^Have you examined the door? It seems that you will need a keycard.^^";
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(2/4)^You haven't been able to contact the mother.
		  						Who else might have a key to her apartment?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(3/4)^Her daughter, maybe?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(4/4)^If you killed Allyson, you can find the keycard
		  						by searching her body. Otherwise, you won't be able to enter
		  						in this playthrough. Don't worry, the game can be completed in several
		  						different ways.^^";
		  				else return 2;
							];
			Option -> -> -> "What to do in the apartment?"
		  		with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  				(1/3)^Make sure that you examine everything thoroughly. That might reveal some clues.^^";
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(2/3)^Search any object that could contain something hidden.^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(3/3)^Like the bookshelf.^^";
		  				else return 2;
							];
			Option -> -> -> "Ok, i've completed the game. What the hell happened at the mother's apartment?"
		  		with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  				(1/2)^Did you find the letter and show it to Adam?^^";
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(2/2)^Doing that gives you a bit of exposition, and tells
		  					you something about the plans of the terrorists. Other than that,
		  					the place is basically a red herring.^^";
		  				else return 2;
							];
		Menu -> -> "At the warehouse";
			Option -> -> -> "I can't open the door"
		  		with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  				(1/9)^The door can only be opened from the inside.^^";
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(2/9)^Have you tried to say something to the speaker?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(3/9)^There is definitely someone in there, but he doesn't want to talk.
		  						Maybe he is expecting a passphrase of some kind?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(4/9)^Try examining the speaker.^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(5/9)^It seems like some kind of code. Maybe Agency can help you decipher it?
		  						Try calling them.^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(6/9)^So it's a book code. Have you found any books that might contain the key?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(7/9)^Maybe Allyson's novel! Try to look up the three codes in the book.
		  						Like ~look up 26-5-9 in book~. Say the resulting words to the speaker^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(8/9)^That didn't seem to work. Maybe some other book?^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(9/9)^Like ~Internal Vigilance~, which can be found at Allyson's house.^^";
		  				else return 2;
							];
			Option -> -> -> "What can I do inside the warehouse?"
		  		with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  				(1/5)^You can always just wait, and see what he has to say. Maybe drink some tea.^^";
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(2/5)^He mentions that they have blocked the actions which might
		  						hurt them. However, if they have only blocked specific actions,
		  						you might still be able to do things they don't expect.^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(3/5)^If you got the pills, you might try to drug the tea, when
		  						he isn't looking.^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(4/5)^Or maybe you can regain control somehow. He seems to consider the
		  						computers important...^^";
		  				else return 2;
		  				@read_char 1 -> i;
		  				if (i=='H' or 'h') 
		  					print "(5/5)^Shoot the computers. Then do whatever you like.^^";
		  				else return 2;
							];
	Menu -> "Act IV:  Conclusion";
		Option -> -> "What should I do at The Tower."
		  with description [; print "Press ENTER to return to the menu, or H for more hints.^^
		  			(1/1)^Just explore the area. Then talk to The Overseer, when you have found him.^^";
							];
		Option -> -> "I have the bomb. What now?"
		  with description [i; print "Press ENTER to return to the menu, or H for more hints.^^
		  			(1/5)^You can always blow yourself up, along with The Overseer.^^";
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(2/5)^If you don't want to die, you can leave it in
		  				the hallway, lie to The Overseer, and then leave.^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(3/5)^If you are still loyal to The Union, you can tell The Overseer
		  				about the bomb, or show it to him^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(4/5)^Ouch! The briefcase must be bugged or something. 
		  					Try leaving it in the hallway before you tell The Overseer about it.^^";
		  			else return 2;
		  			@read_char 1 -> i;
		  			if (i=='H' or 'h') 
		  				print "(5/5)^Other than that, just talk to The Overseer. If you can't decide
		  					whether or not you want to blow him up, maybe you should get to know him first?^^";
		  			else return 2;
							];
!	Menu -> "The different endings";
!		Option -> -> "How to reach all of the endings. Only read this if you have finished the game at least once!
!				Preferably, more than once."
!		  with description [; print "Press ENTER to return to the menu, or H for more hints.^^
!		  			(1/1)^~Internal Vigilance~ has a grand total of nine different conclusions.
!		  				Futhermore, there are about a handful of ~you screw up and die~ type
!		  				of endings.^^
!		  				For convenience the different conclusions can be divided into Loyalist
!		  				and Rebellious, depending on who you choose to support.^
!		  				The rebellious endings are reached by joining the terrorists
!		  				in the warehouse. To get the offer to join them, you will have to have
!		  				reached one of the rebellious endings in the Act II dream sequence.
!		  				What the terrorists ask you to do, depends on what you did in Act II.
!		  				To reach the first rebellious ending, leave the briefcase in the hallway,
!		  				lie to The Overseer, and then leave.^^";
!							];

Object 	gun "gun"
  with  pname '.x' 'my' '.x' 'your' '.x' 'revolver' '.x' 'pistol' '.x' 'weapon' 'gun',
  	has_gone false,
	description "Your trusty service pistol. It is loaded and ready for action.",
	before [;	Examine: return false;
			Draw, Use: "If you want to use your gun, just type SHOOT <NOUN>.";
			default: "You don't want to mess with your trusty gun. Being a field agent
			is a dangerous vocation.";];

Object 	badge "badge"
  with  name 'badge' 'id',
	description "This badge identifies you as Agent Dinnings of the Agency. Above
	your picture is the Agency motto: ~To Observe and Protect.~",
	before [; 	Examine: return false; Show: return false;
			default: "You don't want to mess with your badge. You wouldn't be able
			to identify yourself without it.";],
  has	transparent;
			
Object 	badge_picture "picture" badge
  with	name 'picture',
  	description "Your picture is a bit old, but it works for identification purposes.";
  	
Object 	sunglasses "pair of sunglasses"
  with	name 'sun' 'glasses' 'sunglasses',
  	description "Cool dark sunglasses. They make agents look more intimidating.",
  	after [; Wear: "Everything gets darker.^You get cooler.";],
  has	clothing;
			
Object 	suit "suit"
  with 	name 'suit' 'clothing' 'clothes',
  	description "You are wearing a standard dark suit, issued by the Agency.",
  	before [; Examine: rfalse;
  		default: "You don't want to mess with your suit. It's basically uniform of The Agency.";],
  has	clothing;
  
Object	tie "tie"
  with	name 'tie',
  	description "Your tie is dark with blue stripes.",
  	before [; Examine: rfalse;
  			default: "Why would you want to mess with your tie? It took
  			you forever to get the knot just right.";],
  has	clothing;
			
!------------------------------------------------------------------------------------
Room    office "Your Office"
  with	description [;print "Your office is a small austere room with a bare floor and a desk at the south wall, 
	covered with piles of various work papers. "; 
	if (office_lamp has on) print "The office lamp provides soft white light. ";
	else print "With the lamp turned off, the office is pretty dark. ";
	print "At one side of the desk, your computer "; 
	if (office_computer has on) print "is humming softly, the familiar sound of the ventilation
	filling the office. ";
	else print "patiently awaits the time you will need it. ";
	if (player in office_chair) print "Currently, you are sitting behind your desk,
	on your old office chair. ";
	else print "Behind the desk, a forlorn old chair looks like it is about to fall apart. ";
	print "The office door is to the east, and there is a "; 
	if (office_window hasnt open) print "closed ";
	else print "open ";
	"window in the west wall.";],
	before [; Exit: if (player in self)
			<<Go e_obj>>;],
        e_to office_door;
        
Object	x_office "office" office
  with  pname 'office',
  	before [; default: "You will have to be more specific.";],
  has 	scenery;
  
Furniture office_floor "floor" office
  with	name 'floor' 'bare',
  	description "It is bare, boring and gray. You should get a carpet some day.
  	It would really spice up the office.";


Object office_window "window" office
  with  pname '.x' 'office' 'window',
  	description "The window gives you a nice view of the office building on the other side
  		of the street.",
  	before [; Search: <<Examine self>>;],
  	after [;Open: "You open the window, letting some fresh air into the office.";
  		Examine: if (self has open) print (The) self, " is open.^";
  				else print (The) self, " is closed.^";],
  has openable ~open scenery;
  
Object	office_building "building" office
  with	pname '.x' 'office' 'building',
  	description "The office building is a tall high rise tower. Most of the bureaucractic work
  			of The Agency is done from there.",
  has	scenery;
  
Object 	office_street "street" office
  with	pname 'street',
  	description "The street is almost empty of cars. It is getting late. Most people are probably
  			at home with their families by now.",
  has	scenery;

Furniture desk "desk" office
  with  pname '.x' 'office' 'writing' 'desk' 'table' 'papers' 'work' 'mess' 'piles',
        description 
		"The desk, covered with various papers, is a mess. It has a 
		blue desk lamp attached to the side. Your old office chair is standing behind it. 
		It also contains a drawer, which you use to store all kinds of junk.",
	before [; Rub: "You don't have time to do that right now. It will
			have to wait for a less busy day. You wonder if such a day will
			ever arrive.";
		Search: if (self.file_found==false) {
			print "^Among the mess you discover the file on the prisoner you are
			supposed to interrogate. The CD case seems to have been buried under a collapsed
			paper mountain.
			You quickly snatch it from the desk. Best
			not to let it get lost again. Perhaps you should clean your desk some day?
			If only you had the time.^";
			move file to player;
			self.file_found=true;
			return true;}],
	file_found false,
	after [; 
		!Examine: if (self.file_found==false) {
		!print "^Among the mess you discover the file on the prisoner you are
		!	supposed to interrogate. The CD case with the file seems to have been buried under a collapsed
		!	mountain of papers.
		!	You quickly snatch it from the desk. Best
		!	not to let it get lost again. Perhaps you should clean out your desk some day?
		!	If only you had the time.^";
		!	move file to player;
		!	self.file_found=true;
		!	return true;}

	];

Furniture office_lamp "lamp" office
  with	pname '.x' 'office' 'lamp' 'light',
  	description "An ordinary blue office lamp.",
  	!before 	[; SwitchOff: give office ~light;
  	!	SwitchOn: give office light;],
  has	switchable on;

Object file "file"
  with  name 'file' 'prisoner' 'CD' 'disk' 'case',
	description
		"This file contains all pertinent information on prisoner no. 6,
		whom you are supposed to interrogate. All of it is stored on CD.",
	has_read_mother false,
	has_read_sister false,
	before [word_number i; 
		Consult: if (file has legible) { 
			wn = consult_from;
			word_number = consult_words;
			for (i = 1: i <= word_number: i++) {
				switch(NextWord())  {
				'subject', 'prisoner', 'number', 'six': <<read file>>;
				'family', 'father' : 
					"^The subject's father died when he was a kid. Currently,
					his only relatives are his mother and sister.";
				'mother', 'mum', 'mom':
					self.has_read_mother = true;
					prisoner.ask_mother = 0;
					"^Name: Rebecca Stiles^Age: 61^Height: 171cm^Weight: 72kg^
					Eyes: Blue^Hair: Blond^Education: High School^Occupation: Retired.
					Used to work as a secretary.^^
					Rebecca Stiles lives alone in a small apartment in the center
					of The City. 
					Andrew visits her regularly. She has never done anything
					suspicious, so the file doesn't contain much information
					on her, apart from her address.";
				'sister', 'sibling', 'argument':
					self.has_read_sister = true;
					prisoner.ask_sister = 0;
					"^Name: Allyson Stiles^Age: 31^Height: 164cm^Weight: 52kg^
					Eyes: Blue^Hair: Brown^Education: Masters in Computer Science^
					Occupation: Programmer^^
					Allyson Stiles lives in a house in the suburbs. She currently
					works as a programmer for a large software company in The City. She doesn't
					have much contact with her brother. They had an argument several
					years back and have barely spoken to each other since then. Unfortunately,
					the file doesn't contain any information on what the argument was about.";
				'education', 'philosophy', 'studies', 'college', 'school':
					"^After graduating from high school the subject studied philosophy for
					two years at the City university. He got good grades, but, after two years,
					he dropped out abrubtly. Later, he published an article criticising 
					philosophers, and academics in general, for having become detached from
					real life and concerning themselves more with tenure and recognition than 
					with actually having a positive influence on society.";
				'money', 'employment':
					"The subject is currently unemployed, and is receiving welfare benefits from
					The Union. Luckily for him, actual poverty is a thing of the past.";
				'book', 'writings' , 'civil', 'rights':
					"^Approximately one week ago the subject published a book titled
					'Internal Vigilance', in which he argues that The Union has consistently
					shown a lack of regard for the rights of the individual and should
					therefore be dismantled, returning to the old system of independent nationstates.
					Although this would also bring back the constant conflict and instability,
					of which the period preceding Unification is notorious, the author
					believes this to be a reasonable sacrifice. Freedom is more important
					than safety. It is not hard to understand why he is supected
					of having terrorist connections.";
				'terrorism', 'terrorists', 'terror', 'connections', 'connection':
					"^There is no record of the subject ever having been involved in any kind
					of terrorist acts. Whether or not he has any connections to terrorists is
					what you are supposed to determine.";
						}!end switch
							}! end for
								}!end if
			else "You can't read a CD";
		Read: "^The file contains a ton of information on the prisoner. You
			quickly skim it to find the most important information.^^
			Name: Andrew Stiles^Age: 32^Height: 192cm^Weight: 65kg^Eyes: Blue^Hair: Blond^
			Education: High school graduate. Former philosophy student. Dropped out of college.
			^Occupation: Unemployed writer. On welfare.^^
			The file also contains several photos of the subject and his family.^^
			Stiles recently published a book lambasting The Union for not respecting
			the rights of the individual. Because of this, the Agency suspects that he might
			have connections with anti-Union terrorists. You are to determine whether
			or not this is the case.
			There is much more information here than you can possibly read right now.
			To gain more from it you will need to know what you are looking for 
			('READ ABOUT <TOPIC> IN FILE').";
		Take: give file ~legible; return false;],
	after [; Examine: if (file has legible) "^You can read it on the screen.^";],
	has transparent;

Object 	photos "photos" file
  with 	name 'photo' 'photos' 'pictures' 'picture',
  	description "Several photos of the prisoner, his sister, and his mother. 
  			You will be able to recognise them, should you meet them.";

Object 	office_drawer "desk drawer" desk
  with  pname 'drawer' '.x' 'desk',
	description
		"A small desk drawer."
  has container openable ~open scenery;
  
Object	notebook "notebook" office_drawer
  with	pname 'note' 'notebook' 'notes' '.x' 'book',
  	description "A small handy notebook.",
  	before [; 
  		Read: if (prisoner.has_information) 
  				print "It contains the phone number you got from the prisoner.";
  			else "You haven't made any useful notes yet.";
  			if (act==3) print "^^You have also made some notes about how to get started with
  					the investigation. It would probably be a good idea to 
  					talk to the subject's family. You have their addresses from the file,
  					so you just need to drive there. Also, the phone number needs to be
  					investigated.^
  					The notebook also contains the phone number for Agency, so you
  					can call them if you need help with anything.";
  			if (warehouse_speaker.code_found) "^^You have written down the numbers you found
  					by the speaker. They are: ^
  					~26-5-9.^ 17-8-3.^ 109-12-4.~";
  			"";],
  	after [; Take: "You pick up the notebook and make sure that it is up to date.";],
  has	legible;

Furniture office_chair "office chair" office
  with  pname 'chair' '.x' 'office',
	description "An old worn office chair. Nothing special about it.",
	after [; Enter: "The office chair creaks pitifully as it nobly supports your weight.";],
  has enterable;

Furniture office_computer "computer" office
  with 	pname 'computer' '.x' 'office' '.x' 'machine' '.x' 'drive' '.x' 'cd',
	description "Unlike everything else in your office,
	the computer is a sleek state of the art machine. 
	The Agency considers electronics to be of much higher
	importance than mere material comforts. Like comfortable office chairs.",
	before [;
		Use: "To use the computer, just put a cd in the drive. You will then be able to read
			the contents on the screen.";
		SwitchOff: give file ~legible;
		SwitchOn: if (file in office_computer) give file legible;
		Receive: if (noun == file && office_computer has on) {print "You put the CD into the computer. 
		A few moments later you have the file on screen. You can now read it at your leisure.^"; 
			give file legible;
			move file to office_computer; return true;}
			else {if (noun ~= file) "The computer can't read that!";
				else "The computer is off.";}		
		default: return false;],
  has switchable on container open ~supporter;
  
Object 	email "mail" office
  with	name 'mail' 'e-mail',
  	description [;if (office_computer has on) "From: williams@@64agency.gov^
	To: dinnings@@64agency.gov^
	Subject: Assignment^^
	Dinnings^^I have assigned you to the case of prisoner no. 6, who
	was brought in yesterday. The details are too sensitive to be relayed via
	mail, but I have put the file on your desk. Good luck.^^-Williams^
	Agency supervisor.";
	else "The computer is off.";],
  has	scenery;
  
Object 	office_screen "screen" office
  with	pname '.x' 'computer' 'screen',
  	description [; if (file in office_computer) <<Read file>>;
  			else <<Read email>>;],
  has	scenery;

Normal_door office_door "office door"
  with  pname '.x' 'east' '.x' 'office' 'door',
	description "On the door is a sign with your name on it. Other than
	that, it's a ordinary office door.",
	door_dir [;
	            if (location == office) return e_to;
	            else                  return w_to;
	        ],
	door_to [;
	            if (location == office) return hallway;
	            else                  return office;
        ],
	found_in office hallway,
  has 	scenery transparent door openable open;

Object office_sign "sign" office_door
  with  pname '.x' 'door' '.x' 'office' 'sign' 'plaque',
	description "The sign has your name on it, Agent Dinnings.",
  has scenery;
  
Object your_name "name" office_door
  with 	pname 'name',
  	description "You already know your name.",
  	before [; Examine: return false;
  			default: "That's pretty surreal.";],
  has scenery;


!------------------------------------------------------------------------------------
Room    hallway "Hallway"
  with  description "The hallway runs north and south. To the west is the door to your
		office. Just to the north of where you stand there is a door in the east wall
		leading to the office of your supervisor. Further to the north the hallway 
		terminates at a large metallic elevator door. The floor is covered by a thick
		red carpet, and light is provided by fluorescent tubes in the ceiling.",
	s_to "You don't have time to explore the building right now. You have a job to do.
		The interrogation room is in the basement.",
        w_to office_door,
	n_to n_hallway;
	
Object tubes "lamps"
  with	name 'lamps' 'tubes' 'fluorescent',
  	description "Fluorescent tubes filling the corridor with white light.",
  	found_in hallway n_hallway top_elevator bottom_elevator,
  has scenery pluralname;
	
Furniture h_carpet "carpet"
  with	name 'carpet' 'hallway',
  	description "A thick red carpet. You shouldn't waste your time staring at it.
  	It isn't quite right for your office anyway. You need something with a more
  	subdued color.",
  	found_in hallway n_hallway;


!------------------------------------------------------------------------------------
Room n_hallway "Hallway"
  with description "You are standing just west of the door to your supervisor's office.
		The elevator is just a bit further to the north.",
	s_to hallway,
	e_to s_door,
	n_to top_elevator;

Normal_door  s_door "office door"
  with  pname 'door' 'office',
	description "On the door is a sign with the name of Supervisor Williams on it. Other than
	that, it's a ordinary office door.",
	before [; Knock: "~Come in!~";],
	found_in s_office n_hallway,
	door_dir [;
	            if (location == s_office) return w_to;
	            else                  return e_to;
	        ],
	door_to [;
	            if (location == s_office) return n_hallway;
	            else                  return s_office;
        ],
  has 	scenery transparent door openable ~open;

Object 	s_sign "sign" s_door
  with 	pname '.x' 'door' 'sign' 'plaque' ,
	description "The sign has the name of Supervisor Williams on it.",
  has 	scenery;
  
Object 	s_name "name" s_door
  with	name 'name',
  	description "You already know your supervisor's name.",
  	before [; Examine: return false; default: "That's pretty surreal.";];


!------------------------------------------------------------------------------------
Room s_office "Supervisor's Office"
  with description "This is the office of Supervisor Williams. It is significantly
		larger than your own, with a nice soft carpet on the floor and
		a big desk at the opposite wall.",
	before [; Exit: <<Go w_obj>>;],
	w_to s_door;

Object "papers" s_office
  with  name 'paper' 'papers',
  	description "Some boring administrative stuff, no doubt.",
  has	static scenery;
  	
  	
Furniture s_desk "supervisor's desk" s_office
  with  name 'desk' 'table',
	description "Your supervisor's desk is much larger than your own. If you work hard
			you might get a fancy office of your own some day.";

Furniture s_carpet "carpet" s_office
  with  name 'carpet' 'floor',
	description "A soft red carpet. You wish your office had one, although you would probably
			choose a different color.";

Object supervisor "Supervisor Williams" s_office
  with 	name 'supervisor' 'williams' 'superior' 'william' 'fat' 'bastard' 'man',
	description "Although Williams was once a trained field agent like yourself, he is now
		a large overweight man. He has been spending too much time behind his desk.
		A bald spot has appeared on top of his head, and the remaining hair is turning gray.
		Time takes its toll on everyone. Still, he is as competent as ever, and this Agency division
		is running smoothly under his leadership. Each agent acts a a well-oiled cog in the larger whole.
		You have no doubt that he is doing a better job than you would in a similar position.
		Administration was never your thing.",
	has_answer_file false,
	has_ask_prisoner 0,
	has_ask_freedom 0,
	ask_file 0,
	ask_terror 0,
	ask_union 0,
	initial [; if (location hasnt visited) print "Supervisor Williams is sitting behind his desk, studying some papers, when
			you enter the room. He looks up and smiles warmly. ~Agent Dinnings!
			A pleasure to see you. Can I help you with anything?~";
		else print "Williams is sitting behind his desk, studying some papers.";
		if (self.has_answer_file == false) {
		print "^~Have you checked the file on the prisoner?~";
            }
            	"";
			],
	before [; Search: "Are you kidding? He is your superior.";],
	react_before [; Yes: if (supervisor.has_answer_file == false) {print "^~Yes~, you reply. ~I found it on my desk.~ ^^ 
			~Good. When you are done with it you should get on with
			the interrogation. The prisoner has been put in the interrogation room in the basement.~^";
			supervisor.has_answer_file = true; return true;}				
			No: if (supervisor.has_answer_file == false) {print "^~No, I couldn't find it anywhere.~^^
				~Really? I put it on your desk. It probably just got lost in your mess.~^";
				supervisor.has_answer_file = true; return true;}
			
			],
	Orders [; "He is your superior. You can't order him around.";],
	life [word_number i; Show, Give: switch(noun) {
			notebook: TellWilliams();
					rtrue;
			} ! end switch
		Tell: 	wn = consult_from;
			word_number = consult_words;
			for (i = 1: i <= word_number: i++) {
				switch(NextWord())  {
			'prisoner', 'stiles', 'andrew', 'author', 'terror', 'terrorist', 'connections', 'connection',
			'guy', 'phone', 'number', 'interrogation', 'notebook':
				TellWilliams();
				rtrue;}!end switch
			}!end for
		Attack: remove supervisor; move supervisor_body to location;
		print "You are tired of being ordered around by this fat pig. Before he has time to realise what is going on
		you have put a bullet in his head. He is dead before he hits the ground.^^In retrospect, that probably
		wasn't a very smart thing to do. After all, every room in Agency HQ has video surveillance. Soon after, you
		are sitting in a cell, awaiting your own interrogation....."; deadflag=3; rtrue;
		Answer: switch(noun) {
			'yes', 'sure', 'affirmative': <<yes>>;
			'no', 'nope', 'negative': <<no>>;
			'hello', 'hi', 'good':
				"~Good evening, Williams~, you say politely.^^
				~Good evening, Dinnings~, he replies with a smile.
				~Can I help you with anything?~";
			'goodbye', 'bye':
				"~Goodbye~^^
				~Good luck~, he replies curtly.";}!end switch
		Ask:	wn = consult_from;
			word_number = consult_words;
			for (i = 1: i <= word_number: i++) {
				switch(NextWord())   {
			'file', 'cd':
				switch(self.ask_file) {
					0: print "~What should I look for in the file?~^^
						He thinks for a moment. ~Well, it contains a lot
						of information on the prisoner and his family.
						It's your case. Just see what you can find.~";
					default: "Maybe you should just study it more.";} ! end switch
					self.ask_file++; "";
			'terrorism', 'terrorist', 'terror', 'terrorists', 'anti-union':
				switch(self.ask_terror){
					0: print "~What information do we have on the terrorist organisations that
						might be involved in my case?~^^
						~Not much, I am afraid. We are aware of several different
						anti-union terrorist groups, but we have no idea which, if any,
						he might be connected to. That's what you are supposed to investigate.~";
					1: print "~How much of a threat do you think the terrorists are?~^^
						He smiles. ~Oh, I don't think they are that dangerous anymore. 
						People have realized by now that opposing Unification is ridiculous.
						The terrorists don't have much support anymore.~";
					default: rfalse;}
					self.ask_terror++; "";
		!	'union', 'unification':
		!		switch(self.ask_union){
		!				0: "";} ! end switch
		!		self.ask_union++; "";
			'prisoner', 'stiles', 'andrew', 'author', 'writer', 'man', 'six', '6':
				switch (self.has_ask_prisoner){
			 	0: print "~What can you tell me about the prisoner?~^^
				~He is in the basement, in the interrogation room. 
				If you need any information on him
				you should consult the file.~";
				1: print "~Don't you know anything about him?~^^ He looks annoyed. 
				~I said consult the file. I don't know more than you do.~";
				default: "~Isn't there anything you can tell me about the prisoner?~ ^^
					He sighs. ~Just study the file, ok?~";}
				self.has_ask_prisoner++; "";
			'freedom', 'rights', 'liberty', 'individual', 'book': 
				switch (self.has_ask_freedom) {
					0: print "~Have you read his book?~, you ask. ~He seems to think
					The Union doesn't respect individual liberty.~^^He laughs heartily. 'Mr. Stiles is
					an extremist, Dinnings. Obviously, any kind of government involves some exchange
					of freedom for safety. That's unavoidable. The Union has managed to free mankind from
					the scourge of war, famine and chaos. I think that's worth a few sacrifices.~^";
					1: print "~But don't you think he might have a point?~^^
					He looks worried for a second. ~What's the matter with you today, Dinnings? Just do your job. 
					We don't pay you to debate philosophy.~^";
					default: print "~I think I might read his book someday...~^^ A long pause.
					~Maybe you should take a vacation soon.~^";} !end switch
				self.has_ask_freedom++; return true;
					} !end switch
				}!end for
						],
  has animate proper;

[ TellWilliams;
			if (prisoner.has_information == true){
				print "You tell him about the phone number and everything else you discovered during the
				interrogation. He looks very pleased. ^^~Good job, Dinnings. We pay you well for a reason.
				Now go home and get some sleep. You can continue the investigation tomorrow.~^^
				It is getting pretty late. You have been working too much. A few hours later you are home 
				in your	bed, fast asleep. You are so tired you don't even bother to undress.^^";			
				actii();
				return true;} !end if
			else "You can't think of anything to tell him. You haven't uncovered any
				important information yet.";];

Furniture supervisor_body "Williams corpse"
  with  name 'supervisor' 'williams' 'superior' 'corpse' 'body',
	description "The lifeless corpse of Supervisor Williams. You are in deep trouble now.",
  has proper ~scenery;



!------------------------------------------------------------------------------------

Room 	top_elevator "At the elevator door"
  with	description "You are standing in front of the door to the elevator. It is a large
			metallic door with a silvery color. To the right of the door is a button
			with an arrow pointing down.",
	s_to n_hallway,
	n_to [; <<Enter elevator_door>>;];

Normal_door  elevator_door "elevator door"
  with	name 'door' 'elevator' 'lift' 'metallic' 'silvery' 'lift',
	description "A large metallic elevator door with a silvery color.",
	before [; Open: if (elevator_door hasnt open) {
		print "You push the button. A few moments later the elevator door opens.^";
		give elevator_door open; return true;}
			else {print (The) self, " is already open.";
			"";}
		Enter: if (elevator_door has open) 
			  {if (location == top_elevator) 
				{print "You enter the elevator and push the button for the basement. A few moments later
				the elevator stops, and you exit it. The elevator door closes behind you.^";
				PlayerTo(bottom_elevator);} !end if
			  else 
				{print "You enter the elevator and push the button for the ground floor. A few moments later
				the elevator stops, and you exit it. The elevator door closes behind you.^"; 
				PlayerTo(top_elevator);} !end else
			  give elevator_door ~open; return true;} !end if
			else "The elevator door is closed";],
	found_in top_elevator bottom_elevator,
  has scenery door openable ~open enterable;
  
Object	elevator_button "elevator button"
  with	pname 'button' '.x' 'elevator' 'round' 'arrow',
	description [; if (location == top_elevator) "A round button with a picture of an arrow pointing down.";
			else "A round button with a picture of an arrow pointing up.";
				],
	before [; Push: if (elevator_door has open) "The elevator is already here.";
			else {give elevator_door open; 
		"You push the button. A few moment later the elevator door opens.";}
			],
	found_in top_elevator bottom_elevator,
  has scenery;



!------------------------------------------------------------------------------------

Room bottom_elevator "In the basement"
  with description [;print "You are standing in front of the elevator door. Next to it is a 
  			round button with an arrow pointing up.
  			The hallway here stretches to the south, with
  			walls made of grey stone and a bare cold floor. The air is cool and dry. ";
  			if (location hasnt visited) "You hope you won't get lost in the maze of passages
  			down here."; else "";],
	s_to [; print "You are in a maze of twisty little passages, all alike. Luckily, you have been
			here so often, that you could find your way to the interrogation room in your sleep.^";
			PlayerTo(interrogation); return true;
			],
	n_to [; <<enter elevator_door>>;];
	
Object 	basement_walls "walls"
  with	name 'walls',
  	description "Grey stone walls.",
  	found_in bottom_elevator interrogation cap_elevator captured_hallway,
  has 	scenery;
	

Room interrogation "Interrogation Room"
  with description [;print "The interrogation room is a small square chamber whose only furnishing is a 
			table in the middle of the room. 
			Grey stone walls surround you.
			Two chairs stand at each side of the table, and
			a single lightbulb hangs from the ceiling, casting a sterile white light on everything.
			^The only exit is the door to the south from which you entered.";
			if (location hasnt visited) "You adjust your tie before you enter the room. Might as well look
			your best for the hidden cameras.";
			else "";],
		before [; Exit: if (player in self)
				<<Go s_obj>>;],
		s_to [;if (int_door has open) print "You return to the elevator.^"; return int_door;];
		
Object  lightbulb "lightbulb" interrogation
  with	name 'lightbulb' 'light' 'bulb',
  	description "A single unshielded lightbulb hangs from the ceiling filling the room with a 
  			sterile white light."
  has	scenery;
		
Object  cameras "cameras" interrogation
  with	name 'cameras' 'camera' 'hidden',
  	description "If you could see them, they wouldn't be hidden, would they?",
  	before [; Examine: return false; default: "You can't see them. They are hidden, remember?";],
  has 	scenery pluralname;

Object prisoner "Andrew" interrogation
  with  name 'prisoner' 'andrew' 'stiles' 'author' 'writer' 'subject' 'six' 'man' 'him',
	description "The subject is a tall thin man with piercing blue eyes. He looks like he doesn't get enough to eat.
			Probably spends too much time in front of his computer, neglecting his physical needs. 
			He seems restless, constantly shifting around on the chair, fiddling with his watch,
			and drumming his fingers on the table. 
			He has probably been here for a while. Excellent. Tense people are easier to break.",
	ask_terrorist 0,
	ask_mother 0,
	ask_philosophy 0,
	ask_rights 0,
	ask_phone 0,
	ask_family 0,
	ask_sister 0,
	ask_self 0,
	ask_work 0,
	ask_food 0,
	ask_law 0,
	ask_father 0,
	ask_union 0,
	ask_conform 0,
	has_beaten false,
	has_information false,
	initial [; print "The subject, Andrew Stiles, is sitting on the right chair";
			if (watch in self) print ", fiddling impatiently
			with his watch. ";
			else print ". ";
			print "^";
			if (location hasnt visited) print "He looks at you defiantly when you walk into the room. ~I demand
				that you let me go immediately. This is a blatant violation of my rights!~^";
			return true; ],
	before [; Hit: if (self.has_beaten == false) {print "^You tire of his insolence. Some physical pressure should soften him up a bit. 
				Your punch knocks him off the chair and onto the floor, where he stares at you
				like a deer caught in the headlights. Unfortunately, it doesn't seem to have the
				desired effect. He gets back on the chair and spits blood in your face.^^
				~If you think violence will make me talk, you are gonna be disappointed. I am not
				afraid of pain, you fascist pig!~^^How annoying. It seems like another approach is
				needed.^"; self.has_beaten = true; rtrue;}
		Setfree: "That's not up to you. Your superiors will decide when it is safe to release him.";
		Search: "He has already been searched before he was brought here.";],
	Orders [; "^~I don't take orders from you!~";],
	life [word_number i;
		Tell: "You are here to ask him questions, not to tell him things.";
		Attack: remove prisoner; move prisoner_body to location;
			print "You tire of his insolence. Before he has time to realise what is going on
			you have put a bullet in his head. He is dead before he hits the ground.^
			A moment later, your supervisor comes running into the room, followed
			by two armed guards.^~What the hell are you doing, Dinnings!~, he yells irately,
			his face red with anger and surprise.
			~He was no threat!~^";
			if (self.has_information == true){
				print "He takes a deep breath. ~Well, at least you got the information
					we need. We can always edit the recordings to show that he assaulted
					you. I guess you have been under a lot of stress lately. Why don't
					you go home and get some sleep?~^^
					That sounds like a good idea. You have been working far too much.
					A few hours later, you are home in your bed, sleeping soundly.^";
					actii();} !end if
			else  {
				print "You have never seen him so angry. ~Now we will never know if he knew anything! Are you trying to protect the
				terrorists or what?~ With that, the guards haul you away, placing you in a cell where you
				will be awaiting your own interrogation...."; Deadflag = 3;}
			return true;
		Ask: 	wn = consult_from;
			word_number = consult_words;
			for (i = 1: i <= word_number: i++) {
				switch(NextWord()) {
		'food', 'water', 'eat', 'drink', 'hungry', 'thirsty':
			switch(self.ask_food){
					0: print "^~Are you hungry, Mr. Stiles? Or thirsty perhaps?
							As soon as we are finished here, I can get
							some refreshments.~^^
							He just smiles. ~Well, my throat is getting a bit dry.
							But, luckily, the human body can last up
							to three days with no water or sustenance.~^";
					default: "You don't seem to be getting anywhere with material temptations.";}! end switch
			self.ask_food++; rtrue;
		'work', 'money', 'income', 'bribe':
			switch(self.ask_work){
				0: print "^~So, what do you do for a living, Mr. Stiles?~^^
					~I'm unemployed at the moment. Surely you know that already?
					I was hoping that sales from the book might supplement my income somewhat.
					But you put a stop to that, didn't you?~^";
				1: print "^They say you catch more flies with honey than with vinegar...^^
						~If you have economic problems, I am sure The Agency would
						be willing to help you out. Just be a bit more cooperative...~^^
						He raises an eyebrow, then laughs out loud. ~Trying to bribe me now?
						I have no desire to make any deals with you! You have no right to be
						holding me here in the first place.~^";
				default: "Money doesn't seem to be important to him.";
					}!end switch
			self.ask_work++; rtrue;
		'himself', 'andrew', 'stiles':
			switch(self.ask_self){
				0: print "^~What can you tell me about yourself?~^^
					~That I want to get the hell out of here?~, he replies
					condescendingly. ~You don't honestly expect me to tell
					you the story of my life, do you?~^";
				default: "He doesn't seem willing to talk about himself.";
					}! end switch
			self.ask_self++; rtrue;
		'philosophy', 'education', 'university', 'college', 'school', 'philosophers', 'aspirations', 'accomplish': 
			switch (self.ask_philosophy) {
				0: print "^~I understand that you studied philosophy at college?~^
				He looks at you with contempt. ~I have neither the time nor
				the inclination to debate philosphy right now. What am i
				being charged with?~^";
				1: print "^Why did you quit your studies?~^^~I didn't feel like
				I was accomplishing anything useful. Too much pointless academic crap.~^";
				2: print "^~What would you like to accomplish, then?~^^
				~You already know that. I have no intention of telling you anything more.
				Please release me.~^";
				default: print "It doesn't seem like that will get you anywhere.";} !end switch
			self.ask_philosophy++; return true;
		'rights', 'right', 'freedom', 'demand', 'demands': 
			switch(self.ask_rights) {
				0: print "^~Mr. Stiles, I don't understand why you think your rights are
				being violated. You know as well as I do that the government
				has every right to detain you if you are considered a possible
				threat to the common good.~^^~I am talking about ethics, not law.
				The law might give you the right to keep me here, but it is
				a unjust law. My only crime is to criticize The Union.~^";
				default: "He has already told you his opinion.";}!end switch
			self.ask_rights++; return true;
		'law', 'laws':
			switch(self.ask_law){
				0: print "^~You understand that the law gives us the right to hold you here, don't you?~^^
					~Of course I understand. That's why I wrote my book. The law of The Union is
					unjust and authoritarian. You have no right to treat individual citizens
					like this.~^";
				default: "You don't feel like discussing law with him. This isn't a courtroom.";}! end switch
			self.ask_law++; rtrue;
		'terrorists', 'terrorist', 'terrorism', 'terorism', 'terorists', 'terorist', 'connections', 'connection', 'terror', 'violence' , 'book', 'guy', 'contacts', 'contact', 'activities', 'man', 'fan':
			if (self.ask_mother == 0 || ~~file.has_read_mother){
				switch(self.ask_terrorist){
				0: print "^~Considering that you have just published a book advocating overthrowing
					the government, I hope you can understand why you are here. Do you have any
					contact with terrorist organisations?~^^~I don't believe in violence. Change
					should be made through peaceful means, if at all possible.~^";
				1: print "^~Have you ever been approached by representatives from terrorist organisations?~^^
				~My life is none of your business. Please release me.~^";
				2: print "^~The sooner you cooperate, the sooner you can get out of here. Please answer
				the question: Have you ever been approached by terrorist organisations?~^^
				He doesn't answer.^";
				default: "It doesn't seem like he intends to cooperate.";}!end switch
				self.ask_terrorist++; return true;
				}!end if
			else {
				switch(self.ask_terrorist){
				0: print "^~Please be a bit more cooperative, and answer my questions. Have you ever
				had any contact with terrorists?~^^
				He grinds his teeth. ~Okay, you bastard. About a week after I published my book
				this guy called me and pretended to be a huge fan of mine. He asked me if I
				had the guts to take a more active approach to achieving my goals.~^";
				1: print "^~What did you say to the man on the phone?~^^~That I didn't believe in using violence
				unless all other options have been tried. He gave me a phone number I could call,
				if I were to change my mind.~^";
				default: "He seems to be telling the truth. You are trained to notice it when people lie.";}!end switch
				self.ask_terrorist++; return true;
				}!end else
		'phone', 'number' : 
			if (self.ask_terrorist > 0 && self.ask_mother > 0 && file.has_read_mother) {
		 		switch(self.ask_phone){
					0: print "^~Do you remember the phone number?~^^
					~Yes, I have a very good memory~.^^He gives you the number";
					if (notebook in player) print ", and
					you quickly jot it down in your notebook.^";
					else print ".^";
					default: "You already have the phone number. You don't think you
					will be getting any more useful information out of him.";}!end switch
				self.has_information = true; self.ask_phone++; return true;} !end if
			else "You have no idea what you are going to ask him about.";
		'family', 'relatives', 'siblings', 'brother': 
			switch(self.ask_family) {
				0: print "^~How is your family?~^^His face turns white. 
				~Don't you dare involve my family in this! They haven't done
				anything. They are brainwashed conformists. Just the kind of
				people you like.~^";
				1:print "^~How much contact do you have with your family?~^^
				~I am not going to answer personal questions!~.^";
				default: "You don't seem to be getting anywhere with this line
				of questioning.";} !end switch
			self.ask_family++; rtrue;
		'conformists', 'conformism', 'conformist':
			switch(self.ask_conform){
				0: print "^~Don't you like conformists?~^^
					~Of course not~, he replies, in a tone like the one you
					use when talking to a small child who just doesn't get it.
					~People need to think for themselves. To be individuals.
					If you just do what you are told without a second thought,
					you can hardly be considered a person at all.~^";
				1: print "^~Any kind of community requires some degree of conformism. Society
					is a place where people work together. There is no space for the
					lone wolf.~^^
					~I guess we will just have to agree to disagree~, is his only reply.";
				default: "This debate doesn't seem very constructive.";
				}
			self.ask_conform++; rtrue;
		'father', 'dad', 'daddy':
			switch(self.ask_father) {
				0: print "^~What can you tell me about your father?~^^
					~He died in a traffic accident when I was a kid. What does
					he have to do with anything?~^";
				1: print "^~Were you close to your father?~^^
					~Close? I barely even remember him. I was only 4 when he died.~^";
				default: "His father doesn't seem important.";} !end switch
				self.ask_father++; rtrue;
		'mother', 'mom', 'mum', 'rebecca': 
			if (file.has_read_mother == true) {
				switch(self.ask_mother){
				0: print "^~I understand you visit your mother often?~^^
				~Yes, but she has nothing to do with anything. I just visit
				her to keep in touch. Can we please keep her out of this?~^
				 He sends you a pleading look.^^ ~Of course, Mr Stiles.~, you say
				with a smile. ~If only you would be a bit more cooperative....~^^
				If looks could kill, you would be dead by now.^"; 
				self.ask_terrorist = 0;
				self.ask_sister = 0;
				default: "^~Anything more you can tell me about her?~^^
				~No! Just leave her out of this.~";}!end switch
				self.ask_mother++; return true;} !end if
			else {switch(self.ask_mother){
				0: print "^~What can you tell me about your mother?~^^He laughs. ~I thought
				you guys were supposed to know everything. Don't you have a
				file on her or something?~^";
				default: "You can't think of anything to ask at the moment.";
						}! end switch
				self.ask_mother++; rtrue;}! end else
		'sister', 'sis', 'sibling', 'allyson', 'argument':
			if (file.has_read_sister == true) {
				if (self.ask_mother == 0) {
					switch(self.ask_sister) {
						0: print "^~You had some kind of argument with your sister.
						What was it about?~^^~That isn't any of your business~, he replies
						calmly. ~Leave my family out of this.~^";
						default: "He seems very uncooperative.";}!end switch
					self.ask_sister++; return true;} !end if
				else {
					switch(self.ask_sister) {
						0: print "^~It is in your own interest to cooperate. You had an argument with
						your sister. What was it about?~^^~Allyson is a naive conformist. She truly
						believes that The Union is an omnibenevolent entity which cares for us,
						wants us to be happy and can do no wrong. Just being around her makes me
						feel ill.~^";
						1: print "^~So that's what you argued about?~^^~Yes. She, on the other hand, thinks I am a
						dangerous iconoclast who wants to return to an age of barbarism. We don't get along
						very well.~^";
						default: "You can't think of anything else to ask about.";}!end switch
					self.ask_sister++; return true;}!end else
			}!end if
			else {switch(self.ask_sister){
				0: print "~Do you have a sister?~^^He looks puzzled. ~You mean you don't know?
					Don't you have a file on me or anything?~^";
				default: "You can't think of anything else to ask, at the moment.";
						}!end switch
				self.ask_sister++; rtrue;}!end else
		'union', 'unification', 'politics':
			switch(self.ask_union){
				0: print "^~Mr. Stiles, I don't understand your adversarial attitude towards
				The Union. Surely, you realise that quality of life has improved immensely
				since unification?~^^
				He leans forwards in his chair. ~Yes, life has become easier
				in many regards. But at what price? People have no control over
				their own lives. No freedom to decide for themselves what they want to do.
				Life may have been harder in the past, but at least it was our own.~^";
				1: print "^~So you think people were more 'free' back when they
				were in constant danger of dying from war, poverty or disease?~^^
				~'in Italy'~, he quotes, ~'for 30 years under the Borgias they had warfare, 
				terror, murder, and bloodshed, but they produced Michelangelo, 
				Leonardo da Vinci, and the Renaissance. 
				In Switzerland they had brotherly love - 
				they had 500 years of democracy and peace, and what did that produce? 
				The cuckoo clock.' - 'The Third Man'. I think i'm the only one
				who watches these movies nowadays.
				You have turned people into a bunch of conformist sheep. 
				That price is too high to pay for any reason.~^";
				default: "You are tired of discussing political philosophy.";}!end switch
			self.ask_union++; rtrue;
		!default: "There is no answer. He just stares at you defiantly.";
						}!end switch
					}!end for
				],!end life
  has animate proper transparent;

Object	watch "watch" prisoner
  with	name 'watch' 'arm' 'time',
  	taken false,
  	description "The subject's watch is a cheap timex. Writers aren't rich.",
  	before [; Take: if (watch in prisoner) {print "You grab his arm and tear off the watch.
  	~HEY!~, he shouts. ~That's mine!~^^~Now it belongs to The Union~,
  	you respond with a laugh.^"; move watch to player; self.taken = true; return true;}
  		Give: if (second==prisoner) "Giving it back would make you appear weak.";
  		Read: switch(act){
			1: "It's getting pretty late. You can't wait to get home and get some rest.";
			3: "It's still early in the day. You have plenty of time to finish
				your investigation.";
			4: "It is late in the afternoon.";}
				],
  has 	clothing legible;

Furniture prisoner_body "Andrew's corpse"
  with  name 'prisoner' 'andrew' 'stiles' 'body' 'corpse' 'author',
	description "The lifeless corpse of Andrew Stiles. You are not getting
			any more information from this one.",
  has proper ~scenery;

Furniture int_table "table" interrogation
  with name 'table' 'small',
	description "A small square wooden table.";

Furniture right_chair "right chair" interrogation
  with name 'right' 'chair' 'chairs',
  	description "A small wooden chair.",
	before [; Enter: "The prisoner is already sitting there.";],
  has enterable;

Furniture left_chair "left chair" interrogation
  with name 'left' 'chair' 'chairs',
  	description "A small wooden chair.",
  has enterable;
  
Normal_door  int_door "door" interrogation
  with  name 'door' 'interrogation' 'portal',
  	door_dir [; return s_to;
  	        ],
  	door_to [; return bottom_elevator; !{print "You return to the elevator.^"; PlayerTo(bottom_elevator);}
        ],
  	description "A solid metal door.",
  has	scenery openable ~open door;

!-------------------------------------------------------------------------------------------------------!
! Act II: Flight


Room sky "In the air"
  with	description "You are flying.^Below you, the landscape is spread out like a
  	three dimensional map, buildings and trees like tiny toy models.
  	The wind caresses your wings as you glide through the air, laughing with exuberant joy.
  	Your back is warm from the sunlight.",
  	u_to higher,
  	d_to [;StartDaemon(people); return lower;];
  	
Object icarus sky
  with description [;
  		if (wings in icarus) "Not only are you as good looking as ever, but you
			also have large majestic wings protruding from your back.";
		else "Your wings are gone. You feel sad.";];
  	
Object	buildings "buildings"
  with	name 'buildings' 'building' 'house' 'houses',
  	description "Only a few small houses dot the beautiful landscape below.",
  	found_in sky lower,
  has 	scenery pluralname;
  	
Object	trees "trees" sky
  with 	name 'tree' 'trees' 'plants',
  	description "You can't make out the species of the trees from up here.",
  has 	scenery pluralname;
  
Object landscape "landscape"
  with 	name 'landscape' 'ground',
  	description [;switch (location) {
  			sky: "You can't make out many details from up here, but the landscape is green.
  				Only a few houses spoil the otherwise pristine nature.";
  			higher: "You are above the clouds now. You can barely even see the ground.";
  			lower: "It is a beautiful natural landscape with green grass, flowers and trees.
  				A bit too cutesy for your taste, actually.";}],
  	found_in sky lower higher,
  has scenery;
  	
Object	wings "pair of wings" icarus
  with	name 'wings' 'wing' 'feathers' 'feather' 'back',
  	description "A pair of large majestic wings with white feathers, held together
  		by wax.",
  	before [; Examine: return false;
  			default: "You shouldn't mess with your wings. You need them to fly.";],
  	!found_in sky higher lower ground captured captured_hallway,
  has 	scenery transparent;
  
Object	wax "wax" wings
  with	name 'wax',
  	description "It keeps your wings together. Hopefully it won't melt or anything.";
  

  	
Object 	clouds "clouds"
  with	name 'clouds' 'sky',
  	description "There are very few clouds. The sky is almost completely clear.",
  	found_in sky higher,
  has	scenery;
  
Object 	sun "sun"
  with	name 'sun' 'sunlight' 'rays' 'light',
  	description "The sun hangs in the middle of the almost clear sky, sending its
  		invigorating rays down to the earth below, inviting you to come nearer.",
  	found_in sky higher,
  has	scenery;


!---------------------------------------------------------------------------------------------------

Room higher "Higher up in the air"
  with	description "From up here you can barely make out any details in the landscape below.
  		The warmth from the sun is much more intense, causing sweat to drip from your face.",
  	d_to sky,
  	u_to [; remove wings; print "Disregarding all caution, you fly higher and higher, closer and closer to the sun.
  		Soon, the wax holding together your wings starts to melt. Feathers are jerked loose
  		by the wind, trailing behind you. Flapping your wings merely exacerbates the problem,
  		causing them to fall apart completely. With a fearful cry, you plummet towards the ground.^^
  		You must have lost consciousness on the way down, because the next thing you remember is
  		waking up.^"; PlayerTo(cot); StartDaemon(person); return true;];

!------------------------------------------------------------------------------------------------

Room lower "Closer to the ground"
  with 	description "Down here you can make out many more details. There is a group of people 
  		on the ground, who seem to be discussing something.",
  	u_to [;StopDaemon(people); return sky;],
  	d_to [; print "You land on the ground, near the group, and smile at them.
  		Unfortunately, they do not return your smile. Instead one of them
  		rudely fires a gun at you, and a small dart embeds
		itself in your skin. You stare at it with surprise as you start to feel dizzy.
		You try to get away but your wings feel too heavy to move. As you hit the soft grass,
		you lose the last bit of consciousness.^"; PlayerTo(cot); 
		StopDaemon(people); StartDaemon(person); return true;];
		
Object 	decoration "plants" lower
  with 	name 'trees' 'grass' 'flowers' 'tree',
  	description "It's just a bunch of plants. Pretty, though.",
  has 	pluralname scenery;
 
 
Object 	people "people" lower
  with	name 'people' 'group' 'guys' 'men' 'mibs' 'mib' 'in' 'black' 'man' 'guy',	
  	description "A group of half a dozen people dressed in dark business suits. They seem
  		to be discussing something, but you can't hear it from up here.",
  	event_nr  0,
  	life [; if (people_gun in location) <<Take people_gun>>;
  		default: "They are too far away. You will need to land if you want to
  			interact with them.";],
  	daemon [; 
  		switch(self.event_nr) {
			0: print "";
			1: print "One of them notices you and points at you, alerting the
				others to your presence.^";
			2: move people_gun to location; 
				print "The guy who noticed you draws something from his pocket. It appears
				to be a gun.^";
			3: print "He fires. Instead of being struck by a bullet, a small dart embeds
				itself in your skin. You stare at it with surprise as you start to feel dizzy.
				You try to get away but your wings feel too heavy to move. As you plummet towards
				the ground, you lose the last bit of consciousness.^";
				PlayerTo(cot); Stopdaemon(people); StartDaemon(person);
			default: StopDaemon(people);} !end switch
		self.event_nr++;
		
        ],
  has scenery animate pluralname;
  
  
Object people_gun "gun"
  with	name 'gun' 'pistol',
  	description "You can't quite make out the details, but it's definitely a gun.",
	before [;Take: print "You swoop down and try to snatch the gun from his hands, but he
		is too fast. Instead of being struck by a bullet, a small dart embeds
		itself in your skin. You stare at it with surprise as you start to feel dizzy.
		You try to get away but your wings feel too heavy to move. As you plummet towards
		the ground, you lose the last bit of consciousness.^";
		PlayerTo(cot); Stopdaemon(people); StartDaemon(person); return true;],
  has scenery;
!------------------------------------------------------------------------------------
Room ground "On the ground"
  with	description "You are standing on the ground. The 
  		group of people are in front of you, studying you intently.",
  	u_to lower;
 
!------------------------------------------------------------------------------------
Room	captured "In a cell"
  with	description [; 
  		if (location hasnt visited) print "You wake up in a dark damp prison cell, lying on a simple cot. ";
  		else print "You are in a dark damp prison cell, equipped with a simple cot. ";
  		"The walls are made of stone,
  		and a heavy iron door blocks the exit to the west. The only light comes from a small
  		window in the east wall, iron bars ruining any chance of escape.";],
  	w_to [;if (person.been_killed==true && iron_door has open) {
  			print "You walk through the door into the beautiful landscape outside. You have escaped. ^"; 
  			if (wings in icarus) {print "With your wings spread out behind you, you leap into the air,
  				laughing again with the thrill of freedom. The sky is yours, with its multitude of pleasures
  				and dangers. As you fly away, the landscape gradually fades away around you.
  				You awaken.^"; actiioutcome=freedom; actiii();}!end if
  				else {print "As you walk away, you feel sad that your wings are gone. You will never
  					fly again. But at least your life is your own. As you walk away, the landscape
  					gradually fades away. You wake up.^";
  					actiioutcome=freedomnotwings; actiii();} !end else
  			rtrue;
			} !end if
  		else return iron_door;];
  		
Furniture cot "cot" captured
  with	name 'cot' 'bed' 'simple',
  	knife_found false,
  	description "A hard uncomfortable cot with a dirty blanket. You don't think you will get much sleep here.",
  	before [; LookUnder: if (~~self.knife_found){print "Beneath the cot you discover an old rusty knife. Someone must have
  			dropped it here and forgotten about it.";
  			move knife to captured; 
  			self.knife_found = true; "";}
  				else "You discover nothing new under the cot.";],
  has 	enterable;
  			
Object 	knife "knife"
  with	name 'knife' 'dagger' 'weapon' 'rusty' 'old',
  	description "A very old knife covered in rust. It could still be useful for self defense though.";

Object	blanket "blanket"
  with	name 'dirty' 'blanket' 'sheet',
  	description "A old dirty blanket. This place is far from a luxury hotel.",
  	before [; Examine: return false; default: "Why would you want to mess with a dirty blanket?";];
  	
Normal_door  iron_door "door"
  with 	pname '.x' 'cell' '.x' 'iron' 'door',
  	description "A solid iron door. There is no way you can get out without the key.",
 
 door_dir [;
		            if (location == captured) return w_to;
		            else                  return e_to;
		        ],
		door_to [;
		            if (location == captured) return captured_hallway;
		            else                  return captured;
        ],
        with_key iron_key,
        found_in captured captured_hallway,
  has	scenery door openable ~open lockable locked;

Object 	cell_walls "walls" captured
  with 	pname '.x' 'cell' 'walls' 'wall' ,
  	description "Thick stone walls. You can't possibly get through them in any way.",
  has 	scenery pluralname;
  
Object	cell_window "window" captured
  with 	pname 'window' '.x' 'cell',
  	description "Even if the iron bars weren't there, the window would be too small for you
  		to fit through.",
  has 	scenery;

Object	window_bars "bars" captured
  with	pname '.x' 'window' 'bars' 'iron' 'bar',
  	description "Cylindrical bars of solid steel.",
  has 	scenery;
  
Object	person "man"
  with	name 'person' 'guy' 'mib' 'him' 'man' 'in' 'black' 'agent',
  	description "A tall man dressed in a black suit, wearing a tie embroidered with golden squares on a black background.",
  	ask_cell 0,
  	ask_wings 0,
  	ask_dreams 0,
  	has_answer false,
  	initial [;"A man in a dark suit is standing here.";],
  	been_killed false,
  	before 	[; Attack: if (knife in player) {print "You charge him with the knife. Before he gets a chance to react,
  		you have planted it in his chest. He slumps to the ground with a shocked look on his face, 
  		a pool of blood forming underneath him.";
  		stopdaemon(person);
  		self.been_killed=true;
  		move blood to location;
  		move person_corpse to location;
  		remove person; "";} !end if
  			else "He looks tough. You don't think you can take him in a fair fight.";
					],!end before
  	person_action 0,
  	daemon	[; switch(self.person_action) {
			5: move person to captured;
				print "The door opens. A man wearing a dark suit and a tie enters the cell,
				closing the door behind him.^";
			6: print "^~Greetings~, he says, while carefully examining you.^
				~I hope that you are not too uncomfortable? You must understand 
				that this is all for your own good.~^";
			8: print "^He gestures for you to follow him. ~Come with me.
				There are things we must discuss.~^Leaving the cell,
				he leaves the door open behind him.^";
				give iron_door open;
				give iron_door ~locked;
				move follow_person to location;
				remove person;
				StopDaemon(person);
			default: print "";
						}!end switch
			self.person_action++;],
	life [word_number i;	
		Tell:   wn = consult_from;
			word_number = consult_words;
				for (i = 1: i <= word_number: i++) {
					switch(NextWord()) {
						'knife', 'dagger', 'weapon':
						<<Show knife self>>;
						} ! end switch
							} !end for
		Ask: 
			ask_person();
 			if (self.has_answer) {self.has_answer = false; rtrue;}
	
		Show, Give:
		if (noun==knife){
		remove knife; "He quickly snatches the knife from your hands. Examining it, he says^^
			~I wonder how that ended up here. You did the right thing by turning it over to me.
			 Someone might have gotten hurt.~^^He puts it away in his pocket.";
			}!end if
		],
  has 	animate transparent;
  
Object 	person_tie "tie" person
  with 	name 'tie',
  	description "His tie is black, embroidered with golden squares.";
  
[ask_person word_number i;
  	wn = consult_from;
	word_number = consult_words;
	for (i = 1: i <= word_number: i++) {
		switch(NextWord()) {
	'cell', 'prison', 'imprisonment', 'imprisoned', 'why':
		person.ask_cell++; 
		switch(person.ask_cell) {
			1: person.has_answer = true;
			"^~Why am I here?~, you ask.^^
			~We are keeping you here for your own protection. 
			You are a danger to yourself.~";
			2: person.has_answer = true;
			"~I am not in any danger! Just let me go. I promise I will be careful~^^
				He looks at with pity. ~I'm sorry, but I can't do that. Your
				reckless behavior puts you in far too much danger. We need
				to make sure that you stay on the ground.~";}
	'wings', 'wing', 'flight', 'fly', 'freedom':
		person.ask_wings++; 
		switch(person.ask_wings) {
			1: person.has_answer = true;
			"^~Just let me fly away~, you plead. ~I won't bother you...~^^
			He shakes his head sadly. ~We are acting in your best interest, not our own. 
			Some day you will understand that.~";
			2: person.has_answer = true;
			"~Why won't you let me fly?~^^
				He sighs. ~It is too dangerous. You must understand that.~";}
	'dream', 'dreams', 'dreaming', 'wake', 'awake':
		person.ask_dreams++; 
		switch(person.ask_dreams) {
			1: person.has_answer = true;
			"~Is this a dream?~, you ask.^^
			He smiles. ~Yes, but your choices still matter.
			Choices always matter. They define who you are.~";
			2: person.has_answer = true;
			"~If this is a dream, can't I just wake up?~^^
			He seems troubled by this suggestion.
			~It's possible...~, he says tentatively.
			~But I would advise against it. You are going to miss a lot.~";
				}
			}!end switch
		} !end  for
];

Object 	follow_person "man"
  with	name 'person' 'guy' 'mib' 'him' 'man' 'in' 'black' 'agent',
  	description "The man is no longer here.",
  	before [; Follow: print "You follow the man through the door.";
  			PlayerTo(captured_hallway); rtrue;
  		default: "The man is no longer here.";],
  has	scenery;
  
Object	blood "blood"
  with 	name 'blood' 'pool',
  	description "A pool of blood from the guy you brutally murdered.",
  has 	scenery;
  
Furniture person_corpse "corpse"
  with	name 'corpse' 'person' 'guy' 'mib' 'him' 'man',
  	description "The corpse of the man you have brutally murdered, lying in a pool of his own blood.",
  	before [; Search: if (iron_key in self) {
		move iron_key to location; 
		give iron_key ~concealed; "On the corpse you discover a heavy iron key.";}! end if
		],
  has 	~scenery;
  
Object 	iron_key "key" person_corpse
  with	pname '.x' 'door' 'key',
  	description "A heavy iron key.",
  	before [; Take: if (self has concealed) give self ~concealed;],
  has 	concealed;



!------------------------------------------------------------------------------------

Room 	captured_hallway "A hallway"
  with	description 	"You are standing in front of the cell door to the east. 
  			The hallway here stretches to the west, with
  			walls made of grey stone and a bare cold floor.",
  		e_to iron_door,
  		w_to cap_elevator;
  		
  	
!------------------------------------------------------------------------------------
  	
Room cap_elevator "At an elevator door"
  with	description "You are standing in front of a door to an elevator. It is a large,
			silvery metallic door. To the right of the door is a button
			with an arrow pointing down.",
	e_to captured_hallway,
	w_to [; <<Enter cap_elevator_door>>;];

Normal_door  cap_elevator_door "elevator door"
  with	name 'door' 'elevator' 'lift' 'metallic' 'silvery' 'lift',
	description "A large metallic elevator door with a silvery color.",
	before [; Open: if (self hasnt open) {print "You push the button. A few moments later the elevator door opens";
		give self open; return true;}
			else {
			print (The) self, " is already open.";
			"";}
		Enter: if (self has open) 
			  {if (location == cap_elevator) 
				{print "You enter the elevator and push the button for the basement. A few moments later
				the elevator stops and you exit it into the damp basement.
				The door closes silently behind you.^";
				PlayerTo(cap_basement); StartDaemon(basement_people);} !end if
			  else 
				{print "As you enter the elevator, the door closes behind you. Before
				you get a chance to push any buttons, the elevator starts going down. 
				Your surroundings begin to fade. 
				You struggle to stay asleep to see what will happen, but it doesn't
				work. ^^You awaken.^"; 
				if (wings in icarus) actiioutcome = comply;
				else actiioutcome = complynotwings;
				actiii();} !end else
			  give self ~open; return true;} !end if
			else "The elevator door is closed.";],
	found_in cap_elevator cap_basement,
  has scenery door openable ~open enterable;
  
Object 	cap_elevator_button "elevator button"
  with	pname 'button' '.x' 'elevator' 'round' 'arrow',
	description [; if (location == cap_elevator) "A round button with a picture of an arrow pointing down.";
			else "A round button with a picture of an arrow pointing up.";
				],
	before [; Push: if (cap_elevator_door has open) "The elevator is already here";
			else {give cap_elevator_door open; 
		"You push the button. A few moments later the elevator door opens";}
			],
	found_in cap_elevator cap_basement,
  has scenery;


Room	cap_basement "A meeting room"
  with	description "You are standing in what appears to be a large meeting room. Five men, all in
  			dark suits, are sitting around an oval table, all looking in your direction. A thick red carpet
  			covers the floor. Behind you,
  			to the east, the elevator forms the only exit from the room.",
  	before [; Go, Enter: if (basement_people.has_answer == true) return false; 
  			else "You try to move, but your legs refuse to comply.";],
  	e_to	[; <<Enter cap_elevator_door>>;];


Object	basement_people "people" cap_basement
  with	name 'people' 'men' 'basement' 'man' 'persons' 'person' 'guys' 'guy' 'him' 'them',
  	description "They seem intimidating. You feel a desperate need to get away.",
  	has_asked false,
  	has_answer false,
  	life [word_number i; Show, Give:
		if (noun==knife){
		remove knife; "He quickly snatches the knife from your hands. Examining it, he says^^
			~I wonder how that ended up here. You did the right thing by turning it over to me.
			 Someone might have gotten hurt.~^^He puts it away in his pocket.";}
		Tell:   wn = consult_from;
			word_number = consult_words;
				for (i = 1: i <= word_number: i++) {
					switch(NextWord()) {
						'knife', 'dagger', 'weapon':
						<<Show knife self>>;
						} ! end switch
					} !end for
  		Ask: 
  			if (self.has_asked == true && self.has_answer == false) "~Please answer the question~";
  			else {ask_person(); 
  				if (person.has_answer) {person.has_answer = false; rtrue;}
  					}
  		Answer: if (self.has_asked == true) switch(noun) {
			'yes', 'sure', 'affirmative': <<yes>>;
			'no', 'nope', 'negative': <<no>>;
				}! end switch
			else return false;
  		Attack: if (knife in player) {
  			print "There are five of them versus one of you. The odds are certainly against you, but you don't
  			care. With the element of surprise on your side, you draw the knife and charge them recklessly. 
  			The outcome is as expected. You manage to injure several of them, but they are certainly
  			not untrained in the art of self defense. A well placed punch knocks you out for the second time
  			that day. The shock awakens you.^"; actiioutcome = hopelessfight; actiii(); rtrue;}
  			else "There are five of them versus one of you. Probably not a good idea.";],
  	react_before [; Yes: if (self.has_asked==true) {print "He smiles warmly. ~A very sensible decision.~^Behind you,
  			the elevator door quietly opens. ~Just step into the elevator, and it will take you where you need
  			to go.~^"; give cap_elevator_door open; self.has_answer = true; rtrue;}
  			No: if (self.has_asked==true) {
				print "He looks disappointed. ~That's too bad~, he sighs. ~I was hoping that you would
				be more sensible~^You feel a slight prick as another tranquilizer dart embeds itself
				in your skin. He was so fast, you didn't even see him draw the gun. The shock
				awakens you.^"; actiioutcome = refuse; actiii(); rtrue;} !end if
  				],
  	person_action 0,
  	daemon [; switch(self.person_action) {
			1: print "One of them speaks to you. ~Welcome. I hope you understand that we have
				brought you here for your own good. We just want you to be safe.~^";
				if (wings in icarus) print "He points to your wings. ~You will be happier without them.
					They put you in danger of falling. It is safer on the ground. I assure you that the
					surgical procedure to have them removed will be completely painless. It is in your
					own interest to cooperate. Will you do so?^";
				else print "He points to the stumps on your back where your wings used to be. 
					~You will be happier without them. They put you in danger of falling. 
					It is safer on the ground. We will need to perform a surgical procedure to ensure
					that they don't grow back. It is in your best interest to cooperate. Will you do so?~^";
				self.has_asked = true;
			default: print "";
						}!end switch
		self.person_action++;
							], !end daemon
  has	animate pluralname scenery;
  
Furniture oval_table "table" cap_basement
  with	name 'oval' 'table',
  	description "The oval table is made from dark wood.";
  	
Furniture cap_carpet "carpet" cap_basement
  with 	name 'red' 'carpet' 'floor',
  	description "The floor is covered with a thick red carpet. It seems familiar somehow.";
  	
  	
! act III: Investigation
!--------------------------------------------------------------------------------------------------------------!

Room bedroom "Bedroom"
  with	name 'bedroom' 'bed' 'room',
  	description [;print "You are in your bedroom";
  			if (player in bed) print ", lying on your bed. "; 
  			else print ". ";
  			print "Next to the bed is a small bed table with an alarm clock, a phone, and a lamp. To the east,
  			a wooden door leads out from the room. ";
  			if (curtains hasnt open) {print "The curtains are drawn, blocking most of the sunlight. ";
				if (bed_lamp has on) print "However, the bed lamp provides a soft white light. ";}
  			else {print "Sunlight is streaming in through the ";
  				if (bed_window has open) print "open ";
  				print "window. ";}
			"";	],
	e_to bedroom_door;
  			
Furniture bed "bed" bedroom
  with 	name 'bed' 'cot' 'mattress',
  	keys_found false,
  	description "It's a comfy, but ordinary, bed.",
  	before [; LookUnder: if (~~self.keys_found) {print "The keys to your car are lying under the bed. You must have dropped them
  			when you undressed. You grab them and put them in your pocket.^";
  			move car_keys to player; self.keys_found = true; rtrue;}
  			else "You find nothing else of interest under the bed.";],
  has	enterable;

Object car_keys "the keys to the car"
  with 	pname '.x' 'car' 'keys' 'key',
  	description "The keys to your car.",
  has	proper;
  
Object 	bed_window "window" bedroom
  with  pname '.x' 'bedroom' 'window' 'windows',
  	description [; if (curtains has open) "Through the window
  			you can see the tall gleaming buildings of The City. Cars are already filling the
  			streets below. In the distance, you can just barely make out the Statue of Authority,
  			holding its sword high into the air, ready to protect
  			the citizens of The City, should it be needed.";
  			else "You can't see anything through the curtains.";],
  	before [;Open: if (curtains has open) {print "You open the window, letting some fresh air into the bedroom.^";
  				give bed_window open; rtrue;}
  			else "You can't open the window when the curtains are drawn.";],
  has openable ~open scenery;


Object 	curtains "curtains" bedroom
  with	pname 'curtains' 'curtain',
  	description "White curtains, designed to block out as much of the sunlight as possible.",
  	after [; Open: "You open the curtains letting the sunlight into the room. Through the window
  			you can see the tall gleaming buildings of The City. Cars are already filling the
  			streets below. In the distance, you can just barely make out the Statue of Authority,
  			holding its sword high into the air, ready to protect
  			the citizens of The City, should it be needed.";],
  	before [; Close: if (bed_window has open) "You can't draw the curtains when the window is open.";
  			else {print "You draw the curtains again, blocking your view of The City.^";
  				give curtains ~open; rtrue;}],
  has 	openable ~open scenery pluralname;
  
Furniture bed_table "table" bedroom
  with	pname '.x' 'bed' 'table' 'small',
  	description "A small table. Nothing remarkable about it.";
  	
Object	alarm_clock "clock" bed_table
  with	pname  '.x' 'bed' 'alarm' 'clock' 'radio',
  	has_seen,
  	description [; print "A digital alarm clock with a radio.";
  			if (~~self.has_seen) {
				self.has_seen = true;
				"^It is currently 7:00 am. You might
  				as well get up. There is plenty
  				of work to be done.";}
  			else "";],
  	before [; Take: "You don't need it. It is fine where it is.";
  			Read: "It's still early in the day.";],
  has	scenery legible;
  	
Furniture bed_lamp "lamp" bed_table
  with	pname '.x' 'bed' '.x' 'table' 'lamp' 'light',
  	description "A ordinary blue bed lamp.",
  	!before 	[; SwitchOff: give office ~light;
  	!	SwitchOn: give office light;],
  has	switchable ~on;
  
Object	statue "statue"
  with	name 'statue' 'of' 'authority',
  	description "The Statue of Authority was erected shortly after Unification to celebrate the final
  		elimination of war by The Union. It symbolises the ability of the state to rectify the flaws
  		of the individual.",
  	found_in bedroom living_room,
  has	scenery;

Object	phone "phone" bed_table
  with	name 'phone' 'telephone',
	description "A small red telephone. There is a tracing device attached to it, in case you need to trace a call.",
	called_number false,
	called_sister false,
	called_mom false,
	called_code false,
	told_agency false,
	told_sister false,
	before [; Take: "If you want to call someone, simply type CALL <SOMEONE>.";],
	react_before [word_number i; 
			Call: wn = consult_from; 
			word_number = consult_words;
			for (i = 1: i <= word_number: i++) {
				switch(NextWord())  {
		'home':
			"You are in your home.";
		'pizza', 'food':
			"There will be plenty of time to eat after the investigation.";
		'ghostbuster', 'ghostbusters', 'ghost', 'busters':
			"Backpack mounted unlicensed nuclear accelerators have been banned.
			Besides, you haven't encountered any ghosts.";
		'agency', 'supervisor', 'williams', 'office', 'cryptologist', 'police':
			if (sister.is_gone && self.told_sister) {
				if (sister.is_dead) {
					self.told_agency = true;
					self.told_sister = true;
					remove sister_corpse;
					print "You call Agency and inform them of the events
					concerning Allyson Stiles. They promise to send in a cleaner to deal
					with the mess.^";} !end isdead
				else {	self.told_agency = true;
					self.told_sister = true;
					print "You call Agency and inform them of the events concerning Allyson Stiles.
					All field agents will receive orders to arrest her on sight.^";} !end else
						}! end isgone
			if (warehouse_speaker.code_found && ~~self.called_code){
				self.told_agency = true;
				self.called_code = true;
				print "You decide to see whether some of the Agency code experts might be able to help you
				decipher the numbers you found by the warehouse speaker. The cryptologist thinks it might
				be a book code, meaning the numbers refer to the page, line and word number in a given book.
				Unfortunately, there is no way to break such a code without knowing the identity of the book.";}
			if (~~self.told_agency) "You haven't discovered anything useful yet.
			You are supposed to deal with this assignment on your own.
			Excessive requests for backup are seen as a sign of weakness.
			After all, an agent who constantly needs backup must not be worth the 
			trouble of providing it.";
			self.told_agency = false;
			"";
		'phone', 'number', 'terrorist', 'terrorists', 'connections': 
			if (tracer hasnt on) "You should probably turn on the tracer first."; 
			if (self.called_number == false) {print "You dial the phone number from your notebook. 
			A few rings later, a deep voice says ~Yes?~.^^
			 ~I'm sorry, I got the wrong number~. You hang up. The trace is already complete.
			No reason to give anything away.^
			It seems to be an address in the center of The City.^
			[You can now Drive to the Address anytime
			you want.]^";
			tracer.has_traced = true; self.called_number = true; rtrue;}
			else "You don't need to call the number again.";
		'family': "You will have to be more specific.";
		'sister', 'sibling': if (self.called_sister == false && inside_sister hasnt visited)
			{print "A mellifluous female voice responds. 
			~This is Allyson Stiles speaking. Who is it?~^^
			~Agent Dinnings of the Agency speaking~, you respond. I am currently investigating your brother.
			Do you have time to talk later today?~^^She sighs. ~What kind of trouble has he gotten 
			himself into this time? No, it's no problem. I am always happy to help the government. 
			Just stop by my house any time. I am working at home
			today.~^"; self.called_sister = true; rtrue;}
			else "You don't need to talk to her again.";
		'mother', 'mum', 'mommy', 'mom': 
			if (self.called_mom == false) {"The phone rings several times, but there is no
			response. It seems like no one is home.";}
			else "The phone rings several times, but there is no
			response. It seems like no one is home.";
							}! end switch
									}! end for
			],
  has	static transparent scenery;
	
	
Object	tracer "tracer" phone
  with	pname	'.x' 'phone' 'tracer' 'device' 'tracing',
  	has_traced false,
  	description "The tracer is simple to operate. All you need to do is switch it on, and it will
  			trace any call you are making or receiving.",
  has	switchable ~on;
  

Normal_door bedroom_door "bedroom door"
  with  pname 'door' '.x' 'bedroom' 'east' 'west',
	description "A brown wooden door.",
	door_dir [;
	            if (location == bedroom) return e_to;
	            else                  return w_to;
	        ],
	door_to [;
	            if (location == bedroom) return living_room;
	            else                  return bedroom;
        ],
	found_in bedroom living_room,
  has 	scenery transparent door openable ~open;
 
!---------------------------------------------------------------------------------------------

Room	living_room "Living room"
  with	name 'living' 'room',
  	description "Home, sweet home. A soft carpet covers the floor of your living room.
  			By the east wall, a bookshelf displays a disparate collection
  			of novels and non-fiction works. The couch is where
  			you do most of your reading. A lamp stands in the southwest corner, ready to
  			provide light when the sun is no longer sufficient. In the north wall
  			a large window provides basically the same view of The City as you
  			get from your bedroom. The front door lies to the south.",
  	w_to bedroom_door,
  	!e_to kitchen_door,
  	s_to front_door;

!Normal_door kitchen_door "kitchen door"
!  with  pname '.x' 'kitchen' 'door' 'east',
!	description "A brown wooden door.",
!	door_dir e_to,
!	door_to "You don't feel hungry.",
!	found_in living_room,
!  has 	scenery transparent door openable open;

Object living_window "window" living_room
  with  pname '.x' 'bedroom' 'window' 'windows',
  	description "Through the window
  			you can see the tall gleaming buildings of The City. Cars are already filling the
  			streets below. In the distance, you can just barely make out the Statue of Authority,
  			holding its sword high into the air, ready to protect
  			the citizens of The City, should it be needed.",
  has 	scenery;
  
Furniture living_carpet "carpet" living_room
  with	name 'carpet' 'floor' 'rug',
  	description "A nice persian style rug. You bought it at a market several years ago while
  		on an assignment.";
  		
Furniture living_bookshelf "bookshelf" living_room
  with	name 'bookshelf' 'book' 'shelf' 'books',
  	description "The bookshelf contains an eclectic selection of various books, both
  			novels and short story works. Your taste has always been quite varied.
  			You haven't had much time to read lately, though. There has been far
  			too much work to do.";
  			
Furniture living_couch	"couch" living_room
  with	name 'couch',
  	description "The couch has seen better days. You would never replace it, though.
  			It has accumulated far too much emotional value over the years.",
  	before [; Enter: "You don't have time to relax right now. There is work to do.";],
  has	container;

Furniture living_lamp "lamp" living_room
  with	name 'lamp',
  	description "A metal floor lamp standing next to the couch.",
  has	switchable;

!------------------------------------------------------------------------------

Normal_door front_door "front door"
  with  pname 'door' 'front' 'south',
	description "A brown wooden door.",
	door_dir [;
	            if (location == living_room) return s_to;
	            else                  return n_to;
	        ],
	door_to [;
	            if (location == living_room) {
			    print "You walk down the stairs, and exit the building
			    	into the streets of the city.^";
			    return city;}
	            else                  return living_room;
        ],
	found_in living_room,
  has 	scenery transparent door openable ~open;
  
!Room	front_house "Outside your apartment"
!  with	description "Outside of your apartment, the stairs lead up and down. 
!  		The building is a bit rundown, but it doesn't bother you. You never cared
!  		much for material luxury.",
!  	n_to front_door,
!  	u_to "There is nothing of interest up the stairs. Just more apartments.",
!  	d_to city;
  	
!Object	stairs	"stairs" front_house
!  with	description "Old creaky chairs. But they serve their purpose."
!  has	scenery;

!----------------------------------------------------------------------------------------------!

Room	city "Outside the apartment building"
  with	description "You are standing outside the old apartment building where you live.
  		The entrance door lies to the north.
  		Other similar buildings block the view to the rest of The City. A couple of cars
  		pass you on the street, although rush hour is still a short while away.",
  	u_to building_door,
  	n_to building_door,
  	in_to [; <<Enter car>>;],
  	cant_go "Why walk when you can take the car?";
  	

Object	building "apartment building" city
  with	pname '.x' 'apartment' '.x' 'home' 'building' ,
  	description "A tall sleek high rise building. You have lived there for the last few years.
  			Your apartment isn't one of the larger ones, but the view is excellent.",
  	before [; Enter: <<Go n_obj>>;],
  has	scenery;
  
Normal_door building_door "door"
  with  pname '.x' 'building' '.x' 'south' 'door',
	description "A brown wooden door.",
	door_dir n_to,
	door_to [; 	print "You ascend the stairs, and enter your apartment once again.";
			return living_room;],
	found_in city
  has 	scenery transparent door openable ~open;
  
Object 	car "car" city
  with	name 'car' 'vehicle' 'ignition' 'engine',
  	description "A nice black electric Toyota. It's nothing special but it gets you from A to B.",
  	driver "none",
  	when_on "Your car is parked here with its engine running.",
  	when_off "Your car is parked here.",
  	before [; Go: if (self has on) {print "Just type DRIVE TO <LOCATION>, where <LOCATION> is the place
  					you want to go. For example, 'DRIVE TO SISTER'.^"; return 2;}
  		 else {print "The engine isn't running.^"; return 2;};
  		 SwitchOn: if (car_keys in player) {print "You turn the keys in the ignition, and
  		 			the electric engine starts with nary a sound.^"; give car on; rtrue;}
  		 			else "You can't find the keys. You must have dropped them somewhere.";
  		 SwitchOff: if (car_keys in player) {print "You turn off the engine.^"; give car ~on; rtrue;}
  		 			else "You can't find the keys. You must have dropped them somewhere.";
  		 WalkAround: if (~~player in self) {print "You walk around the car before entering it.^"; <<Enter self>>;}
  		 Take: <<Enter car>>;
  		 ], !end before
  	react_before [drive_goal is_the; Drive: 	if (car hasnt on) "The engine isn't on.";
  				wn = consult_from;
  				drive_goal = NextWord();
  				do {
					is_the = false;
  				switch(drive_goal) {
					'the':
						drive_goal = NextWord();
						is_the = true;
					'statue':
						"Although the Statue of Authority is a popular
						tourist spot, you really don't have time to visit
						right now. You have a job to do.";
					'office', 'agency', 'work':
						"There is no reason to return to your office at the moment.
						You have work to do in The City.";
					'sister' , 'sibling', 'sis', 'allyson': 
						if (location == outside_sister) "You are already there.";
						else move car to outside_sister; PlayerTo(car); rtrue;
					'home' ,'apartment', 'building':
						if (location == city) "You are already there.";
						else move car to city; PlayerTo(car); rtrue;
					'mother', 'mum', 'mom', 'rebecca':
						if (location ==mother) "You are already there.";
						else move car to mother; PlayerTo(car); rtrue;
					'address', 'terrorist', 'terrorists', 'connections', 'warehouse':
						if (location == warehouse) "You are already there.";
						if (tracer.has_traced) {
							move car to warehouse; PlayerTo(car); rtrue;}
						else "You don't know how to get there.";
					default: "You don't need to go there."; } ! end switch
				} !end do
				until (~~is_the);
				
			],
  has	switchable enterable static container open ~on;
 
!-------------------------------------------------------------------------------------------

Room outside_sister "In the suburbs"
  with	description "The house of Allyson Stiles is located in the suburbs, away from
  			the ubiquitous noise and pollution of the inner City. 
  			Living here is quite expensive, to say the least. The house,
  			which lies to the east, is a small one story building.",
  	e_to	[;if (~~sister.is_gone) <<Push doorbell>>;
  			else return house_door;],
  	in_to [; <<Enter car>>;],
  	cant_go "You should use your car.";
  	

Normal_door house_door "front door"
  with  pname   '.x' 'house' '.x' 'front' '.x' 'east' '.x' 'west' 'door',
	description "A sturdy door, with a doorbell next to it. The name ~Allyson Stiles~ is printed
			on a small sign by the door.",
	door_dir [;
	            if (location == outside_sister) return e_to;
	            else                  return w_to;
	        ],
	door_to [;
	            if (location == outside_sister) return inside_sister;
	            else                  return outside_sister;
        ],
        before [; Open: 
        	if (~~sister.is_gone) {
        		print "It is probably not a good idea to barge in unannounced. "; <<Push doorbell>>;}
        			],
	found_in outside_sister inside_sister,
  has 	scenery transparent door openable ~open;


Object	sister_sign "sign" outside_sister
  with	pname  '.x' 'door' 'sign',
  	description "The sign says ~Allyson Stiles~.",
  has	scenery;

Object sister_house "house" outside_sister
  with 	pname  '.x' 'sisters' '.x' 'sister' '.x' 'suburb' 'house',
  	description "A nice small house. It must still have cost a small fortune to buy a
  			house in the suburbs. Miss Stiles
  			certainly isn't poor.",
  	Before [; Enter: if (~~sister.is_gone) <<Push doorbell>>;
  				else <<Go e_obj>>;],
  has	scenery;
  
Object	doorbell "door bell" outside_sister
  with	name  'bell' 'doorbell' 'button' 'bellbutton',
  	description "A nice round button. A simple press should announce your presence.",
  	before [; Push: if (player in car) print "After exiting the car, you stroll up to the door. ";
  			if (~~sister.is_gone) {
			print "You push the bell button, and a simple melody plays inside the house. ";
			if (phone.called_sister) print "A short time later, the door is opened by a short brunette, whom you recognise from the
  			pictures in the file. She smiles when she sees you, revealing a row of perfect teeth.^^
  			~You must be Agent Dinnings. Please come inside. I will make you a cup of tea.~^^
  			You thank her, and follow her inside.^"; 
  			else print "A short time later, the door is opened by a short brunette, whom you recognize
  			from the pictures in the file. She smiles when she sees you, revealing a row of perfect teeth.^
  			~Hello, there. Can I help?~^^
  			~Good morning, Miss Stiles~, you reply politely. ~I am Agent Dinnings from The Agency.
  			We are currently investigating your brother. Do you have a moment to talk?~^^
  			A sigh. ~What kind of trouble has he gotten himself into this time?
  			Sure, I have a moment. Come in and have a seat. I'll make you a cup of tea.~^^
  			You thank her, and follow her inside.^";
  			PlayerTo(sister_chair); StartDaemon(sister); rtrue;}
  			else "The doorbell rings. There is no response.";],
  has	scenery;
 
!----------------------------------------------------------------------------------------------------------------------

Room	inside_sister "The living room" 
  with	description [; print "This is a very nice room, with a carpet on the floor and a clock on the wall that produces
  			a soothing ~tick tock~ sound. The room is furnished with a comfy chair";
  			if (player in sister_chair) print ", on which you are currently sitting,";
  			else print ",";
  			print " a couch,
  			and a small table. ";
  			if (teatray in sister_table) {
				print "On the table is a wooden tray";
  				if (teacup in inside_sister) print ", with a cup of hot tea on it. ";
  				else print ". ";}!endif
  			print "The door to the west leads out, and the one to the north leads
  			to the kitchen.";
  			if (sister_bedroom hasnt visited) " There is also a door in the south wall, but you don't know where
  			that goes.";
  			else " To the south is the bedroom door.";],
  	before [; Go, Exit, Enter: if (~~sister.is_gone){ 
					if (~~sister in inside_sister) "She said she would bring some tea. You
					should probably wait.";
  					else "In the middle of the conversation? That would be pretty rude.";}
  					],
  	w_to house_door,
  	n_to inside_sister_door,
  	s_to sister_bedroom_door;
  			
Furniture sister_table "table" inside_sister
  with	name  'small' 'table',
  	description [; print "A small table. "; 
  			if (teatray in sister_table) print "There is a tray on it.^";
  			else "There will be some tea on it soon enough.";
  			]
  	has transparent;
  	
Furniture sister_clock "clock" inside_sister
  with  name 'grandfather' 'clock' 'tick' 'tock',
	description "A charming old fashioned grandfather clock. Its soothing noise makes
	you feel at home and relaxed. A dangerous feeling. You remind yourself to stay alert.",
	react_before [; Listen: "You hear the ticking of the clock. ";];
	
Furniture sister_carpet "carpet" inside_sister
  with	name "carpet" "floor",
  	description "A nice carpet with a floral pattern, giving the place a homely atmosphere. You really should
  	get a carpet for your office after you finish this case. Staring at the
  	empty floor every day is getting a bit depressing.";
  
Furniture sister_chair "chair" inside_sister
  with	name 'chair' 'comfy',
  	description "It's a soft comfy chair.",
  has 	enterable;
  
Furniture sister_couch "couch" inside_sister
  with	name 'leather' 'comfy' 'couch',
  	description "A nice expensive leather couch.",
  	react_before [; Open: if (~~sister.is_gone){
			if (~~sister in inside_sister) "She said whe would bring some tea soon. You should probably wait.";
			else "In the middle of the conversation? That would be pretty rude.";} ],
  has	enterable;

Normal_door inside_sister_door "kitchen door"
  with  pname 'door' 'north' 'kitchen',
	description "A sturdy door. It leads to the kitchen.",
	door_dir [; if (location == inside_sister) return n_to;
		    else return s_to;
		    ],		   
	door_to [; if (location == inside_sister) return sister_kitchen;
			else return inside_sister;
			],
	found_in  inside_sister sister_kitchen,
  has 	scenery transparent door openable ~open;

Object  teatray "tray"
  with  name 'tray' 'teatray',
  	before [; Take: "Don't be silly. What do you need a tray for?";],
  	description [;print "A nice wooden tray. ";
  			if (teacup in location) print "There is a steaming hot cup of tea on it.^";
  			else print "There is nothing on it.^";],
  has 	scenery transparent concealed;
 
Object  teacup "cup of tea"
  with  name 'tea' 'cup' 'teacup',
  	description "A cup of very hot tea. It is made of fine china with a picture of a
  	blue dragon painted on it.",
  	has_drunk false,
  	teacup_sleep 0,
  	daemon [; 	self.teacup_sleep++;
  			switch(self.teacup_sleep) {
	                3: "^You feel sleepy.";
	                6: "^You struggle to keep your eyes open.";
	                7: StopDaemon(teacup); StopDaemon(sister); print "^Allyson is saying something, but you can't make out the words.
	                There is just the pleasant drone of her voice. The soothing 'tick tock' 
	                of the clock in the background. The comfortable softness of the chair.
	                ^^You sleep.^^
	                When you wake up, you are in a completely different location, lying on a cold floor.
	                You get up, brushing the dust off your suit.";	                
	                move warehouse_allyson to inside_warehouse; PlayerTo(inside_warehouse);
	                StartDaemon(adam_wayne);
	                rtrue;}! end switch
		],
  	before [; Drink: if (sister.has_gun) {
			print "You sip your tea. It has a interesting spicy taste. 
				Seconds tick by as you stare down the barrel of the gun.
				You start feeling sleepy. Allyson is saying something, but you can't make out the words.
	                	There is just the pleasant drone of her voice. The soothing 'tick tock' 
	                	of the clock in the background. The comfortable softness of the chair.
	                	^^You sleep.^^
	                	When you wake up, you are in a completely different location, lying on a cold floor.
	                	You get up, brushing the dust of your suit."; StopDaemon(sister); 
	                	move warehouse_allyson to inside_warehouse; PlayerTo(inside_warehouse);
	                	StartDaemon(adam_wayne); rtrue;			
  					}!endif
  			StartDaemon(teacup); self.has_drunk = true; "You sip your tea. It has a interesting spicy taste."; 
  		  Drop: move teacup to inside_sister; "You put the teacup back on the tray.";
  		  Take: move teacup to player; if (sister.sister_action > 16) sister.sister_action--;
  		  	"You carefully pick up the teacup, making sure not to
  		  	spill the hot contents.";
  		  Throw: if (sister in location) {<<ThrowAt teacup sister>>;}
  		  ThrowAt: if (second == sister) {if (sister.has_gun) {print "^Acting quickly, you throw the hot tea in her face.
  		  She yells in pain, and reflexively raises her hands to her face.^";
  		  if (sister.sister_action > 16) sister.sister_action--; 
  		  sister.is_distracted = true; remove teacup; rtrue;
				}!end if gun
			}!end if sister
  		  ],
  has	transparent concealed;



Object	dragon "picture of a dragon" teacup
  with	name 'dragon' 'picture' 'drawing',
  	description "A stylised picture of a smiling blue dragon is drawn on the teacup.";

Object 	sister "Allyson" 
  with 	name 'sister' 'allyson' 'stiles' 'her' 'chick' 'bitch',
  	description "A short brunette of above average looks. She has a beautiful smile
  	and shoulder long chestnut hair. Something about her disturbs you though. 
  	Her piercing blue eyes seem to look right through you, clashing with her
  	amiable demeanor.",
  	ask_brother 0,
  	ask_book 0,
  	ask_argument 0,
  	ask_job 0,
  	ask_house,
  	ask_father 0,
  	ask_mother 0,
  	sister_action 0,
  	is_distracted false,
  	is_gone false,
  	is_dead false,
  	ask_tea false,
  	initial [; "Allyson is sitting on the couch, looking at you expectantly.";],
  	life[word_number i;  Attack: if (~~self.has_gun) {
					if (teacup.has_drunk) "Your gun feels much too heavy to lift...";
					"She is no threat at the moment. You don't use violence
  					if it isn't necessary.";}
  		else { if (self.is_distracted) {remove sister;
  						self.is_gone = true; self.is_dead = true;
  				move sister_corpse to inside_sister; StopDaemon(sister);
  				"While she is distracted you pull out your gun and
  				quickly plant a bullet in her head. You were trained to shoot to kill.
  				Her dead body slides off the couch and drops limply onto the floor.";
				}! endifdis
			else {print "You reach for your gun, but she is too fast. Before you can
					get it out you have already been shot several times. If only you could
					have distracted her somehow...";
					deadflag = 3; rtrue;}
			}! end else
		Answer: switch(noun){
				'yes': <<Yes>>;
				'no': <<No>>;
				'hello', 'hi', 'greetings':
				"~Good morning, Miss Stiles~, you say. ~I hope this is not too inconvenient?~^^
				~Not at all!, she laughs. ~I was looking for an excuse not to work anyway.~";
				'goodbye', 'adieu':
				"~Goodbye.~^^
				~Leaving so soon? Why don't you finish your tea first...~";}
		Tell:	wn = consult_from;
			word_number = consult_words;
			for (i = 1: i <= word_number: i++){
			switch(NextWord()){
				'brother', 'subject', 'andrew', 'stiles':
					AskBrother();
					if (self.ask_brother < 3) rtrue;
					}! end switch
						}! end for
  		Ask: if (self.has_gun) "The time for questions is over. You need to act.";
  			wn = consult_from;
			word_number = consult_words;
			for (i = 1: i <= word_number: i++) {
			switch(NextWord()) {
		 'father', 'dad':
		 	self.ask_father++;
		 	switch(self.ask_father){
				1: "^~Do you remember your father?~^^
					She looks puzzled. ~Yes, he was a very good man. But I don't
					see what he has to do with anything.~";
				default: rfalse;}
		 'mother', 'mum', 'rebecca', 'vacation':
		 	self.ask_mother++;
		 	switch(self.ask_mother){
				1: "^~Do you visit your mother often?~^^
					~Yes, of course. Even Andrew does that. The poor woman
					just stays in her apartment all day long. I have been trying to
					convince her to take a vacation, but she refuses to leave.~";
				2: "^~Why do you want your mother to take a vacation?~^^
					~She needs to get out more, I think. It must be depressing to do nothing
					all day.~";
				default: rfalse;}
  		 'brother', 'subject', 'andrew', 'stiles':
			AskBrother();
			if (self.ask_brother < 3) rtrue;
		 	'book', 'writing', 'draft', 'first':
		 		self.ask_book++;
		 		switch(self.ask_book) {
					1: "^~Do you have a copy of his book?~^^
					~Sure. I will get it for you when we have finished our tea.~";
					2: "^~What did you think of the book?~^^
					She shakes her head derisively. ~It's the same drivel he has been spouting
					since we were kids. Government is bad, and we should all go live in
					anarcho-syndicalist communes in the woods. I wish he would just grow up.
					He is not a bad person, you know. I hope you aren't being too rough on him?~";
					3: "^~About the book...~^^She smiles again. 
					~Patience! I said I would get it when we are done with the tea.~
					She sips her tea, and looks at you expectantly.";
					}!end book
			'argument', 'discussion', 'fight':
				self.ask_argument++;
				switch(self.ask_argument) {
					1: "^~I understand you had an argument with your brother some years ago. Can you tell
					me anything about that?~^^
					~Oh, we have been arguing all our lives. This time he just got madder than usual, 
					and we both said some nasty things. I don't really mind, but he hasn't talked 
					to me since.~";
					2: "^~I know you aren't seeing each other, but what was the argument about?~^^
					~Oh, just the usual stuff. The evils of government blahblahblah.~
					She laughs.
					~He thinks I am supporting The Man, you know. Because of my job.~";
					}! end argument
			'job', 'work', 'programmer':
				self.ask_job++;
				switch(self.ask_job) {
					1: "^~What is it that you do, exactly?~^^
					~I am a programmer. I work for VisionTech, designing security systems.
					I believe your agency actually utilizes some of our technology.~";
					2: "~Do you enjoy your job?~^^^
					She smiles once again. ~Of course! I love to know that I am 
					helping people feel safe and secure!~";
					}!end askjob
			'house', 'home', 'suburbs', 'money', 'income':
				self.ask_house++;
				switch(self.ask_house) {
					1: "^~You have a very nice house. It must have cost a lot to buy a
					place in the suburbs. How can you afford it?~^^
					~I am very good at my job!~, she replies proudly.";
					2: "^~I don't doubt that you are good at your job.
					However, programmers still don't make that much. Do
					you have another source of income?~^^
					A pause. ~No, I just save up rather than spending my 
					paycheck right away. A bit of frugality is all you need to
					do well in life.~";
					}! end askhouse
			}!end ask switch
			}!end for
						],!end life
	has_gun false,
	before [; Hit: if (sister.has_gun) {print "You try to take her out with a quick punch, but her
			gun is quicker. That was pretty dumb.^"; deadflag = 3; rtrue;}
		print "^You are tired of playing games. A single well placed punch send her tumbling out of the
		couch with a broken nose. ~Tell me what you know, bitch~, you say coldly. ~I know you are hiding something~.
		^^She has a gun in her hand. You didn't see her pull it out at all. Impressive. 
		You try to reach for your own, but it is too late."; deadflag = 3; rtrue;],
	react_before [; Yes: if (self.ask_tea) "She smiles. ~Great! I'm sure you will like it.~";
			No: if (self.ask_tea) "~That was a rhetorical question~, she says impatiently.
						~Please drink your tea. I made it just for you.~";],
 	daemon	[; self.sister_action++;
 			switch(self.sister_action) {
			5: move sister to inside_sister;
			   move teatray to sister_table;
			   move teacup to inside_sister;
			   print "^Allyson reenters the room carrying a tray with two cups of nice hot tea on it.
			   She puts it on the table, sits down on the couch and picks up one of the cups herself.
			   ^^~Now, what did you need to talk to me about?~^";
			6: "^Allyson sips her tea. She sighs contentedly.";
			9: if (~~teacup.has_drunk) {
					self.ask_tea = true; "^~Aren't you going to drink your tea?~";}
			12: if (~~teacup.has_drunk) "^She looks annoyed. ~Your tea is getting cold.
				Aren't you going to drink it?~";
			15: if (~~teacup.has_drunk) {self.has_gun = true; move sister_gun to inside_sister;
				"^She suddenly has a gun in her hand. You didn't see her
				pull it out at all. Impressive. ~Drink the fucking tea, Agent~, she sneers.
				~I spent a lot of time preparing it~. She smiles. Those annoyingly perfect teeth again.
				Who is this woman?";}!endif 
			18: if (~~teacup.has_drunk && ~~self.is_distracted) {print "She sighs. ~We were hoping to take you alive.
				But I guess we can deal~. She pulls the trigger, and the bullet hits you 
				right between the eyes."; deadflag = 3;
					}!endif
				else {if (self.is_distracted) {self.sister_action--; self.is_distracted = false;}}!end else
						}!end switch
				],
  has	animate proper female;
  
  
[ AskBrother; 
  			sister.ask_brother++;
  			switch(sister.ask_brother) {
				1: "^~I am afraid I have some bad news about your brother~, you say. 
				~He has recently been arrested for disloyal activities. We fear
				he might have connections to anti-union terrorist groups.~
				^^~I feared something like this might happen someday~,
				she replies sadly. ~He was always a troublemaker. I am afraid there
				is not much I can do to help. He and I haven't had much contact in recent years.~";
				2: "^~Haven't you spoken to him at all?~^^
				She thinks for a moment. ~He did ask me to read the first draft for his book.
				Apparently, he thought it would be useful to get feedback from someone who disagreed
				with its conclusions.~";
				default: rfalse;
				}!end brother
];

Object 	sister_gun "allyson's gun" 
  with	pname '.x' 'sisters' '.x' 'allysons' '.x' 'revolver' 'gun' ,
  	description "A small .38 revolver.",
  	initial [; rtrue; ],
  	before [; Take: if (sister.is_distracted) { move sister_gun to player;
  			move warehouse_allyson to inside_warehouse;
  			remove sister; sister.is_gone = true; StopDaemon(sister);
  			move follow_sister to location;
  			"Time to act quickly. While she is distracted you manage to tear the gun from her hands.
  			Unfortunately, she quickly recovers and delivers a swift kick to your groin, causing
  			you to double over in pain. When you recover she has already escaped through the front door.
  			Dammit.";
				}
		else print "You reach for her gun, but she shoots you before you can get it.";
			deadflag = 3; rtrue;
		
  					],
  has	proper;
  
Object 	follow_sister	"Allyson"
  with 	name 'sister' 'allyson' 'stiles' 'her' 'chick' 'bitch',
  	description "Allyson is no longer here.",
  	before [; Follow: print "You pursue Allyson through the front door.";
  				PlayerTo(outside_sister); rtrue;
  		default: "Allyson is no longer here.";],
  has	scenery proper;

Object 	sister_corpse "Allyson's corpse"
  with	name 'corpse' 'allyson' 'sister' 'allysons' 'sisters' 'body' 'dead' 'sister' 'bitch' 'her',
  	description "Allyson's limp lifeless body. Blood is oozing from the gunshot wounds.",
  	has_searched false,
  	initial [; "Allyson's limp corpse lies sprawled on the carpet.";],
  	before [; Take: "The body is much too heavy for you to carry.";
  		  Search: if (self.has_searched) rfalse; 
  		  	move sister_card to player;
  		  	"In one of the trouser pockets, you discover a small white card with a magnetic stripe on the back.
  		  	It looks like a access card of some kind. You put it in your own pocket.
  		  	It might come in handy.";	],
  has	proper;
  
Object	sister_card "magnetic card"
  with	name 'magnetic' 'access' 'white' 'small' 'card',
  	description "A small magnetic card. It is uniformly white, except for the black magnetic stripe
  			on one side.";

Normal_door sister_bedroom_door "south door"
  with  pname 'door' 'south' 'bedroom',
	description "A sturdy door in the south wall.",
	door_dir [; if (location == inside_sister) return s_to;
		    else return n_to;
		    ],		   
	door_to [; if (location == inside_sister) return sister_bedroom;
			else return inside_sister;
			],
	found_in  inside_sister sister_bedroom,
  has 	scenery transparent door openable ~open;

!-------------------------------------------------------------------------------

Room 	sister_kitchen "The Kitchen"
  with 	description [;print "The kitchen is a very messy place with pots and pans
  			stacked haphazardly everywhere. A teapot is standing next
  			to the sink, with a smidgen of tea still left at the buttom.
  			Above the sink";
  			if (first_cabinet has open) print " an open cabinet displays a large selection
  			of cups and glasses.";
  			else print " is a closed cabinet.";
  			if (second_cabinet has open) " Another open cabinet is situated next to it.
  			It contains several stacks of plates, as well as some pots and pans.";
  			else " Another closed cabinet is situated
  			next to it.";],
  	s_to inside_sister_door;
  	
Object 	sister_teapot "teapot" sister_kitchen 
  with	name 'teapot' 'tea' 'pot' 'clay' 'green',
  	description "It is a green clay teapot. There is a small amount of lukewarm tea left at
  			the buttom. ",
  	before [; Take: "You would look pretty silly carrying a teapot around with you.
  			It's not like it can fit in your pockets.";
  		  Drink: "You suspect that the tea is drugged. She seemed excessively eager for you to drink it.
  		  	It's probably best not to touch it. The lab can have it analysed later.";
  		  Receive: if (noun has general) {remove noun; "You put the pill into the teapot, where
  		  		it quickly dissolves, leaving no visible trace.";}],
  has	container scenery;
  
Furniture sister_sink "sink" sister_kitchen
  with	name 'sink',
  	description "A shiny metal sink with a tap.";
  	
Object	sister_tap "tap" sister_kitchen
  with	name 'tap',
	description "A tap capable of producing water if you turn it on.",
	before [; SwitchOn: if (sister_glass in player) <<Fill sister_glass>>;
				else "You don't feel like drinking directly from the tap.";],
  has	switchable ~on scenery;
  
Object	first_cabinet "first cabinet" sister_kitchen
  with	name 'first' 'cabinet' 'cabinets',
  	description [; print "A kitchen cabinet.";
  			if (first_cabinet has open) " It is currently open,
  			displaying a large selection of various cups and glasses.";
  			else " It is currently closed.";],
  	before	[; Search: "You search the first cabinet thoroughly, but find nothing
  			except even more cups and glasses.";],
  has	openable ~open container scenery;
  			
Object	second_cabinet "second cabinet" sister_kitchen
  with	name 'second' 'cabinet' 'cabinets',
  	description [; print "A kitchen cabinet.";
  			if (second_cabinet has open) " It is currently open,
  			displaying a large selection of plates, pots and pans.";
  			else " It is currently closed.";],
  	before [; Search: if (~~pillglass.has_taken) {move pillglass to player; 
  			!give pillglass ~concealed;
  			"Behind a stack of plates you discover a small unlabeled glass
  				of white pills. You stuff it in your pocket for later analysis.";}
  			else "You don't find anything else of interest."; ! end search
  			Take: "You don't need any of that stuff.";],
  	after [; Open: "You open the cabinet, revealing a large selection of plates, pots and pans.";],
  has	openable ~open container scenery;
  
Object	potpans "pots and pans" sister_kitchen
  with	name 'pots' 'pans' 'and' 'pot' 'pan' 'plate' 'plates',
  	description "Pots and pans are scattered all over the kitchen. Several more are stored
  			in the second cabinet, along with some plates.",
  	before [; Take: "You don't need that. You have no desire to start cooking at the moment.";],
  has 	scenery;
  
Object	sister_cups "cups and glasses" first_cabinet
  with	name 'cups' 'and' 'glasses' 'contents' 'glass',
  	description "A collection of fine cups and glasses of various sorts. Including
  			several kinds of porcelain teacups like the one she gave you.",
  	before	[; Take: if (~~sister_glass.glass_taken) <<Take sister_glass>>;
  			else 
  			"You don't need anything else from the cabinet.";
  									],
has	scenery;
  	
Object	sister_glass "cup" first_cabinet
  with	name 'small' 'ordinary' 'cup',
	description [;print "A small cup"; if (tap_water in sister_glass) " filled with water."; else ".";],
	glass_taken false,
	!glass_full false,
	before [;Take: 
				if (~~self.glass_taken) {move sister_glass to player; 
  				self.glass_taken = true; 
  				!give sister_glass ~concealed;
  				"You decide to take the small cup.";}! end glasstaken
  		Fill: if (tap_water in sister_glass) "The cup is already full.";
  			if (location == sister_kitchen) {
				move tap_water to sister_glass; 
				"You turn on the tap and fill the cup with water.";}
		Receive: if (noun has general) {
				if (tap_water in sister_glass) 
					{remove noun; tap_water.is_poisoned = true;
						"You put the pill in the cup where it quickly dissolves
						leaving no visible trace.";} ! end if full
				else "Why put a pill in an empty cup?";} !end general
				"Why put that in the cup?";
		LetGo: if (noun == tap_water && location == sister_kitchen) 
			{remove tap_water; tap_water.is_poisoned = false;
				"You pour the water into the sink";}! end if
			else "That would cause a mess.";
		Drink: if (tap_water in self) <<Drink tap_water>>;
  		],
  has	container open transparent;
	
Object	pillglass "glass of pills"
  with	name 'glass' 'of' 'pills' 'pillglass' 'medicine' 'poison',
  	has_taken false,
  	description "A small unlabeled glass container.",
  	before [; Enter: "Don't be silly!";
  		Receive: if (noun hasnt general) "Don't be silly. That's way too big to fit in the pillglass.";],
  has	transparent container open;

Object	tap_water "some water"
  with	name 'water' 'tapwater' 'fluid' 'cool',
  	description "Plain old tap water",
  	is_poisoned false,
  	before [;  Drink: 
			if (self.is_poisoned) 
				"That would probably not be prudent. You have no idea what the pill does.";
			else  remove tap_water; self.is_poisoned = false;
				"The water is cool and refreshing";],
  has	proper;
 
Class   pill(10)
  with	name 'white' 'pill' 'tablet' 'poison' 'pills/p' 'tablets/p',
  	short_name "white pill",
  	plural "white pills",
  	before [; Eat: "That would probably not be prudent. You have no idea what it does.";],
  has	edible general;
  	
pill single_pill1 pillglass;
pill single_pill2 pillglass;
pill single_pill3 pillglass;
pill single_pill4 pillglass;
pill single_pill5 pillglass;
pill single_pill6 pillglass;
pill single_pill7 pillglass;
pill single_pill8 pillglass;
pill single_pill9 pillglass;
pill single_pill10 pillglass;



  
	
!Object 	single_pill "white pill" pillglass
!  with	name 'pill' 'tablet' 'white',
!  	before [new_pill;
!  			Take: new_pill = pill.create();
!  			if (new_pill == nothing) "There are no more pills in the glass.";
!  			move new_pill to location; <<Take new_pill>>;],
!  has	edible;



!------------------------------------------------------------------------------!

Room 	sister_bedroom "Bedroom"
  with	description [;print "This seems to be the bedroom. 
  	There is a large bed
  	by the east wall, with a small table next to it. ";
  	if (sister_book in sister_bed_table) print "A book lies on the table
  	with a bookmark placed approximately halfway through. ";
  	print "A small bookshelf
  	stands south of the bed.
  	Above the bookshelf is a large window ";
  	if (sister_bed_curtains hasnt open) "with the curtains drawn,
  	making the room a bit dark.";
  	else "through which sunlight is streaming into the room.";],
	n_to sister_bedroom_door;

Furniture sister_bed "bed" sister_bedroom
  with	name 'bed' 'large',
  	description "A large comfy looking bed. It doesn't look like anyone has used it recently, though.";

Furniture sister_bed_table "table" sister_bedroom
  with	name 'table',
  	before [; Search: if (sister_book in self) "On the table is a crime novel, with a bookmark in it.";],
  	description [; print "A small table standing next to the bed.";
  				if (sister_book in self) print " A book lies on top of it.";
  			"";];
  			

Object sister_book "crime novel" sister_bed_table
  with	name 'book' 'crime' 'novel',
  	description "A cheap paperback novel titled 'Murder in the Greenhouse' by 
  	someone named David Palmers. There is a bookmark placed halfway through.",
  	before [; Read: "The book tells a lurid story about a dead body being found
  			in a greenhouse belonging to a hedonistic earl.";
  		  Take: give sister_book ~concealed;
  		  Consult: 	wn = consult_from;
				switch(NextWord()) {
					'code': "You will have to be more specific.";
					'26-5-9', '26': "You look up page 26, line 5, word number 9.
					The word is ~detective~.";
					'17-8-3', '17': "You look up page 17, line 8, word number 3.
					The word is ~blood~.";
					'109-12-4', '109': "You look up page 109, line 12, word number 4.
					The word is ~murder~.";
					}],
  has	legible transparent concealed;

Object bookmark "bookmark" sister_book
  with	name 'mark' 'bookmark',
  	description "You open the book at the place marked by the bookmark. At
  		this point in the plot, Dr. Hulot, the Swiss master detective,
  		has just discovered that the earl is an imposter!",
  	before [; Take: "Nah. You decide to leave it in the book.
  			Maybe the passage in question is a code of some kind?";];
  		
Furniture sister_bookshelf "bookshelf" sister_bedroom
  with	name 'shelf' 'bookshelf',
  	has_examined false,
  	before [; Search: if (~~self.has_examined) {
					<<Examine self>>;}],
  	description [;print "The bookshelf contains a small number of different books. Scanning the covers quickly,
  		you find that they seem to consist of a few novels and some programming textbooks. ";
  		if (sister_bed_curtains hasnt open) print "You can't quite make out the titles in the dark. ";
  		else if (~~self.has_examined) {print "A familiar title catches your attention. Among the textbooks you discover 
  		a copy of ~Internal Vigilance~ by Andrew Stiles.";
  		move internal_vigilance to sister_bedroom; self.has_examined = true;}
  		"";
  			];

Object 	internal_vigilance "'Internal Vigilance'" 
  with	name 'book' 'internal' 'vigilance',
  	has_read false,
  	describe [; "In the bookshelf is a copy of ~Internal Vigilance~.";],
  	before [; Take: self.describe = "^You see a copy of ~Internal Vigilance~ here."; 
  			print "You take the book.^"; rfalse;
  		Read: "You skim the book, to get a sense of the contents. It contains
  			a long discussion of the value of individual liberty, and the primacy
  			of freedom over safety. People who would give up any amount of freedom
  			in favour of safety or comfort are derided as contemptible sheep. 
  			Anarchy is the only solution.^^Nothing surprising.";
		Consult: 	wn = consult_from;
				switch(NextWord()) {
					'code': "You will have to be more specific.";
					'26-5-9', '26':
						"You look up page 26, line 5, word number 9.
						The word is ~freedom~.";
					'17-8-3', '17': 
						"You look up page 17, line 8, word number 3.
						The word is ~from~.";
					'109-12-4', '109': 
						self.has_read = true;
						"You look up page 109, line 12, word number 4.
						The word is ~protection~.";
					}],
  	description "A softcover edition of ~Internal Vigilance~ by Andrew Stiles.",
  has	legible proper;

Object 	sister_bed_window "window" sister_bedroom
  with  pname  '.x' 'bedroom' 'window' 'windows',
  	description [; if (sister_bed_curtains has open) "Through the window
  			you can see the house next door. It looks just like this one.";
  			else "You can't see anything through the curtains.";],
  	before [;Open: if (sister_bed_curtains has open) {print "You open the window, letting some fresh air into the bedroom.^";
  				give bed_window open; rtrue;}
  			else "You can't open the window when the curtains are drawn.";],
  has openable ~open scenery;


Object 	sister_bed_curtains "curtains" sister_bedroom
  with	pname 'curtains' 'curtain',
  	description "White curtains, designed to block out as much of the sunlight as possible.",
  	after [; Open: print "You open the curtains letting the sunlight into the bedroom,
  			chasing away the dark.";
  		!	if (~~sister_wallet.is_found) {print " Now that the room is no longer dark, you suddenly
  		!	notice, that a wallet seems to have been forgotten in the corner of the room.
  		!	Someone must have dropped
  		!	it there by accident."; move sister_wallet to location; sister_wallet.is_found = true;}
  			"";],
  	before [; Close: if (bed_window has open) "You can't draw the curtains when the window is open.";
  			else {print "You draw the curtains again, blocking your view, and letting the darkness
  			seep into the room again.^";
  				give sister_bed_curtains ~open; rtrue;}],
  has 	openable ~open scenery pluralname;

!Object 	sister_wallet "wallet"
!  with	name 'wallet',
!  	is_found false,
!	description "A brown leather wallet.",
!  has	container open;
  
!Object 	sister_credit "credit card" sister_wallet
!  with 	name 'credit' 'card' 'creditcard' 'money',
!  	description "A credit card with a magnetic stripe and a small picture of Allyson Stiles.",
!  has	transparent;
  
!Object	sister_picture "credit card picture" sister_credit
!  with	name 'picture' 'image',
!  	description "It is a small black and white picture showing a younger version of Allyson.";

!------------------------------------------------------------------------------!

Room 	mother "Outside an apartment building"
  with	description "This is supposedly where the subject's mother lives. A nondescript
  		apartment building in the middle of The City. Above you, the rows of windows 
  		disappear into the clouds. Luckily, the address you have is for the ground floor.
  		Apartment 24.^
  		To the east, a large door leads into the building.",
  	in_to [; <<Enter car>>;],
  	e_to mother_door;
  		
Normal_door mother_door "door" mother
  with	name 'door' 'metal' 'entrance',
  		door_dir e_to,   
		door_to [; print "You enter the building and manage to find the apartment you are looking for.^"; 
				give self ~open;
				return mother_hallway;],
	before [; Open, Close: "There is no discernible handle of any kind.";],
  	description "A metal door with a security camera hanging above it, pointing resolutely to the area
  		in front of the door. There is no handle of any kind. Next to the door is a keypad, 
  		with a speaker located above it.",
  	found_in mother,
  has	scenery door openable ~open;
  
Array called_rooms --> 1000;
  
Object 	keypad "keypad" mother
  with	name 'keypad' 'key' 'pad' 'buttons',
  	call_num 0,
  	push_num 100,
  	digit_num 0,
  	button_num 1,
  	description "A keypad, allowing you to enter the numbers from one to nine.
  			A tenth, larger, button is labeled ~Enter~.",
  has	scenery transparent;
  
Class	keypad_button
 with	before [; Enter: <<Push self>>;];

keypad_button	button_0 "zero button" keypad
  with	name '0//' 'zero' 'button' 'zeroth',
  	before [; Push: keypad.button_num = 0;
  			push_button();
  			rtrue;
  			],
  	description "A square button numbered ~0~.",
  has	scenery;

keypad_button	button_1 "first button" keypad
  with	name '1//' 'one' 'button' 'first',
  	before [; Push: keypad.button_num = 1;
  			push_button();
  			rtrue;
  			],
  	description "A square button numbered ~1~.",
  has	scenery;
  
keypad_button	button_2 "second button" keypad
  with	name '2//' 'two' 'button' 'second',
  	before [; Push: keypad.button_num = 2;
  			push_button();
  			rtrue;
  			],
  	description "A square button numbered ~2~.",
  has	scenery;
  
keypad_button	button_3 "third button" keypad
  with	name '3//' 'three' 'button' 'third',
  	before [; Push: keypad.button_num = 3;
  			push_button();
  			rtrue;
  			],
  	description "A square button numbered ~3~.",
  has	scenery;
  
keypad_button	button_4 "fourth button" keypad
  with	name '4//' 'four' 'button' 'fourth',
  	before [; Push: keypad.button_num = 4;
  			push_button();
  			rtrue;
  			],
  	description "A square button numbered ~4~.",
  has	scenery;
  
keypad_button	button_5 "fifth button" keypad
  with	name '5//' 'five' 'button' 'fifth',
  	before [; Push: keypad.button_num = 5;
  			push_button();
  			rtrue;
  			],
  	description "A square button numbered ~5~.",
  has	scenery;
  
keypad_button	button_6 "sixth button" keypad
  with	name '6//' 'six' 'button' 'sixth',
  	before [; Push: keypad.button_num = 6;
  			push_button();
  			rtrue;
  			],
  	description "A square button numbered ~6~.",
  has	scenery;
  
keypad_button	button_7 "seventh button" keypad
  with	name '7//' 'seven' 'button' 'seventh',
  	before [; Push: keypad.button_num = 7;
  			push_button();
  			rtrue;
  			],
  	description "A square button numbered ~7~.",
  has	scenery;
  
keypad_button	button_8 "eighth button" keypad
  with	name '8//' 'eighth' 'button',
  	before [; Push: keypad.button_num = 8;
  			push_button();
  			rtrue;
  			],
  	description "A square button numbered ~8~.",
  has	scenery;
  
keypad_button	button_9 "ninth button" keypad
  with	name '9//' 'nine' 'button' 'ninth',
  	before [; Push: keypad.button_num = 9;
  			push_button();
  			rtrue;
  			],
  	description "A square button numbered ~9~.",
  has	scenery;
  
keypad_button	call_button "Enter button" keypad
  with	name 'enter' 'button' 'return' 'call',
  	before [; Push: 
  		if (keypad.digit_num == 0) "An emotionless female voice issues from the speaker:
  					~Please enter an apartment number between 1 and 999, then press Enter.~";
   		if (keypad.digit_num == 1) keypad.call_num = keypad.call_num/100;
  		if (keypad.digit_num == 2) keypad.call_num = keypad.call_num/10;
  		keypad.push_num = 100;
  		if (called_rooms-->keypad.call_num == 1) {
			keypad.call_num = 0; keypad.digit_num = 0;
			"You already called that apartment. No need to do it again.";}
		else called_rooms-->keypad.call_num = 1;
  		print "You press the Enter button. A moment later a neutral female voice issue from the
  		speaker: ~Calling apartment ", keypad.call_num, "...~^^";
  		room_call();
  		keypad.call_num = 0; keypad.digit_num = 0; rtrue;],
  	description "A slightly larger rectangular button labeled ~Enter~.",
  has	scenery;
  
[ push_button;
  	keypad.call_num = keypad.call_num + (keypad.button_num * keypad.push_num);
  	keypad.push_num = (keypad.push_num / 10);
  	keypad.digit_num++;
  	print "You push the button labeled ~", keypad.button_num, "~. A small ~beep~ issues from the speaker.^";	
  	if (keypad.digit_num == 4) Too_many();
];
  
[ Too_many; 
	keypad.push_num = 100;
	keypad.digit_num = 0;
	keypad.call_num = 0;
	"A neutral female voice issues from the speaker: 
	~The number you have entered is too large. Please enter a apartment number
	between 1 and 999, and press Enter.~"; 
];

[ room_call random_call;
	if (keypad.call_num == 24) "You wait for a while, but there is no response. It seems like
					no one is home.";
	mother_camera.is_watching = true;
	random_call = shuffle(7);
	switch(random_call) {
		1: "You wait for a while, but there is no response. It seems like
					no one is home.";
		2: "After a short pause, a coarse male voice emerges from the speaker. 
			~Who the hell are you? Do I know you?~^^
			~This is Agent Dinnings from Agency', you reply tersely. 
			'I need to enter this building as part of an investigation.
			Please open the door.~^^
			~Oh, yeah? Anyone can make that claim. Prove it!~";
		3: "~Hello there!~, a female voice purrs from the speaker.^^
			~Who is it~, another woman asks. ~Wow! I didn't realise
			you were having such a handsome guest for dinner, Belinda!~^^
			~That's not Marcus! I have never seen him before in my life!~^^
			~Excuse me~, you interrupt their little discussion.
			~I am not your guest. I am Agent Dinnings from Agency. I am in the middle
			of an important investigation. I need you to let me in~^^
			~Oh, do you?~, replies Belinda inquisitively. ~How do we know
			you are really an Agent and not some shady criminal trying to trick
			us innocent girls?~";
		4: "Soft slow music plays from the speaker followed by a male voice.^^
			~Good evening. I am afraid we do not admit single guys at the club.
			Do you have a partner?~^^
			~Good evening~, you reply politely. ~This is Agent Dinnings from Agency.
			I need to investigate this building. Will you let me in?~^^
			~Hmmmm. How do I know you are not just some loser trying to crash the party?~";
		5: "LOUD music roars from the speaker followed by some inaudible mumbling.
			You can just barely make out someone yelling ~Dude! Turn down the music.~
			The volume of the music decreases to a more acceptable level.
			~Dude, I think you have the wrong place~, a voice announces from the speaker.^^
			~That's okay~, you reply. ~I just need you to open the front door.
			This is Agent Dinnings from Agency...~ You never get a chance to finish.^^
			~Dude!~, the voice says with alarm. ~There are no drugs at this party.
			It's totally legit and everything!~^^
			~I am sure it is. However, I need to enter this building as part of an ongoing investigation.
			Please let me in.~^^
			~Yeah, right. Any dude can dress up in a suit to look respectable. Prove it!~";
		6: "~Hi!~. The voice of a small boy sounds from the speaker, followed shortly by a shrill
			female voice. ~Tom! How many times have I told you not to answer the door!
			Get back in your room immediately! Excuse me, sir. Can I help you?~^^
			~Yes, I believe you can, ma'am~, you reply. ~The Agency is investigating this building.
			I need you to let me in.~^^
			~The Agency you say? How do I know you are not just a burglar? Show me some proof!~";
		7: "~I don't want to buy anything~, a male voice says tersely from the speaker.
			~Try somewhere else~.^^~Sir, I am not here to sell anything~, you reply. 
			~I need to enter this building as part of an Agency investigation. If you will
			just open the door, I will leave you alone.~^^
			~Anyone can claim that! Show me some proof.~";
		default: "There is no reply";}
];

Furniture mother_camera "security camera" mother
  with	name 'security' 'camera' 'surveillance' 'person' 'tenant',
  	is_watching false,
  	life [; Show: if (self.is_watching && noun == badge) {
			give mother_door open; self.is_watching = false;
			"You hold the badge in front of the camera for a few moments.
			A short while later, there is a buzzing sound, and the door slides
			open.";
  				}
  			],
  	description "A old fashioned non-hidden security camera.
  			It is probably used by people inside to see who is calling.",
  has	animate;
!------------------------------------------------------------------------------!

Room 	mother_hallway "A hallway in the apartment bulding"
  with	description "You are standing in a hallway east of the door to apartment 24,
  			where the subject's mother supposedly lives. 
  			The hallway stops abruptly a few feet to the north. This is the final door.
  			The front door lies
  			to the south. ",
  	w_to mother_apartment_door,
  	s_to [;print "You exit the building the way you came. The front door closes silently behind you.^";
  			return mother;];

!Object dust "dust" mother_hallway
!  with	name 'dust' 'dusty' 'floor',	
!  	description "The floor is covered with dust. You estimate, that it must be several
!  			weeks since anyone else has been here.",
!  has	scenery;
  			
Normal_door mother_apartment_door "door" mother_hallway
  with	name 'door' 'apartment',
  	description [; if (location == mother_hallway) "Like with the front door, there is no handle of any kind on the door.
			The dull monotony of the surface is only broken by a plaque with
			the number ~24~ emblazoned on it.
  			A card reader sits on the wall next to the door.";
  			else "The door looks the same from the inside, sans the number plaque and the card reader.
  				In place of the reader a small button adorns the wall next to the door.";],
  	before [; Open, Close: "There is no handle of any kind.";
  			Knock: "You knock on the door. There is no response.";],
	door_dir [; if (location == mother_hallway) return w_to;
		    else return e_to;
		    ],		   
	door_to [; if (location == mother_hallway) {
					print "You step into the apartment, and the door closes behind you.^";
					give mother_apartment_door ~open;
					return mother_apartment;}
			else {	give mother_apartment_door ~open;
				print "You exit the apartment, and the door closes behind you.^";
				return mother_hallway;}
			],
	found_in mother_hallway mother_apartment,
  has 	scenery transparent door ~open;

Object	plaque "plaque" mother_apartment_door
  with	name 'plaque' 'sign' 'number' 'twentyfour',
  	description "A square sign displaying the number ~24~ in thick black letters.";
  	
Object	card_reader "card reader" mother_hallway
  with	name 'reader' 'card' 'cardreader' 'slit',
  	before [; Receive: if (noun == sister_card) {
					give mother_apartment_door open;
					"You insert the card in the slit.
  					The LED turns green as the door slides open.";}
!  			if (noun == sister_credit) "The LED turns red. Nothing else happens.";
											],
  	description "A small white box with a slit allowing you to insert the access card. 
  			A small LED is placed just above the slit.",
  has	container scenery transparent;
  
Object	led_light "led" card_reader
  with	name 'led' 'light' 'lamp',
  	description "A small light emitting diode placed above the slit in the card reader. 
  			It is currently dark.";
  			
!--------------------------------------------------------------------------------!

Room 	mother_apartment "The apartment of Rebecca Stiles"
  with	is_searched false,
  	description [; print "Total chaos.^Something bad has definitely happened here.
  			Furniture is broken. Books are thrown all over the floor.
  			The bookshelf has been toppled over.
  			The TV is lying face down on the floor with pieces of glass
  			scattered all over. There is no card reader on this side.
  			Instead a small button adorns the wall next to the door. ";
  			if (~~self.is_searched) "It seems like there has been a fight here.
  			The place is completely trashed.";
  			else print "The place has been trashed, but the person in question
  			wasn't fighting anyone. This was done for its own sake.
  			Maybe to make it appear like the act of a clumsy burglar?^";
!  			print "There is a fine layer of dust on everything.
!  			Whatever happened here must have happened a while ago.^";
									],
  	e_to mother_apartment_door;
 
Object	door_button "button" mother_apartment
  with	name 'button',
  	before [; Push: give mother_apartment_door open;
  			"You push the button, and the door slides open.";],
  	description "A small button next to the door.",
  has	scenery;
 
!Object 	apartment_dust "dust" mother_apartment
!  with	name 'dust' 'dusty' 'floor',	
!  	description "Everything is covered with dust. You estimate, that it must be several
!  			weeks since anyone else has been here.",
!  has	scenery;
  
Furniture mother_tv "television" mother_apartment
  with	name 'tv' 'television',
  	is_searched false,
  	description [;print "A broken television lying on the floor.^";
  				self.is_searched = true;
  				all_searched();];
  	
Furniture mother_books "books" mother_apartment
  with 	name 'book' 'books' 'novels' 'novel',
  	is_searched false,
  	description [;print "Various books are strewn randomly around the room.
  			Mostly cheap crime novels. Rebecca Stiles doesn't seem
  			to have been a great connoisseur of fine literature.
  			You don't see any copies of her sonss book.^";
  			self.is_searched = true;
  			all_searched();],
  has	pluralname;
  			
Object 	mother_glass "pieces of glass" mother_apartment
  with	name 'glass' 'pieces' 'of',
  	is_searched false,
  	description [;print "Pieces of sharp glass from the broken television screen. You take
  			care not to step on them.^";
  			self.is_searched = true;
  			all_searched();],
  has	scenery pluralname;

Furniture mother_bookshelf "bookshelf" mother_apartment
  with	pname  '.x' 'book' 'bookshelf' 'shelf',
  	is_searched false,
  	letter_found false,
  	before [; Search: if (~~self.letter_found) {
				move mother_letter to player;
				self.letter_found = true;
				"A letter seems to have gotten stuck behind one of the shelves.
				You manage to pull it out without tearing it, and stick it in your pocket.";}
						],
  	description [;print "The bookshelf has been toppled and is now lying empty on the floor.^";
  			self.is_searched = true;
  			all_searched();];
 
 
[ all_searched;
	if (mother_tv.is_searched && mother_books.is_searched && mother_glass.is_searched && mother_bookshelf.is_searched)
	{
		mother_apartment.is_searched = true;
		"^Now that you have searched the room thoroughly, you start getting suspicious.
		This doesn't look like the room was trashed during after all. It's too random. Books are strewn everywhere,
		not just in front of the bookshelf. 
		The TV set has been thrown on the floor for no particular reason.
		Everything seems to have been carefully smashed to make
		it appear like the act of some random burglar. Something else has 
		happened here.";
	}
];

Object 	mother_letter "letter"
  with	name 'letter' 'paper',
  	before [; Read: "~Dearest mother.^^I know you don't like travelling but I really think it
  			would be a good idea if you took a vacation soon. You are spending
  			far too much time just sitting around in your apartment. It's not healthy.
  			You should at least get out of The City for a few weeks.^^-Allyson.~";],
  	description "It is a letter from Allyson Stiles to her mother.",
  has	legible;
!-------------------------------------------------------------------------------!

Room 	warehouse "Outside an old warehouse"
  with	description "The address you got from the tracer turns out to be 
  			an old warehouse on the outskirts of The City. 
  			The windows are all boarded up. It seems
  			old and abandoned, but that might just be a ruse.
  			The only visible entrance is a small door to the north.
  			There is a speaker next to the door.",
  	n_to	warehouse_door,
  	in_to [; <<Enter car>>;],
  	cant_go "You should use your car.";

Normal_door warehouse_door "door" warehouse
  with	name 'door' 'metal' 'entrance',
  		door_dir [; if (location == warehouse) return n_to;
  				else return s_to;],   
		door_to  [; if (location == warehouse){ 
					StartDaemon(adam_wayne);
					return inside_warehouse;}
				else return warehouse;],
  	description "A old rusty metal door. There is a speaker next to it.",
  has	scenery ~open lockable locked;

Object warehouse_speaker "speaker" warehouse
  with	name 'speaker' 'comm' 'microphone' 'phone',
  	code_found false,
  	talk_no 0,
  	description [;  print "It seems to be a standard two way speaker, with a microphone,
  			allowing someone to communicate with the people
  			inside. Assuming anyone is there at all.^
  			Someone has scrawled some text above the speaker, in barely legible writing.
  			It says: ^~26-5-9.^ 17-8-3.^ 109-12-4.~";
  			if (~~self.code_found) {
				self.code_found = true;
				if (notebook in player) "^You write down the numbers in your notebook so you won't forget them.";
				else "";}
			else "";],
	react_before [; Passphrase: SpeakerPass();
					rtrue;
			PartPhrase: SpeakerPart();
					rtrue;],
  	life [w1 w2 w3; Answer, Ask, Tell: wn = consult_from; w1 = Nextword(); w2 = NextWord(); 
  				w3 = NextWord();
  				if (w1 == 'freedom' && w2 == 'from' && w3 == 'protection') {
					SpeakerPass();
					rtrue;
				}
				if (w1 == 'freedom' or 'from' or 'protection'){
					SpeakerPart();
					rtrue;}
  				self.talk_no++;
  				switch(self.talk_no) {
					1: "A deep male voice replies from the speaker: 
  					~Please say the passphrase.~";
  					2: "~No, that's not it~, says the voice. 
  						~If you have forgotten it, just look it up. The current code is
  						above the speaker.~";
  					3: "~Please leave. You obviously have no business here.~";
  					default: "There is no reply this time. It doesn't seem like he
  						wants to talk to you.";} !end switch
  						],
  has	scenery animate;

[ SpeakerPass;
	if (~~internal_vigilance.has_read) "You see no reason to say that.";
	if (warehouse_door has locked){
	give warehouse_door ~locked;
	"~Correct~, says the voice from the speaker. You hear the sound
	of the door being unlocked.";}
	else "The door has already been unlocked.";
];

[ SpeakerPart;
	if (warehouse_door has locked)
	"~And?~, asks the voice from the speaker.
	~You will have to say the entire passphrase to be admitted.~";
	else "The door has already been unlocked.";
];

Object	warehouse_object "warehouse" warehouse
  with	name 'warehouse' 'ware' 'house',
  	description "A old seemingly abandoned warehouse. Someone in there picked
  			up the phone, though, so it can't be completely empty.",
  has	scenery;

Object	warehouse_windows "windows"
  with	name 'window' 'windows',
  	description "The windows are all boarded up. You are not getting anywhere through them.",
  	found_in warehouse inside_warehouse,
  has	scenery pluralname;

!----------------------------------------------------------------------------------!

Room 	inside_warehouse "Inside the warehouse"
  with	description [;print "The warehouse is abandoned in the sense that it is no longer
  			being used to store anything. The huge building is empty of
  			any kind of crates or large objects that someone might want
  			to put away for storage. The windows are all boarded up.
  			It is not, however, devoid of human presence. Someone has set up a large
  			group of computers in one of the corners, along with several chairs
  			and a telephone on a small table. ";
  			if (player in warehouse_chair) {print "You are currently sitting on one
  			of the chairs. Adam ";
				if (warehouse_allyson in inside_warehouse) print "and Allyson ";
			print "are in front of you";
			print ". Between you is a small table with a teapot. ";}
  			if (warehouse_computers in inside_warehouse) print "The computers are all on, humming softly
  			to themselves, the screens displaying symbols that mean nothing to you.";
  			else print "The computers are off. All the screens are blank and dead.";
  			"";],
  	s_to	warehouse_door;

Object 	adam_wayne "Adam" inside_warehouse
  with	name 'adam' 'wayne' 'man' 'person' 'guy' 'terrorist',
  	daemon_action 0,
  	will_convert false,
  	give_bomb false,
  	has_trust false,
  	describe [; "^A tall bearded man, casually dressed, is sitting on one of the chairs.
  			You don't recognize him.
  			His confident smile disturbs you. Something is not right here.";],
  	description "He looks fit and capable. You don't like the confident look on his face.
  			He seems annoyingly sure of himself. Maybe he is just overconfident?",
  	life [; Attack: if (broken_computers in inside_warehouse) {
				print "Reaching for that gun was a mistake. You might have let him live otherwise.
				Just as his hand closes around it, you pull your own piece. Shots
				echo through the warehouse once again. He collapses back in the chair, coughs up some blood,
				and then expires.^^";
				remove adam_wayne;
				if (warehouse_allyson in inside_warehouse) print "Allyson doesn't even bother to try.
				Luckily, she doesn't seem to be armed this time. You keep your gun pointed at her, while
				you use the phone to call for backup. Some awkward minutes pass in silence, until the
				tactical team finally arrives, along with your supervisor, and takes her away.^^
				~Great job, Dinnings~, he says, after assessing the situation. ~You've performed beyond
				our wildest expectations. The Overseer himself has requested to see you. He has taken an
				interest in this case for some reason. Congratulations! You have a bright career ahead of you.~^";
				else print "You use the phone to call for backup. With only the corpse
				to keep you company, the next few minutes pass in silence. Finally, the tactical team
				arrives, along with your supervisor. ^^
				~Good job, Dinnings~, he says, after assessing
				the situation. ~A pity you couldn't take any of them alive, though. We could have
				used some information. The Overseer himself has requested to see you. He has taken 
				an interest in this case for some reason. Congratulations! You have a bright
				career ahead of you.~^";
				rebels_defeated = true;
				activ();
				rtrue;}
		Show: if (noun == mother_letter) "He takes a look at the letter, then smiles. 
			~Allyson really wanted her mother out of town. It's not going to be pleasant
			here when the Overseer gets toppled. She tried everything,
			but the old fool stubbornly refused to leave her apartment.
			In the end, she had to abduct her own mother. She tried to make it look like
			the act of a random criminal, to throw potential investigators off our track. 
			She didn't do a very good job, did she?~^
			He laughs. ~Not that it matters. No one ever came to visit her except you.~^^
			You consider this for a moment. Apparently, their plans involve some major
			destruction.";],
	before [; Hit: if (broken_computers in inside_warehouse) {
			print "You try to rush him, but he is too fast. He manages to grab the gun before
			you can reach him, and shoots you several times. You collapse on the floor, where
			you get shot again. He doesn't want to take chances, it seems.^";
			deadflag = 3; rtrue;}
					],
  	daemon [; self.daemon_action++;
  			switch(self.daemon_action) {
				1: print "~Welcome, Agent Dinnings~, says the man in a deep voice. ";
					if (warehouse_door hasnt locked)
						print "We have been expecting you. I knew you would be able to figure
						out our little passphrase. I just wanted to see if you were as smart
						as I thought. You didn't disappoint me. ";
					"You can call me Adam, although
					that is, of course, not my actual name.~";
				2: 	print "He throws a furtive glance at the screens. ";
					if (actiioutcome == wake_up) print "~We have been trying to determine
					whether you might be worth saving. Unfortunately, you woke
					up before we could procure any useful data.~^^";
				   	if (actiioutcome == freedom) {print "~We have been trying to determine
				   	whether you might be worth saving, and I am happy to say that the results have been
				   	most promising. The fact that you chose to kill rather than stay in the cell
				   	indicates that, deep down, you do value your freedom most highly. We may have uses
				   	for you, if you are willing to consider it.~^^"; self.will_convert = true;
				   	self.has_trust = true;}
				   	if (actiioutcome == freedomnotwings) {print "~We have been trying to determine
				   	whether you might be worth saving, and I am happy to say that the results have been
				   	most promising. The fact that you chose to kill rather than stay in the cell
				   	indicates that, deep down, you do value your freedom most highly. 
				   	Flying into the sun does seem a bit reckless, but that's not necessarily
				   	a bad thing. We could use some intrepid agents, if you are willing
				   	to consider the option.~^^"; self.will_convert = true; self.give_bomb = true;
				   	self.has_trust = true;}
				   	if (actiioutcome == comply) print "~We have been trying to determine
				   	whether you might be worth saving, but I am sorry to say that results have
				   	been most disappointing. Your subconscious behavior indicates that you
				   	tend to obey authority without much thought. I don't think you will be of any
				   	use to us.~^^";
				   	if (actiioutcome == complynotwings) {print "~We have been trying to determine whether 
				   	you might be worth saving, but I am sorry to say that results have
				   	been slightly disappointing. Your subconscious behavior indicates that you
				   	tend to obey authority without much thought. However, your reckless flight towards
				   	the sun does show a capacity for independent action. We may have some use for you, if
				   	you are willing to consider the option.~^^"; self.will_convert = true;}
				   	if (actiioutcome == hopelessfight) {print "~We have been trying to determine whether
				   	you might be worth saving, and our results have been most interesting. The fact
				   	that you were willing to die in a hopeless struggle rather than comply with
				   	authority shows a surprising capacity for independent thought. We may have
				   	uses for you, if you are willing to consider the option.~^^";
				   	self.will_convert = true; self.give_bomb = true; self.has_trust = true;}
				   	if (actiioutcome == refuse) {print "~We have been trying to determine whether 
				   	you might be worth saving, and our conclusions have been quite favorable.
				   	The fact that you refused to obey authority shows some capacity for independent
				   	thinking, even if you aren't consciously aware of it. We may have some use for you,
				   	if you are willing to consider the option.~^^";
				   	self.will_convert = true; self.has_trust = true;}
				   	"The truth hits you like a jackhammer. They have access to
				   	your NeuroChip! That's why you have been having all those weird dreams
				   	lately. But how? VisionTech assured the Agency that their
				   	system was impossible to crack.";
				3: "He glances at the screens again. ~You may have noticed that you are unable to perform
					certain actions at the moment. We wouldn't be here if we thought you might
					be a threat. I don't suppose you knew that your chip can be used to
					control you as well as to monitor your thoughts? 
					It's a failsafe for controlling rogue
					agents. We have taken the liberty of disabling any actions that might harm us.
					Also, you will only speak when spoken to. We do not have time for idle chat
					at the moment.~";
				4: if (warehouse_allyson in inside_warehouse) print "~You will sit down Agent~, he 
					states matter-of-factly, and you find that he is right. You are now sitting on one
					of the chairs near the computers. He rolls out a small table on wheels from behind
					the machines.
					On it is a familiar looking teapot and three cups. ~Allyson has made us a cup of tea.
					Have a cup and we will discuss your future. And don't worry. It isn't drugged this time.
					That would be pretty dumb. We are drinking it too.~. They both laugh.^";
				 	else print "~You will sit down Agent~, he 
					states matter-of-factly, and you find that he is right. You are now sitting on one
					of the chairs near the computers. He rolls out a small table on wheels from behind
					the machines.
					On it is a familiar looking teapot and two cups. ~I have made us a cup of tea.
					Have a cup and we will discuss your future. And don't worry. It isn't drugged this time.
					That would be pretty dumb. I'm drinking it too!~. He laughs heartily.^";
					PlayerTo(warehouse_chair);
					move tea_table to location;
					move warehouse_teapot to location;
					move teacup1 to location;
					move teacup2 to location;
					move table_wheels to location;
					if (warehouse_allyson in location) move teacup3 to location;
				5:	remove teacup2; remove teacup3; 
					print "He pours a nice cup of tea for each of you. ";
					if (warehouse_allyson in location) 
						print "They both pick up their cups and taste the tea.
						He smiles. ~Excellent as usual, Allyson. Have a cup,
						Agent Dinnings.~^";
					else print "After tasting the tea, he nods approvingly. ~It's okay.
						Allyson could have done better, but you took her out of the equation.
						Have a cup, Agent Dinnings.~^";
				6:	if (warehouse_teapot.is_poisoned) teadrug();
					else	{print "~I bet you didn't think you would be drinking tea with us when you came
						here, did you?~. ";
						if (warehouse_allyson in location) "They both laugh again.";
							else "He laughs again.";}
				7: 	if (warehouse_allyson in inside_warehouse) "He pours a fresh cup of
					tea for all of you. ~Better drink it while it's hot!~";
					else "He pours a fresh cup of tea for both of you. ~Better drink it while it's hot!~";
				8:	if (warehouse_teapot.is_poisoned) {teadrug(); rtrue;}
					if (self.will_convert) {print "~I hope you will consider our offer. Deep down, you don't
						like The Union anymore than we do. I know that. It takes away our freedom under the
						pretense of protecting us. We are, apparently, too stupid to protect ourselves. 
						Our plans will come to fruition sooner or later anyway, but your help would
						definitely expedite things. Will you help us?~>>";
						if (YesOrNO()) {print "^^~Excellent, Robert. Allyson didn't believe you would, but
						I knew we could count on you.~ You wince. It has been years since anyone has
						used your first name. ^^
						~After you return to Agency, you will be asked
						to meet the Overseer. He has taken an interest in this case.
						Perhaps he suspects how important you are to us. ^^";
							if (self.give_bomb){
								print "We will require you to carry a bomb into the The Tower of the Overseer. 
								The very heart of The Union
								where you will detonate it as close to him as possible.^^ The resulting chaos should
								make it possible for us to dismantle The Union and return independent sovereignty
								to the individual nations. ^^People will once again be in control of their own lives.
								There will be a lot of suffering along the way, of course. A renaissance can not
								occur without birth pains. Will you be willing to sacrifice yourself for the greater
								good?~>>";
									if (YesOrNO()) {print "^~Excellent! You will be remembered as a hero, Robert.
										Good luck.~^^";
										move briefcase to player;
										activ();}! end sacrifice
									else {print "^~Slightly disappointing, but perfectly understandable. Wery
										well, then. Just place the bomb somewhere inconspicuous, and leave.
										We can detonate it remotely. The Overseer might survive that way, of course,
										but I think we can deal.~^^"; 
										move briefcase to player;
										activ();}! end else
									} ! end givebomb
							else {
								print "You will be required to carry a bomb into the headquarters of the Overseer. 
								The very heart of The Union. Just place it somewhere inconspicuous, and we will detonate
								it remotely. ^^
								The resulting chaos should
								make it possible for us to dismantle The Union, and return independent sovereignty
								to the individual nations. People will once again be in control of their own lives.^^
								There will be a lot of suffering along the way, of course. A renaissance can not
								occur without birth pains. That is a regrettable necessity.
								Good luck, Robert.~^^"; move briefcase to player; activ();}!end else givebomb
								} !end yesno
						else {print "^^He looks disappointed. ~That's a pity. Allyson didn't think
						you would, but I always thought we could count on you. Apparently,
						I was wrong. Goodbye, Agent.~ Without further ado, he casually pulls out a gun
						and shoots you dead."; deadflag = 4; rtrue;} !end else answer
						}! end ifconvert	
					else {print "He seems lost in thought for a moment. 
						~Agent Dinnings, I have considered the situation, and I am afraid
						that we simply can't take any chances. Please don't take this personally.~
						^^With that, he draws a gun and casually shoots you dead.";
						deadflag = 3; rtrue;}
				51: print "Shots echo through the warehouse once again. Bullets penetrate your skin.
						You drop to the floor, where you get shot several times more. Apparently,
						he doesn't like to take chances...";
				 	StopDaemon(adam_wayne); deadflag = 4; rtrue;
				default: rtrue;
								}
						],
  has	animate proper;
  
[ teadrug;
	if (warehouse_allyson in location) print "The cup drops from his hand, 
	scattering into a dozen pieces. Realisation dawns on his face.
	~Allyson... did you...~^^
	Allyson doesn't answer. The cup drops from her hands as well. She
	makes a feeble attempt to stand, but slumps back in the chair instead.
	They both fall asleep in their chairs, Adam snoring loudly.^";
	else print "The cup drops from his hand, 
	scattering into a dozen pieces. Realisation dawns on his face.
	~The drug... how did you...~^^
	He makes a feeble attempt to stand, but slumps back in the chair instead.
	Seconds later, he is sound asleep in the chair, snoring loudly.^";
	print "You sit around for a while, still unable to leave.^^
	Several hours later, a tactical team finally bursts into the
	warehouse, followed by your supervisor. ~Dinnings!~, he proclaims,
	~We lost contact with your chip several hours ago. Apparently,
	there was a backdoor in our system.
	It took us hours to reestablish control~. He smiles approvingly.
	~Luckily, it looks like you have got things under control here.
	The Overseer himself has requested to speak to you. He has taken
	an interest in this case for some reason. Congratulations, Dinnings!
	You have a bright career ahead of you.~^";
	rebels_defeated = true;
	activ();
];
  
Furniture phone_table "table" inside_warehouse
  with	pname '.x' 'small' '.x' 'phone' 'table',
  	description "A small table holding a phone.";
  	
Object	warehouse_phone "phone" inside_warehouse
  with	name 'phone' 'telephone',
  	description "It's an old fashioned telephone with buttons instead of voice recognition.",
  	react_before [; Call: "You find it very unlikely that they will grant you the right to
  				make a phone call.";],
  has	scenery;

Furniture 	tea_table "table"
  with	pname '.x' 'tea' 'table',
  	description "A small table with wheels, allowing it to be pushed around easily.",
  has	transparent;
  
Object	warehouse_teapot "teapot"
  with	pname     '.x' 'clay' '.x' 'green' '.x' 'tea' 'pot' 'teapot',
  	is_poisoned false,
  	description "It is a green clay teapot full of hot tea.",
  	before [; Take: if (warehouse_computers in inside_warehouse) "Your arm feels numb.";
  		  Drink: "You should probably wait for him to pour it.";
  		  Receive: if ((adam_wayne.daemon_action == 4 or 6) && noun has general) {remove noun; 
  		  		self.is_poisoned = true;
  		  		"While he is busy laughing, you carefully flip
  		  		the pill into the teapot, where
  		  		it quickly dissolves, leaving no visible trace. Apparently, they didn't
  		  		predict you would try something like that. At least your chip didn't prevent it.";}
  		  		else {if (noun hasnt general) "Why would you put that in the teapot?";
  		  			if (adam_wayne.daemon_action > 4) "He would notice it. You might have
  		  			had a chance while he was busy laughing at you, but it's too late now.";
						}],
  has	container concealed open;

Class   teacups(3)
  with	name 'teacup' 'tea' 'cup' 'teacups/p' 'cups/p',
  	short_name "teacup",
  	plural "teacups",
  	describe [; rtrue; ],
  	before [; Take: if (adam_wayne.daemon_action < 5) "He hasn't poured the tea yet.";
  			else {move teacup1 to player; "You pick up your tea.";}
  		  Drink:if (warehouse_teapot.is_poisoned) "Probably not a good idea. You just
  		  				drugged it."; 
  		  	if (adam_wayne.daemon_action < 5) "He hasn't poured the tea yet.";
  		  	else "You sip your tea.";
  			!else {if (teacup1 in player) "You sip your tea.";  
  			!	else <Take self>;}
  			Receive: <<Insert noun warehouse_teapot>>;
  					],
  has	concealed;
  	
teacups teacup1;
teacups teacup2;
teacups teacup3;

  
Object	table_wheels "wheels"
  with	name 'wheels',
  	description "They are round!",
  has	scenery;
  
Object	warehouse_allyson "Allyson"
  with	name 'allyson' 'stiles' 'sister' 'terrorist' 'woman' 'her' 'bitch',
  	describe [; "^Allyson is here, smiling at you
  			as if nothing had happened.";],
  	  life	[; Attack: if (broken_computers in inside_warehouse) "You decide to take out Allyson first.
  	  		A well placed shot does the job. Her limp body drops to the floor.
  	  		Unfortunately, this gives Adam time
  			to pull out his gun.^";],
  	before [; Hit:	if (broken_computers in inside_warehouse) "You throw yourself at Allyson and
  				wrestle her to the floor. Unfortunately, this gives Adam time to
  				pull out his gun.^";],
  	description "She looks as pretty as ever. Smiling at you as if everything was fine. Creepy.",
  has	animate proper female;
  	
Furniture warehouse_computers "computers" inside_warehouse
  with	name 'computers' 'computer' 'groups' 'of' 'screen' 'screens',
  	description "About half a dozen computers, connected with a tangled mess of wires.
  			The screens on top of them display a everchanging mess of symbols whose meaning
  			you can't decipher. Some of them seem strangely familiar, though.",
  	before [; Attack: print "Surprisingly, nothing prevents you. Apparently they did not predict that
  			you might try to destroy the computers. That was a serious mistake on their part.
  			The shots echo through the empty
  			warehouse. Sparks shoot from the cabinets. The screens go blank. 
  			You feel a sharp pain in the back of your head. Realising what that means,
  			you quickly turn your gun on Adam ";
  			if (warehouse_allyson in location) print "and Allyson";
  			print ". ~Surrender, and you will get a fair trial.~^^
  				Not that you seriously expected a peaceful solution. 
  				Instead, Adam snaps to attention, the chair toppling over behind him,
  				reaches into his pocket, and starts pulling out a gun.^";
			adam_wayne.daemon_action = 49;
  			remove warehouse_computers;
  			move broken_computers to inside_warehouse;
  			rtrue;
  		Switchoff: "You have no idea which buttons to press. These are not standard issue office computers.";
  			],
  	react_before [; Go: "You find that your legs refuse to obey.";
  			Hit: "Your body refuses to obey you. How frustrating.";
  			Attack: if (~~noun == self) "Your body refuses to obey you. How frustrating.";
  			Ask, Tell: "Your mouth refuses to open."; ],
  has	pluralname switchable on;
  
Furniture broken_computers "computers"
  with	name 'computers' 'computer' 'groups' 'of' 'screen' 'screens',
  	description "The computers seem to have short circuited completely. The screens
  			are all blank.",
  has	pluralname;

Furniture warehouse_chair "chair" inside_warehouse
  with	name 'chair' 'chairs',
    	before [; Enter, Exit: "You can't.";],
  	description "They are all nce new office chairs.";

!---------------------------------------------------------------------------------!
! Act IV: Conclusion

Object 	briefcase "briefcase"
  with	pname 'briefcase' '.x' 'brief' '.x' 'case',
  	description "A inconspicuous black briefcase. A small button on the top allows
  			you to detonate the bomb manually, if it should become necessary.",
  	before [;	Hide: if (location == overseer_hallway) <<Drop self>>;
  			else "You can't hide it here. It would most certainly be discovered.";
  			Drop: if (location == overseer_hallway) {
				move self to location;
				"You carefully leave the briefcase	
  				behind one of the plants. Hopefully,
  				that is close enough
  				for the bomb to do
  				its job.";}
  			else "You can't leave it here. It would most certainly be noticed.";],
  	react_after [; Inv: if ((~~gun in player) && ~~gun.has_gone){
				gun.has_gone = true;
				"Your gun seems to be gone. Security here is very efficient. And subtle...";}],
  has	container openable ~open;
  
Object	bomb_button "button" 
  with	pname '.x' 'briefcase' '.x' 'detonator' 'button',
  	before [; Push: switch(location){
  				overseer_room:
  					ending_four();
  					rtrue;
  				overseer_hallway:
  					"If you are going to commit suicide, you might as well
  					be as close to your target as possible.";
  				default: print "You push the button and the bomb explodes in your hands.
  						You die instantaneously, taking several random bystanders
  						with you.";
  						deadflag = 3; rtrue;
						}!end switch
  					],
  	description "A small button allowing you to detonate the bomb.
  			You better be careful not to touch it by mistake.",
  	found_in [; if (briefcase in player or location) rtrue;],
  has	scenery;

Object 	bomb "bomb" briefcase
  with	name 'bomb' 'explosive',
  	before [; Take: "You really shouldn't try to remove it from the briefcase.
  			Firstly, someone might see it, and secondly, you are not
  			entirely sure it might not explode.";],
  	description "A powerful bomb. You are not entirely sure how powerful, but they
  			assured you that it will do the job.";

Room union_headquarters "Outside The Tower"
  with	name 'tower',
  	description "You are standing outside The Tower that constitutes the heart of the entire Union.
  			From here The Overseer supposedly influences every aspect of the government,
  			making sure that its actions are in compliance with the ideals of The Union.
  			Although he cannot
  			of course, personally keep an eye on every district, he is still
  			the most powerful individual there is, and the most important symbol as well.^
  			The Tower is the tallest building in the world. Its peak lost in the
  			clouds far above you.^
  			In front of you, to the north, are the large white marble stairs that lead to the entrance doors.
  			Dozens of official-looking people constantly move in and out of the tower, up and down the
  			stairs. 
  			They all wear suits and carry briefcases. You are unlikely to be noticed.",
  	n_to	[; <<Go u_obj>>;],
  	u_to	[; print "You walk up the stairs. A few other people pass you on the way. No one 
  			looks in your direction.^"; return tower_entrance;];
  			
Furniture tower_stairs "stairs"
  with	name 'stairs' 'stair',
  	description "The large marble stairs lead from the ground to the entrance. 
  			Placing the entrance where you need stairs to reach it does
  			seem a bit pointless. The purpose of the stairs is probably to make
  			the building seem more imposing. And maybe to ensure that employees
  			get some exercise.",
  	found_in union_headquarters tower_entrance,
  has	pluralname;
  
Object 	tower_people "people"
  with	name 'people' 'men' 'employees',
  	before [; Examine: rfalse;
  			default: "They ignore you.";],
  	description "Lots of official-looking people in suits are constantly moving in and out of The Tower.
  			They all seem very busy.",
  	found_in union_headquarters tower_entrance inside_tower,
  has	pluralname scenery;
  	
Object	tower_clouds "clouds"
  with	name 'cloud' 'clouds',
  	description "One of them looks somewhat like a big white carpet.",
  has	pluralname scenery;
  			
!------------------------------------------------------------------------------------------------------------!

Room tower_entrance "In front of the entrance doors."
  with	description [; print "The top of the stairs. Finally. Working here must be excellent exercise. 
  			A row of identical sliding glass doors to the north allows ingress to The
  			Tower. At each door, a guard in an official uniform carefully examines
  			every person passing through the doors. Futhermore, a security camera at 
  			each door performs the exact same function. You suspect that the purpose of
  			the guards is mostly ceremonial.";
  			if (briefcase has open) {
				print "^One of the guards notices the bomb, which is conspicously visible
				in your open briefcase. He yells for you to stop. Other guards turn their 
				attention towards you. At that moment, before anyone has a chance to act,
				the bomb explodes. You die instantly, taking several innocent bystanders
				with you.";
				deadflag = 3; rtrue;}
			else "";],
  	n_to	[;print "^~Welcome to the Tower, Agent Dinnings~, the guard says as you walk past him.^";
  			StartDaemon(guide);
  			return inside_tower;],
  	s_to	union_headquarters,
  	d_to	union_headquarters;
  	
Object  tower_cameras "cameras" tower_entrance
  with	name 'security' 'tower' 'camera' 'cameras',
  	description "Swivelling security cameras keep track of everyone entering the tower.",
  has	scenery;
  	
Object 	tower_guards "guards" tower_entrance
  with	name 'guards' 'guard' 'soldiers' 'soldier',
  	description "Identical guards with identical uniforms. Guarding the tower
  		is supposed to be a huge honor. Only the best soldiers are chosen
  		for the job. Not that they have ever been needed. ",
  	before [; Hit: <<Attack self>>;],
  	life [word_number i; Show: if (noun == badge) "~Don't you need to see my badge~, you ask the nearest guard.^^
  			~That will not be necessary, Agent Dinnings~, he replies, and signals
  			for you to proceed.^^
  			~But I could be anyone.~^^
  			~No, you could not~, he states matter-of-factly.
  			~This is Information Retrieval!~. You stare at him for a few seconds.^^
  			~Sorry~, he says sheepishly. ~I've always wanted to say that.~";
  			if (noun == briefcase) <<Tell tower_guards 'bomb'>>;
  		Tell:	wn = consult_from;
			word_number = consult_words;
			for (i = 1: i <= word_number: i++) {
			switch(NextWord()){
				'bomb', 'briefcase', 'plot', 'assasination':
					if (briefcase in player){
					print "~Excuse me~, you say to the closest guard.
					~I have some very important information. This briefcase
					contains a bomb which...~^^
					The bomb explodes. You die instantly, along with several
					innocent bystanders.";
					deadflag = 3; rtrue;}
					}! end switch
						}! end for 
  		Attack: print "These are some of the best trained soldiers in The Union.
  			However, you decide that you are badass enough to take them all down.
  			Unfortunately, you are wrong. You manage to take down the nearest guard
  			before dying in a hail of gunfire. That is all.";
  			deadflag = 3;
  			rtrue;
  		default: "The guard ignores you and signals for you to proceed.";],
  has	pluralname animate scenery;
  
Object 	tower_doors "glass doors"
  with	name 'sliding' 'glass' 'doors' 'door',
  	before [; Open, Close: "They are automatic doors. Just walk through them.";],
  	description "Sliding glass doors that open automatically when you approach them.",
  	found_in tower_entrance inside_tower,
  has	scenery pluralname;

!---------------------------------------------------------------------------------!

Room 	inside_tower "The Entrance Hall"
  with 	description "This is the entrance hall of The Tower. The floor is gleaming marble, like
  			the stairs outside. You can almost see your reflection in it.
  			Hundreds of people are milling around the huge hall like ants, 
  			oblivious to anything except their destination. In the middle of everything stands 
  			the imposing marble Statue of Unification, towering over everyone.
  			Scores of elevator doors open and close at regular intervals, elevators
  			taking people to one of the hundreds of floors in The Tower. You have
  			absolutely no idea where you are supposed to go.",
  	s_to [; if (overseer.is_satisfied) {
			if (briefcase in overseer_hallway) {
				ending_one(); rtrue;}
			if (overseer.want_infiltrate) {
				ending_nine(); rtrue; }
			switch(overseer.success){
				1: ending_five();
				2: ending_six();
				3: ending_seven();
				4: ending_eight();
				rtrue;
				}! end switch
					rtrue;} ! end if satisfied
  		return tower_entrance;];
  
Object uni_statue "statue" inside_tower
  with	name 'statue' 'of' 'unification',
  	description "The Statue depicts The Unifier himself, signing the Declaration
  			of Unification. Also signing the Declaration is the Maiden of justice,
  			holding her scales high into the air, and the Dove of Peace, using one of its
  			own feathers.^
  			A plaque adorns the base of the statue.",
  has	transparent scenery;
  			
Object tower_plaque "plaque" uni_statue
  with	name 'plaque' 'plate' 'sign',
  	description "~Those who would give up safety for freedom deserve neither.~^^
  			Beneath the quote is the name of The Unifier.",
  has	transparent;
  
Object	unifier_name "name" tower_plaque
  with	name 'unifier' 'name',
  	description "You already know the name of The Unifier. Everybody knows that.";
  	
Object tower_elevators "elevators" inside_tower
  with	name 'elevator' 'elevators',
  	before [; Examine: rfalse; default: "You have no idea which elevator to use.";],
  	description "Scores of different elevator doors adorn the walls. You have no idea which one
  		you should take, or which floor you should go to. Nobody told you any details.",
  has	scenery;

Object	guide	"guide"
  with	name 'guide' 'man' 'person' 'mib',
  	action_no  0,
  	daemon [; self.action_no++;
  			switch(self.action_no) {
				4: 	StopDaemon(guide);
					print "While you are wondering what to do, someone taps you
					on the shoulder. You spin around to confront yet another
					person in a suit, carrying a briefcase. ^
					~Please follow me, Agent Dinnings~, he states politely. 
					~I'm here to guide you to The Overseer.~ He turns around,
					not waiting for you to reply. You follow him to a small nondescript
					elevator door in the corner of the hall. There are no buttons near the door
					at all, but when you approach, it silently slides open to reveal
					a small compartment.^
					~Please step inside, Agent Dinnings~, he requests. You do so, without 
					a second thought. The acceleration pushes you against the floor, almost
					making it hard to stand, but you persevere. After what feels like several minutes
					the elevator finally stops, and the door opens, revealing a hallway to the north.^";
					remove gun; remove sister_gun;
					PlayerTo(inside_elevator); StopDaemon(guide);}],
  	description "Yet another person in a suit, carrying a briefcase.",
  has	animate proper;

!-----------------------------------------------------------------------------------!
Room inside_elevator "Inside the elevator"
  with	description "You are standing inside a small wooden elevator compartment. Buttons
  			of any kind are conspicuously absent. The door to the north is open, 
  			revealing a long hallway terminating in a pair of large doors.",
  	n_to overseer_hallway;
  	
Object	overseer_elevator_door "elevator door"
  with	name 'elevator' 'door' 'doors',
  	description "The door to the elevator is open. There seems to be no way to close it.
  			There are no buttons on the outside either.",
  	found_in inside_elevator overseer_hallway,
  has	scenery;


!-----------------------------------------------------------------------------------!

Room overseer_hallway "Hallway"
  with	description "The hallway stretches to the north, terminating in a pair of large wooden doors.
  			The only decoration consists of two potted plants at each side of the northern doors.
  			White light falls gently from the ceiling, making sure the hallway is fully lit.",
  	n_to [; StartDaemon(overseer); return overseer_door;],
  	s_to	[; if (overseer.is_satisfied) {
				if (briefcase in player) "You need to do something about the bomb first.
						You can't just take it home with you.";
				print "As you enter, the door closes behind you, and
				the elevator takes you back to the ground floor hall again. 
				Standing in the hall, it suddenly occurs to you that you can no longer
				remember the location of the elevator you just exited. Weird.";
				move gun to player;
				return inside_tower;}
  			return inside_elevator;];
  	
Object	overseer_lights "ceiling" overseer_hallway
  with	name 'ceiling' 'light' 'lights',
  	description "The ceiling is glowing with soft white light. It is either made of some
  			phosphorescent material, or it is transparent with lights of some kind behind it.
  			You can't tell which.",
  has	scenery;
  
Object overseer_plants "plants" overseer_hallway
  with	name 'potted' 'pot' 'plant' 'plants',
  	description "The plants turn out to be fakes, made of plastic. Apparently The Overseer
  			doesn't have time for the real thing.",
  has	pluralname scenery;

Normal_door overseer_door "wooden doors" overseer_hallway
  with	name 'double' 'door' 'doors' 'entrance',
  		door_dir [; if (location == overseer_hallway) return n_to;
  				else return s_to;],   
		door_to  [; if (location == overseer_hallway)
					return overseer_room;
				else return overseer_hallway;],
  	description "Two large wooden doors block the entrance. On each door
  			is a shining polished brass handle.",
  has	scenery ~open transparent pluralname;
  
Object brasshandles "handles" overseer_door
  with	name 'brass' 'handle' 'handles',
  	description "Shiny brass handles. They are used to open the doors.";
  	
!--------------------------------------------------------------------------------!

Room overseer_room "The residence of The Overseer"
  with	description [;print "This is it. The residence of The Overseer himself. 
  		Screens glow at you on all sides in this large circular room,
  		each showing a silent scene from somewhere
  		in The Union. 
  		In the middle of the room is a chair, mounted on the floor
  		so it can swivel in any direction when needed.^";
  		if (location hasnt visited) { print "When you enter the room, the chair silently
  		turns until it faces in your direction.^";
					} ! end ifvisist
  		rtrue;],
  	s_to	[; if (~~overseer.is_satisfied) "~Please stay, Agent Dinnings~, says The Overseer
  				in a voice that can not be disobeyed. ~We have much left to discuss.~";
  			else return overseer_hallway;];
  	
Furniture overseer_chair "chair" overseer_room
  with	pname '.x' 'swiveling' 'overseers' 'chair',
  	before [; Receive: "You don't believe The Overseer would like that.";],
  	description "A large comfortable looking chair capable of turning in any direction.
  			On one of the arm supports are several buttons whose purpose
  			you can not ascertain.";
  
Object chair_buttons "chair buttons" overseer_room
  with	pname 'overseers' 'buttons' ,
  	description "The Overseer's chair has several buttons. You have no idea
  			what any of them do.",
  has	scenery;
  			
Object 	overseer "The Overseer" overseer_chair
  with	name 'overseer' 'leader' 'man',
  	description "This is him. The official leader of The Union. 
  			The eye at the top of the pyramid. He looks disappointingly normal.
  			A tall thin man who seems to be somewhere in his 40's or 50's. He is wearing
  			a marine blue suit, and a few traces of gray have started to appear in 
  			his otherwise brown hair. His brilliant blue eyes are examining you with interest.",
  	is_satisfied false,
  	success 0,
  	action_no 0,
  	want_infiltrate false,
  	has_told false,
  	ask_adam 0,
  	ask_sister 0,
  	ask_andrew 0,
  	ask_screens 0,
  	ask_dream 0,
  	ask_freedom 0,
  	!told_truth false,
  	before [; Hit: "You gotta be kidding..."; ],
  	life [word_number i; 
  		Tell:  	wn = consult_from;
			word_number = consult_words;
			for (i = 1: i <= word_number: i++) {
				switch(NextWord()){
					'bomb', 'briefcase', 'plot', 'assasination':
						tellbomb();
						if (~~self.has_told) {self.has_told = true;}
						rtrue;
					}!end switch
						}! end for
		Answer: switch(noun) {
				'hello', 'hi', 'greetings':
				"~Good evening, Overseer~, you say respectfully.^^
				~Good evening, Agent Dinnings. I have been looking forward to meeting you.~";
				'goodbye', 'adieu':
				if (overseer.is_satisfied)
					"~I'd better be going now...~^^
					~Goodbye, Agent Dinnings~, he responds.
					~Good luck with your career.~";
				else "~Please stay, Agent~, he says.
					~We have much to discuss.~";}
		Ask: 	wn = consult_from;
			word_number = consult_words;
			for (i = 1: i <= word_number: i++) {
				switch(NextWord()){
					'adam', 'wayne', 'leader':
					if (rebels_defeated || self.has_told){
						self.ask_adam++;
						switch(self.ask_adam){
							1: if (adam_wayne in inside_warehouse)
								"~What will happen to Adam?~, you ask.^^
								~He will be interrogated, of course.
								After that, I suppose we will just lock
								him up and throw away the key. He deserves to rot
								in jail for the rest of his sorry life.~";
								else print "~Do you have any idea who this 'Adam' might have 
								been?~, you ask.^^";
								if (warehouse_allyson in inside_warehouse)
									"~Not at all. But, I am confident that the interrogation
									of Miss Stiles will provide valuable information in that regard.~";
								else "~Not at all, and we will probably never know. At least not in the near future.
								If only you could have taken one of them alive...~";
							2: if (adam_wayne in inside_warehouse) 
								"~How will Adam's interrogation proceed?~^^
								He laughs. ~Aren't you just the consummate agent? Always trying to improve your
								technique. The details are classified, of course, but I assure you that
								we will use any means necessary to make him talk.~ ";
								else "~Will you let me know what we figure out concerning Adam's identity?
								I'm quite curious about the whole thing.~^^
								He shakes his head. ~Sorry, Agent. The Agency operates on a need to know
								basis. You know that. More experienced anti-terror agents will take over
								the case from here. You just relax and get some rest. You've earned it.~ ";
							}
								}! end rebels_defeated
					'sister', 'allyson', 'bitch':
					if (rebels_defeated || self.has_told){
						self.ask_sister++;
						switch(self.ask_sister){
							1: if (warehouse_allyson in inside_warehouse)
								"~What will be done with Allyson?~, you ask.^^
								~She will be interrogated, of course. If she cooperates,
								she might get an early release. Maybe she will even get a job at
								Agency. Plenty of repentant terrorists get jobs with Agency. It takes one
								to catch one, after all. And we could certainly use someone with her talents in
								The Union.~";
								else print "~I'm sorry I had to kill Allyson you say. Do you think we might
									have learned anything important from her?~^^
									~It's possible~, he admits. ";
								if (adam_wayne in inside_warehouse) "~But at least you got Adam. He seems
									to be the most important one. I doubt Allyson would have made any
									major difference.~ ";
									"~If only you had been able to take this 'Adam' alive it wouldn't matter
									much. As it is, we have practically no useful leads.~ ";
							}
								}! end rebels_defeated
					'andrew', 'subject', 'prisoner', 'stiles':
						self.ask_andrew++;
						switch(self.ask_andrew){
							1: print "~What about Andrew?~, you ask. ~I never did figure out whether he had
								terrorist connections~^^";
								if (prisoner in interrogation) "~Well, his sister was a terrorist, but I doubt
									he knew that. I would love to release him, if only he would promise
									to keep his mouth shut. That doesn't seem very likely, however. We will
									probably have to keep him under surveillance a while longer.~";
								else "~Well, I guess we will just cremate the body, and send his mother a check.
									By the way, it would be nice if you would be a bit more... restrained...
									during your next interrogation. This kind of thing doesn't reflect very
									well on The Union.~";
							}
					'screens', 'screen', 'surveillance', 'tv', 'tvs', 'privacy':
						self.ask_screens++;
						switch(self.ask_screens){
							1: "~Excuse me for asking, Overseer~, you say nervously, ~but what are the point
								of all those screens? They don't seem to be displaying any useful information.~^^
								He smiles. ~They can show more useful things, of course. But I love
								watching those little slices of life in The Union. They remind me of my duties
								to the citizens.~";
							2: "~Your duties to the citizens don't include granting them the right to privacy, though?~,
								you continue, emboldened by his kind demeanor.^^
								He seems surprised by this sudden criticism. ~I like you, Dinnings. 
								You are certainly a lot
								more brazen than most other agents i've spoken to. You might notice,
								that I only watch people in public areas. When you are out in public, 
								people can see you. That's why we call it 'public'. Besides, everyone
								knows that public areas are under constant surveillance. It's not like
								they expect differently.~";
							}					
					'dream', 'dreams', 'chip', 'neurochip':
						self.ask_dream++;
						switch(self.ask_dream){
							1: if (rebels_defeated || self.has_told) "~Overseer~, you say. ~I am worried about the security breach concerning my
								neurochip. Those terrorists managed to somehow hack their way in and gain
								access to my thoughts. Is there any chance that such a thing might happen again?~^^
								~Don't worry about that, Dinnings~, he replies assuringly. ~This was an exceptional
								occurrence. It seems that Miss Stiles was part of the team which developed your chip
								security system. She managed to sneak in a backdoor, which has, of course, been removed
								by now. In the future, all programs utilized by The Union will have their source code
								vigilantly scrutinized. Such a thing will never be allowed to happen again.~";
							2: if (rebels_defeated || self.has_told) "~To be honest, Overseer~, you say politely. ~I am not entirely comfortable with having
								a chip implanted in my brain. Even if it is entirely unhackable.~^^
								He looks sympathetic. ~We all feel like that sometimes, Dinnings. It is
								our civic duty when working directly for The Union. We need to be able to
								control our operatives, after all! The citizens of The Union live stable
								happy lives because of our sacrifices.~";
							}					
					'freedom', 'liberty', 'rights', 'book', 'internal', 'vigilance', 'union', 'unification':
						self.ask_freedom++;
						switch(self.ask_freedom){
							1: "~I examined his book~, you say. ~It is a bit extreme, but don't you think he might
								have some valid points? The Union doesn't really give people very much freedom
								to decide how to live their lives.~^^
								~You certainly speak your mind, Dinnings. You're lucky we don't suspect you of being
								a terrorist.~ He laughs. ~We must, of course, give up some luxuries if society is to
								function. The alternative is chaos, and nobody wants that. Have you studied what
								life was like before The Union? War, famine and disease everywhere! We are
								truly privileged to be living in this golden age.~";
							2: "~It would be nice to have a little more control over my own life, though...~, you think out loud.
								^^The Overseer seems annoyed at your persistence. ~At what price? Without
								an orderly society you would probably get killed in a war, or starve to death.
								You wouldn't get to live at all then! I'm not really interested in debating political
								philosophy with you, Dinnings. Do you have any other questions?~";
							}						
					}!end switch
						}! end for
		Show: if (noun == briefcase or bomb) {ending_two(); rtrue;}
  		Attack: "You don't have your gun.";
							],! end life
	daemon [; self.action_no++; 
			switch(overseer.action_no) {
				1:  if ((~~adam_wayne.has_trust) && (briefcase in player)) ending_three();
				2: 	move player_chair to overseer_room;
					"The Overseer smiles. ~Welcome, Agent Dinnings.~ His voice feels
					warm and soothing. ~I have been looking forward to this meeting.
					Please have a seat.~ He pushes a button, a trapdoor opens in the floor,
					and another chair appears, rising silently from below.";
				3: 	print "~As you have been told, Agent, I have taken an interest in this
						case. Intel suggests that the terrorists are planning a major
						strike against The Union. I was hoping that you would
						procure some more information in the process of your investigation.~^^";
					self.is_satisfied = true;
					StopDaemon(overseer);
					if (rebels_defeated) {
						print "~You did a fantastic job dealing with the
						terrorists in the warehouse. ";
						if (adam_wayne in inside_warehouse && warehouse_allyson in inside_warehouse){
							self.success = 1;
							"Drugging their tea was a brilliant move.
							The information they can give us will be invaluable.
							I am confident that we will be able to eliminate
							most of their organisation once they have been interrogated properly.
							You have done a great service to the citizens of The Union.
							I just wanted to congratulate you personally. If you have any 
							questions, just ask. Otherwise, feel free to return to Agency
							any time you want. We will work out the details of your promotion
							together.~";}
						if (adam_wayne in inside_warehouse){
							self.success = 2;
							"Drugging the tea was a brilliant move. The information
							this 'Adam' should be able to give us will most likely
							be invaluable. I am confident that we will be able to 
							deal with his organisation once he has been interrogated properly.
							He seems to be the leader, or at least a high ranking member.
							You have done a great service to the citizens of The Union.
							I just wanted to congratulate you personally. If you have any 
							questions, just ask. Otherwise, feel free to return to Agency
							any time you want. We will work out the details of your promotion
							together.~";}
						if (warehouse_allyson in inside_warehouse){
							self.success = 3;
							"It's a pity you couldn't take both of them alive, though.
							This 'Adam' character seems to have been the leader, or at least
							a high ranking member of their organisation. However,
							I am confident that Miss Stiles will also be able
							to provide most valuable information once she has been
							interrogated properly. I am confident that we will
							be able to deal with the threat posed by these terrorists.
							You have done a great service to the citizens of The Union.
							I just wanted to congratulate you personally. If you have any 
							questions, just ask. Otherwise, feel free to return to Agency
							any time you want. The details of your promotion will
							be worked out later.~";}
						self.success = 4;
						"It's a great pity that you couldn't take any of them alive, though.
						They would most likely have had been able to provide invaluable information.
						As it is, we don't even know which organisation they were working for.
						However, their deaths should be a major blow to whoever their leader was. This 'Adam' person seems to have been a high ranking
						member. The service you have done to The Union is highly
						appreciated. Make no mistake about that. I just wanted
						to congratulate you personally. If you have any questions, just ask.
						Otherwise, feel free to return to Agency any time you want.
						You will most likely be promoted in the near future.~";}! end rebels_defeated
					else 	print "~Hopefully you will not disappoint me. What are the preliminary
						results of your investigation?~^^
						Do you tell the truth?";
						if (YesOrNo()) {tellbomb(); rtrue;}
						else "You tell The Overseer about your investigation, carefully
							leaving out the events at the warehouse. Instead, you 
							pretend that you weren't able to get in at all.^^
							He looks disappointed. ~That's a pity. I was hoping
							you would have some more useful information for me. 
							I will have a tactical team check out the warehouse, but
							whoever was there is probably long gone now.
							Oh, well,
							you can't win every time I guess. Feel free to return
							to Agency any time you want, Agent. I am sure you have
							important work to do. I'm sorry that I have wasted
							your time.~";
					} ! end switch
				],
  has	animate proper;

[ tellbomb;
	if (briefcase in player) <<Show briefcase overseer>>;
	if (briefcase in overseer_hallway){
	StopDaemon(overseer);
	overseer.is_satisfied = true;
	overseer.has_told = true;
	!overseer.told_truth = true;
	remove briefcase;
	self.want_infiltrate = true;
	print "~Sir~, you say. ~I have managed to infiltrate the organisation
	of the terrorists. The briefcase in the hallway contains a bomb, which was
	supposed to be used for your assassination. I believe the briefcase might be bugged,
	so I couldn't tell anyone earlier.~^^
	The Overseer's eyes widen in surprise as he quickly pushes a button on
	his chair. ~Security, please remove the briefcase in my hallway immediately.
	And quietly, please.
	Also, call the bomb squad. It probably contains a powerful explosive
	of some kind.~^^
	There is a short pause. ~Well, Agent~, he continues. ~Aren't you just
	full of surprises? You certainly haven't been wasting your time.
	I would very much like to hear how you managed to accomplish this.~^^
	You briefly recount the events leading to your gaining the trust
	of the terrorists.^^
	He smiles broadly. ~That is excellent work, Agent Dinnings. I am happy 
	to see that The Agency is continuing its tradition of hiring only 
	the best and brightest. Hopefully we will be able to convince the terrorists
	that your failure to complete the mission was an honest mistake. You
	were tailed by security and had to dump the bomb, or something like that.
	We will work out your story together. Feel free to ask if you have
	any questions. Otherwise, you may return to Agency. I will brief your
	supervisor on the details of the situation.~^";} ! end else briefcase
];

Object screens "screens" overseer_room
  with	name 'screen' 'screens' 'video' 'tv' 'surveillance',
  	has_observed false,
  	description [choosescreen ; 
  				choosescreen = shuffle(8);
  				if (~~self.has_observed) {
  					print "The screens all show different scenes. 
  					You pick out one at random.^";
  					self.has_observed = true;}
  				switch(choosescreen) {
					1: "The screen shows a bird's eye view of a group of children
						on a sunny playground. A few of them seem to be playing tag, 
						while others are busy with the swings.";
					2: "A man in a huge overcoat is walking along a sidewalk,
						trying to shield himself from the heavy raindrops
						falling from the sky. Suddenly, he appears to change his mind.
						Pulling back his hood, he stands still for a moment, facing the sky,
						enjoying the rain on his face.";
					3: "Two elderly men are sitting by a table in a park, engrossed
						in a game of chess. The sky is cloudy. A light rain
						is starting to fall, but the two men are so focused on
						the game that they notice nothing else.";
					4: "Several people are eating at a charming outdoor restaurant. Waiters
						scurry back and forth carrying plates of food and drink. At one of
						the tables, a man has apparently just told a joke. Everyone
						laughs.";
					5: "Two young men are playing tennis at an outdoors court. One of them
						misses the ball, and they both shake hands in the middle. The game
						seems to be over.";
					6: "A bird's eye view of several young people swimming and playing
						in a large pool. Some of them are having fun with a large ball.
						Others are just relaxing by the side, enjoying the sun.";
					7: "The screen shows a large sunny beach. A group of people are playing beach
						volleyball. Others are just relaxing in the sun, or swimming in
						the azure ocean.";
					8: "A man is selling carpets from a booth at a marketplace. Some of them seem very nice.
						Once you get your paycheck, you should probably check out some of the marketplaces
						in The City.";
					}! end switch
  				],
  has	scenery;

Furniture player_chair "your chair"
  with	pname '.x' 'my' '.x' 'your' 'chair',
  	description "It looks very comfortable.",
  	before [; Receive: if (noun==briefcase) "You can't leave it here. The Overseer would get suspicious.";],
  has	enterable proper;

!---------------------------------------------------------------------------------!
!Major endings

[ ending_one;
	print "^
	You leave The Tower as fast as you can without drawing
	attention to yourself. Just as you turn the key in the ignition,
	the sound of an explosion reverberates across The City. The
	entire Tower shakes, debris falling to the ground below. A few people get crushed
	by falling bricks. As you pull away from The Tower, you see the remaining people
	break into a stampede, desperate to get away in time. You never find out how many
	of them made it.^^
	Several more explosions follow at various landmarks across The City.
	They didn't tell you anything about that. In the distance,
	the Statue of Authority crumbles and collapses, like a fragile sand
	sculpture.^^
	The next few months pass in a haze. With central authority gone, nobody
	can agree on who is in charge. With the actions of Adam's organisation
	to exacerbate the problems, The Union quickly collapses into a conglomerate
	of warring nationstates. Civil war engulfs the globe. The City, being the
	center of the old Union, is almost completely eradicated as several states
	attempt to claim it. You manage to make it out and get a job as a police officer
	in one of the new states. Life becomes a lot tougher for everyone. But 
	people are free to choose how they live it. Those who survive, anyway.
	And that is the most
	important thing.^^
	Or so you tell yourself.^^
	This is ending number one out of nine possible ones.";
	deadflag = 6; rtrue;
];

[ ending_two;
	print "^
	~Sir~, you say. 
	~I have managed to infiltrate the organisation of the terrorists.
	This briefcase actually contains a bomb, which....~^^
	The bomb explodes in your hand. Death is instantaneous.^^
	This is ending number two out of nine possible ones."; 
  	deadflag = 5; rtrue;
];

[ ending_three;
	print "^
	Unfortunately, this is the last thing you ever see.
  	Just as the door closes behind you, the bomb explodes in
  	your hand. Death is instantaneous. It would seem that
  	they didn't trust you after all...^^
  	This is ending number three out of nine possible ones.";
  	deadflag = 5; rtrue;
];

[ ending_four;
  	print "^
  	This is it. The time has come. You take a deep
  	breath, then push the button. Death is instantaneous.^^
  	This is ending number four out of nine possible ones.";
  	deadflag = 5; rtrue;
];

[ ending_five;
	print "^
		You return to Agency once again. The next few months are quite eventful.
		Using the information acquired from the interrogation of Adam and Allyson,
		The Agency systematically eliminates every cell of their organisation.^^
		Freed from the scourge of terrorism, The Union becomes even more
		perfect and prosperous. Of course, people don't actually get to choose
		how they live their perfect lives. It would be impossible to maintain
		order if everybody could just do whatever they want. But life in The Union
		is safe and prosperous. And that's the most important thing.^^
		Or so you tell yourself.^^
		As for you, you are handsomely rewarded for your loyal service. 
		You soon find yourself running an entire division of The Agency
		from your new luxurious carpeted office. Sometimes you miss
		the field work, but your huge paycheck more than makes up for it.^^
		This is ending number five out of nine possible ones.";
	deadflag = 6; rtrue;
];

[ ending_six;
	print "^
		You return to Agency once again. The next few months are quite
		eventful. Using the information acquired from the interrogation of Adam,
		The Agency manage to eliminate most of the cells in his organisation.
		A few manages to slip through the cracks, unfortunately. 
		Terrorism is hard to stamp out completely. There will always
		be those who despise peace and prosperity for their own
		inscrutable reasons. However, terrorist attacks become
		a rare and minor occurrence.^^
		The citizens
		of The Union can live safely and prosperously. Of course,
		people don't actually get to choose how they live their
		almost perfect lives. It would be impossible to run the state
		properly if everybody could just do whatever they liked.
		But life in The Union is peaceful and safe. And that's the
		most important thing.^^
		Or so you tell yourself.^^
		As for you, you are generously awarded for your loyal service.
		You are promoted to a position of authority in The Agency,
		where several agents answer to you. Your old office is replaced by a much larger one
		with a carpet and a comfortable office chair. Sometimes you miss
		the field work, but your large paycheck makes up for the lack of excitement.^^
		This is ending number six out of nine possible ones.";
		deadflag = 6; rtrue;
];

[ ending_seven;
	print "^
		You return to Agency once again. The next few months are quite eventful.
		Using the information acquired from the interrogation of Allyson,
		The Agency manages to stamp out a large number of the cells of which
		their organisation consists. The frequency of terrorist attacks
		drops dramatically.^^
		The Union becomes even more safe and prosperous.
		Of course, people don't actually have much choice in how to live
		their safe and prosperous lives. It would be impossible for the state
		to maintain control if everyone could just do whatever they liked.
		But life in The Union is peaceful and safe. And that's the most important thing.^^
		Or so you tell yourself.^^
		As for you, you are rewarded for your loyal service. Your old office
		is replaced by a much larger one, with a carpet and a comfortable chair.
		Other than that, life continues much as it always has.^^
		This is ending number seven out of nine possible ones.";
		deadflag = 6; rtrue;
];

[ ending_eight;
	print "^
		You return to The Agency once again. The next few months are quite uneventful.
		More experienced anti-terror agents are assigned to further
		investigate the terrorist organisation you uncovered. 
		No major terrorist attacks occur. Apparently the death of their leaders
		was enough to disrupt whatever plans they had.
		Your loyal service
		is rewarded with a new more comfortable office chair and a respectable raise. 
		Life in The Union continues much as always has. People live safely and
		peacefully, with only the occasional terrorist bombing to disturb
		their almost perfect lives. Of course, people don't actually get to choose how
		they live. But life is safe and prosperous. And that's the most important thing.^^
		Or so you tell yourself.^^
		This is ending number eight out of nine possible ones.";
	deadflag = 6; rtrue;
];

[ ending_nine;
	print "^
		The next few months are quite eventful. You manage to convince Adam that
		you were prevented from completing your mission by the meddling security
		forces in The Tower. Performing operations for him to earn his trust, you gradually
		penetrate deeper and deeper into the organisation. Unfortunately, it turns
		out to consist of several autonomous cells, making it impossible for 
		you to gather intel on more than a small fraction of the members. Finally, 
		when you have gathered as much information as possible, The Agency strikes.
		Adam and Allyson are arrested, along with every member of the cell to which
		you had been assigned. The rest of the organisation continues to operate,
		but is severely weakened by the operation. The frequency of terrorist attacks
		drop dramatically.^^
		The Union becomes even more safe and prosperous.
		Of course, people don't actually have much choice in how to live
		their safe and properous lives. It would be impossible for the state
		to maintain control if everyone could just do whatever they liked.
		But life in The Union is peaceful and safe. And that's the most important thing.^^
		Or so you tell yourself.^^
		As for you, you are a assassinated by a disgruntled terrorist seeking
		revenge for your betrayal.^^
		This is ending number nine out of nine possible ones.";
	deadflag = 6; rtrue;
];

!_-------------------------------------------------------------------------------!
Include "Grammar";

![ ScoreSub;
!  if (deadflag == 0)
!    "There is no score in this game.";
!];

[ ReadSub; <<Examine noun>>; ];

[ TalkSub; "If you want to talk to someone, you should use
	~ASK <PERSON> ABOUT <TOPIC>~ or ~TELL <PERSON> ABOUT <TOPIC>~.
	<TOPIC> can be more than one word if necessary. For example,
	~ASK JONES ABOUT HIS VIEWS CONCERNING THE RIGHTS OF THE INDIVIDUAL~
	is perfectly acceptable. The parser will simply search for keywords in
	the topic.^
	If only one person is present, ~A <TOPIC>~ and ~T <TOPIC>~ will 
	ask or tell him	about the topic. Asking about the same topic more
	than once will continue the conversation in that vein. Most people have
	more than one thing to say on a given topic. Also, the same question
	can produce different answers at different points in the conversation.^
	Questions can be answered simply by typing ~YES~ or ~NO~.^
	If you want to say something more specific, you can use
	~SAY <SOMETHING> TO PERSON~.";];

[ GunSub; 
	if (second==gun) <<Attack noun>>;
	else "Maybe you should try using your gun instead?";];
	
[ TSub; wn = consult_from; 
	if (supervisor in location) <<Tell supervisor NextWord()>>;
	if (prisoner in location)  <<Tell prisoner NextWord()>>;
	if (person in location) <<Tell person NextWord()>>;
	if (basement_people in location) <<Tell basement_people NextWord()>>;
	if (people in location) <<Tell people NextWord()>>;
	if (sister in location) <<Tell sister NextWord()>>;
	if (adam_wayne in location) <<Tell adam_wayne NextWord()>>;
	if (tower_guards in location) <<Tell tower_guards NextWord()>>;
	if (location == overseer_room) <<Tell overseer NextWord()>>;
	"There is no one around.";
	];

[ ASub; wn = consult_from; 
	if (supervisor in location) <<Ask supervisor NextWord()>>;
	if (prisoner in location)  <<Ask prisoner NextWord()>>;
	if (person in location) <<Ask person NextWord()>>;
	if (basement_people in location) <<Ask basement_people NextWord()>>;
	if (people in location) <<Ask people NextWord()>>;
	if (sister in location) <<Ask sister NextWord()>>;
	if (adam_wayne in location) <<Ask adam_wayne NextWord()>>;
	if (tower_guards in location) <<Ask tower_guards NextWord()>>;
	if (location == overseer_room) <<Ask overseer NextWord()>>;
	"There is no one around.";
	];
	
[ LandSub; <<Go d_obj>>; ];
	
[ HintSub; hints_menu.select(); ];

[ TracephoneSub; 
	if (act==3) "How do you intend to do that? You will have to be more specific.";
	"You don't have the necessary equipment.";];

[ DrawSub; if (noun has open) <<Close noun>>;
		else <<Open noun>>;];

[ CallSub; if (~~location == bedroom) "There is no phone here.";
		else "You have no reason to dial that number.";];

[ DriveSub; "You are not in any kind of vehicle.";];

[ BugSub; ];

[ HitSub; "Moderate physical pressure isn't the answer to this one.";];

[ TurnKeySub; if (car has on) <<SwitchOff noun>>; <<SwitchOn noun>>;];

[ xyzzySub; "You reminisce fondly about your time in secret agent school, when you
		would play Adventure all night long. Those were the days...";];
		
[TortureSub; "Due to budget cuts, torture is currently not allowed. You will 
		have to use more subtle methods.";];

[SetfreeSub; <<Drop noun>>;];

[FollowSub; "You can't follow that.";];

[WalkAroundSub; "You see no reason to walk around that.";];

[FlySub; if (act == 2) {
		if (wings in player) {
			if (noun == d_obj or u_obj or w_obj or e_obj or n_obj or s_obj)
			<<Go noun>>;
			else "You need to enter a compass direction to fly that way.";}
		else "You can no longer fly.";}
	else "How? By moving your arms up and down?";];
	
[ThrowSub; <<Drop noun>>;];

[KnockSub; print "You knock on ", (the) noun;
		".";];
		
[WriteSub; "You don't need to use this command. Important clues will automatically be recorded
		in your notebook.";];

[InvestigateSub; "Why don't you just type ~solve game~ while you are at it?";];

[SolveSub; "Sigh. You will have to be a bit more specific than that.";];

[ CreditsSub; "Writing and coding by: Simon Christiansen^^
		Testing done by: Katzy, Ramona, James Anderson and Stephan Schonberg.^^
		Library extensions used in the game:^
		Shuffle.h by Roger Firth.^
		Pname.h by Neil Cerutti.^
		Menus.h by Graham Nelson.^
		Smartcantgo.h by David Wagner.^^
		Proofreading for version 2.0 was done by a member of the IF community
		whose identity i have unfortunately forgotten. If you are the person in question
		don't hestitate to contact me so i can credit you properly.";];
		
[ HideSub; "Why would you want to hide that?";];

[ ThanksSub; "You don't believe in unnecessary chat.";];

[ EscapeSub; "You will have to more specific. How do you intend to escape?"; ];

[ UseSub; "You will have to be more specific. What exactly do you intend to do?";];

[ SighSub; "You sigh. Sometimes your work can be very frustrating.";];

[ DrugSub; "How are you gonna do that?";];

[ PartphraseSub; "You see no reason to say that.";];

[ PassphraseSub; "You see no reason to say that.";];

Extend 'read' first 
  * legible 		-> Read;
  
verb 'talk' 'interrogate' 'question'
  * noun		-> Talk
  * 'to' noun		-> Talk;
  
extend only 'torture' first
  * noun		-> Torture;

verb 'fire' 'shoot'
  * noun		-> Attack
  * 'at' noun		-> Attack
  * noun 'with' noun	-> Gun
  * 'at' noun 'with' noun -> Gun
  * noun 'at' noun 	-> Gun reverse;
  
verb 't'
  * topic		-> T;

verb 'a'		
  * topic		-> A;
  
extend 'ask'
 * 'about' topic	-> A;
 
extend 'tell'
 * 'about' topic	-> T;
  
verb 'hints' 'help' 'hint'
  *			-> Hint;
  
verb 'land'
  *			-> Land;
  
verb 'trace'
  * 'number'		-> Tracephone
  * 'phone' 'number'	-> Tracephone;
  
verb 'draw'
  * noun		-> Draw;
  
verb 'call' 'phone' 'dial'
  * topic		-> Call;
  
verb 'drive' 'ride'
  * 'to' topic		-> Drive
  * topic		-> Drive;
  
extend 'turn' first
 * noun			-> TurnKey;
 
verb 'start' 'stop'
 * noun			-> TurnKey;
 
verb 'bug'
 * topic		-> Bug;
 
extend only 'hit' first
 * noun			-> Hit
 * noun 'with' noun	-> Hit;
 
verb 'beat' 'slap' 'kick' 
 * noun			-> Hit
 * 'at' noun		-> Hit
 * 'on' noun		-> Hit
 * noun 'up'		-> Hit
 * 'up' noun		-> Hit;
 
 verb 'grab' 'seize'
  * noun		-> Take;
 
 verb 'xyzzy'
  * 			-> xyzzy;
  
verb 'release' 'let'
 * noun			-> Setfree
 * noun 'go'		-> Setfree;
 
verb 'follow' 'pursue' 'catch' 'chase'
 * noun			-> Follow;
 
extend 'walk'
 * 'around' noun	-> WalkAround;

verb 'fly'
 * noun			-> Fly;
 
extend only 'throw' first
 * noun			-> Throw;
 
verb 'ring'
 * noun			-> Push;
 
verb 'knock' 'pound'
 * 'on' noun		-> Knock
 * noun			-> Knock;
 
verb 'write' 'scribble'
 * 'in' noun		-> Write
 * 'on' noun		-> Write
 * noun			-> Write
 * topic		-> Write;
 
verb 'investigate'
 * topic		-> Investigate;
 
verb 'solve'
 * topic		-> Solve;
 
verb 'credit' 'credits'
 *			-> Credits;
 
verb 'hide' 'conceal'
 * noun			-> Hide
 * noun 'behind' noun	-> Hide;
 
extend only 'put' first
 * noun 'behind' noun	-> Hide;
 
verb 'thank' 'thanks'
 * noun			-> Thanks;
 
verb 'use' 'utilize'
 * noun			-> Use;
 
verb 'escape'
 *			-> Escape
 * topic		-> Escape;
 
verb 'sigh'
 *			-> Sigh;
 
verb 'drug' 'poison'
 * noun			-> Drug;
 
verb 'freedom' 'from' 'protection'
 *			-> Partphrase
 * 'from' 'protection'	-> Passphrase;
 
Extend 'attack'
 * noun 'with' noun	-> Attack;
!------------------------------------------------------------------------------!
